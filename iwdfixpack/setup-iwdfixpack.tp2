BACKUP ~iwdfixpack/backup~ // location to store files for
SUPPORT ~https://www.gibberlings3.net/forum/152-icewind-dale-mod-roundup/~ // email address displayed if install fails\

ALWAYS

  ACTION_IF NOT VARIABLE_IS_SET game_is_iwd THEN BEGIN // check to make this happen only once per install
    ACTION_IF GAME_IS iwd   BEGIN OUTER_SET game_is_iwd   = 1 END ELSE BEGIN OUTER_SET game_is_iwd   = 0 END
    ACTION_IF GAME_IS how   BEGIN OUTER_SET game_is_how   = 1 END ELSE BEGIN OUTER_SET game_is_how   = 0 END
    ACTION_IF GAME_IS totlm BEGIN OUTER_SET game_is_totlm = 1 END ELSE BEGIN OUTER_SET game_is_totlm = 0 END
  END
  
  ACTION_IF game_is_iwd BEGIN // iwd uses MAP_GROUP_X_HOSTILE, how/totlm uses MAP_GROUP_HOSTILEX
    OUTER_SPRINT prefix ~MAP_GROUP_~
    OUTER_SPRINT suffix ~_HOSTILE~
  END ELSE BEGIN
    OUTER_SPRINT prefix ~MAP_GROUP_HOSTILE~
    OUTER_SPRINT suffix ~~
  END
  OUTER_FOR (index = 1 ; index < 10 ; ++index) BEGIN
    OUTER_SPRINT var_name ~group_%index%_hostile~
    OUTER_SPRINT EVAL ~%var_name%~ ~%prefix%%index%%suffix%~
  END
  
END

VERSION ~v7~

README ~iwdfixpack/readme-iwdfixpack.html~

AUTO_TRA ~iwdfixpack/languages/%s~

LANGUAGE
  ~English~                                                     
  ~english~
  ~iwdfixpack/languages/english/setup.tra~
LANGUAGE
  ~Czech (Razfallow and VlasÃ¡k)~
  ~czech~   
  ~iwdfixpack/languages/english/setup.tra~
  ~iwdfixpack/languages/czech/setup.tra~
LANGUAGE
  ~Francais (Anomaly, Elgaern the Dragon Slayer, Isaya, elminster)~
  ~french~
  ~iwdfixpack/languages/english/setup.tra~
  ~iwdfixpack/languages/french/setup.tra~
LANGUAGE
  ~Deutsche (Leonardo Watson, Caswallon, jester, Mike)~
  ~german~
  ~iwdfixpack/languages/english/setup.tra~
  ~iwdfixpack/languages/german/setup.tra~
LANGUAGE
  ~Italiano (Andrea Colombo, Antonio Favata)~
  ~italian~
  ~iwdfixpack/languages/english/setup.tra~
  ~iwdfixpack/languages/italian/setup.tra~
LANGUAGE
  ~Polski (yarpen, Artanis, dragionian, Evendur, Grjgori, Neminus)~
  ~polish~
  ~iwdfixpack/languages/english/setup.tra~
  ~iwdfixpack/languages/polish/setup.tra~
LANGUAGE
  ~Russian (Kulyok, Creepin, Domi, Serdrick, aerie.ru)~
  ~russian~
  ~iwdfixpack/languages/english/setup.tra~
  ~iwdfixpack/languages/russian/setup.tra~
LANGUAGE
  ~Espanol (Clan DLAN)~
  ~spanish~ 
  ~iwdfixpack/languages/english/setup.tra~
  ~iwdfixpack/languages/spanish/setup.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Assorted Fixes                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @100
REQUIRE_PREDICATE GAME_IS ~iwd how totlm~ @113
LABEL ~cd_iwd_fixpack~
  
// patch functions
INCLUDE ~iwdfixpack/lib/functions.tpa~

/////                                                  \\\\\
///// misc file fixes                                  \\\\\
/////                                                  \\\\\

// limit to English until we get translation updates
ACTION_IF ("%LANGUAGE%" STRING_COMPARE_CASE "english" = 0) THEN BEGIN

  STRING_SET  3028 @103028 // zz05we: doesn't actually have bonus thac0 vs lawful
              6394 @106394 // DECASTAVE DRAINING
              8663 @108663 // clbhand: damage bonus +2 > +3 from being composite long bow
              8712 @108712 // kaybow: damage bonus +3 > +4 from being composite long bow
              8911 @108911 // lxbowbm: damage bonus +2 > +3 
              9764 @109764 // thac0/damage bonus for hammer
             12174 @112174 // four turns > rounds for chromatic orb
             12182 @112182 // vocalize only affects the caster
             14357 @114357 // uflal2a: 1d6+1 > 1d6+2 for base flail damage
             14959 @114959 // disease > phase for descript of War Hammer of Phasing +3
             17716 @117716 // zzh8gk: doesn't provide any thac0 bonus vs. giants; damage vs. giants is actually +5, not +4

  // description not even close to accurate
  COPY_EXISTING ~potn34.itm~ ~override~
    SAY 0x0c @115
    SAY 0x54 @116
    BUT_ONLY
    
  ACTION_IF !game_is_iwd BEGIN // how, totlm
  
    STRING_SET 21478 @121478 // cause disease is abjuration school but necromantic sphere--like its inverse, cure disease
    STRING_SET 22605 @122605 // for asking about wylfdene for the first time
    STRING_SET 24328 @124328 // Tongue of the Gloomfrost should do piercing, not slashing, damage
    STRING_SET 25818 @125818 // storm bow is usable by thieves; remove +1 damage since it's not a long bow
    
    ACTION_IF !game_is_how BEGIN // totlm
    
      STRING_SET 27373 @127373 // 1d6+4 > 1d6+5 for base flail damage
                 27377 @127377 // zz36dgd: +5 > +6 damage vs. good

    END

  END
  
END
  
COPY_EXISTING ~worldmap.wmp~ ~override~ // inconsistent travel times
  READ_LONG 0x0c mos_off
  READ_LONG (mos_off + 0x20) area_num
  READ_LONG (mos_off + 0x24) area_off
  READ_LONG (mos_off + 0x28) link_off
  FOR (index = 0 ; index < area_num ; ++index) BEGIN
    READ_ASCII (area_off + 0x08 + (index * 0xf0)) area
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar2100" = 0) BEGIN // kuldahar
      SET kuld = index
    END ELSE
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar6000" = 0) BEGIN // dorn's deep
      SET dorn = index
    END ELSE
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar5000" = 0) BEGIN // severed hand
      SET hand = index
    END ELSE
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar4000" = 0) BEGIN // dragon's eye
      SET deye = index
    END ELSE
    PATCH_IF ("%area%" STRING_COMPARE_CASE "ar7000" = 0) BEGIN // wyrm's tooth
      SET wyrm = index
    END
  END
  FOR (index = 0 ; index < area_num ; ++index) BEGIN // loop through areas
    FOR (index2 = 0 ; index2 < 4 ; ++index2) BEGIN // loop through four sets of links (n,s,e,w)
      READ_LONG (area_off + 0x50 + (index2 * 0x08) + (index * 0xf0)) link_idx
      READ_LONG (area_off + 0x54 + (index2 * 0x08) + (index * 0xf0)) link_num
      FOR (index3 = 0 ; index3 < link_num ; ++index3) BEGIN // loop through actual links
        READ_LONG (link_off +        ((link_idx + index3) * 0xd8)) target
        PATCH_IF (target = wyrm) BEGIN // if link to wyrm's tooth, assign entrance point
          WRITE_ASCII (link_off + 0x04 + ((link_idx + index3) * 0xd8)) ~FRWMap~ #32
        END ELSE
        PATCH_IF ((index = kuld) AND (target = dorn)) BEGIN // kuldahar to dorn's deep
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 14 // travel time
        END ELSE  
        PATCH_IF ((index = deye) AND (target = hand)) BEGIN // dragon's eye to severed hand
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 12 // travel time
        END ELSE  
        PATCH_IF ((index = wyrm) AND (target = hand)) BEGIN // wyrm's tooth to severed hand
          WRITE_LONG (link_off + 0x24 + ((link_idx + index3) * 0xd8)) 72 // travel time
        END   
      END
    END
  END
  BUT_ONLY

// in case 1pp has been installed before us, don't f*&^ up their paperdolls
ACTION_IF NOT MOD_IS_INSTALLED ~1pp/1pp.tp2~ ~101~ BEGIN

  ACTION_FOR_EACH pdoll IN chfc1inv chfc2inv chfc3inv chfc4inv chft1inv chft2inv chfw1inv chfw2inv chfw3inv chfw4inv BEGIN

    // paperdoll fixes for human female clerics, thieves, and mages
    COPY ~iwdfixpack/bam/%pdoll%.bam~ ~override~

  END

END

COPY ~iwdfixpack/wav/choke.wav~ ~override~ // missing sound for poison.itm

/////                                                  \\\\\
///// ids fixes                                        \\\\\
/////                                                  \\\\\

// bitch queen's envoy blocks *all* elementals, not just water 1/2; see also shldbch.itm and general bulk creature changes
// tribe and barbarian entries so that barbs can be properly raced as human, but scripting will still work
// find_traps is for find trap spell shenanigans
APPEND ~specific.ids~ ~114 WATER_ELEMENTAL
115 TRIBE_ELK
116 TRIBE_WYRM
117 BARBARIAN
118 FIND_TRAPS~

APPEND ~spell.ids~ ~1299 CLERIC_FIND_TRAPS_INTERNAL~ // find trap spell shenanigans

// new state needed for fixing Dead(object) triggers
APPEND ~state.ids~ ~0x00000fc0 STATE_REALLY_DEAD~ UNLESS ~0x00000fc0 STATE_REALLY_DEAD~

// fix to not reference non-existent alignment.ids
COPY_EXISTING ~trigger.ids~ ~override~
  REPLACE_TEXTUALLY EXACT_MATCH ~Alignment(O:Object*,I:Alignment*Alignment)~ ~Alignment(O:Object*,I:Alignment*Align)~
  BUT_ONLY

ACTION_IF !game_is_iwd THEN BEGIN // fixes for how & totlm

  // add totlm actions to how
  APPEND ~action.ids~ ~276 PlayBardSong(I:BardSong*bardsong)~           UNLESS ~^276~
  APPEND ~action.ids~ ~277 TurnAMT(I:Amount*)~                          UNLESS ~^277~
  APPEND ~action.ids~ ~278 DropInventoryEXExclude(S:ResRef*,O:Object*)~ UNLESS ~^278~

  // in case some mod overwrites this (looking at you, AB)
  APPEND ~class.ids~ ~202 MAGE_ALL~    UNLESS ~202 MAGE_ALL~
  APPEND ~class.ids~ ~203 FIGHTER_ALL~ UNLESS ~203 FIGHTER_ALL~
  APPEND ~class.ids~ ~204 CLERIC_ALL~  UNLESS ~204 CLERIC_ALL~
  APPEND ~class.ids~ ~205 THIEF_ALL~   UNLESS ~205 THIEF_ALL~
  APPEND ~class.ids~ ~206 BARD_ALL~    UNLESS ~206 BARD_ALL~
  APPEND ~class.ids~ ~207 PALADIN_ALL~ UNLESS ~207 PALADIN_ALL~
  APPEND ~class.ids~ ~208 DRUID_ALL~   UNLESS ~208 DRUID_ALL~
  APPEND ~class.ids~ ~209 RANGER_ALL~  UNLESS ~209 RANGER_ALL~

  // missing ids files
  ACTION_FOR_EACH file IN diffmode difflevl BEGIN
  
    ACTION_IF (NOT FILE_EXISTS_IN_GAME ~%file%.ids~) THEN BEGIN

      COPY ~iwdfixpack/ids/%file%.ids~ ~override~

    END

  END

  // fix how ids reference (not actually used)
  COPY_EXISTING ~spell.ids~ ~override~
    REPLACE_TEXTUALLY ~2423 CLERIC_WALL_OF_MOONLIGHT~ ~1423 CLERIC_WALL_OF_MOONLIGHT~
    BUT_ONLY

END

/////                                                  \\\\\
///// misc 2da fixes                                   \\\\\
/////                                                  \\\\\

// magic armor can be worn with magic jewelry/cloaks
ACTION_FOR_EACH item IN leat06 mourner ogien plat04 BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%item%.itm~ BEGIN

    APPEND ~itemexcl.2da~ ~%item%  1~ UNLESS ~^%item%~

  END

END

// should be 3 lore for 15 win/int
COPY_EXISTING ~lorebon.2da~ ~override~
  SET_2DA_ENTRY 15 1 2 ~3~
  BUT_ONLY_IF_IT_CHANGES

// add new entries for area music - see music section in area fixes
COPY_EXISTING ~music.2da~ ~override~   
  SET music3603 = 0
  REPLACE_EVALUATE ~^\([^ %TAB%]+\)~ BEGIN
    PATCH_IF ("%MATCH1%" STRING_COMPARE_CASE "NO_MUSIC" = 0) BEGIN SET music3603 = 0 END ELSE BEGIN SET music3603 += 1 END
  END ~%MATCH1%~
  BUT_ONLY

OUTER_SET music5103 = music3603 + 1
OUTER_SET music5104 = music3603 + 2

APPEND ~music.2da~ ~mx3603 mx3603.mus
mx5103 mx5103.mus
mx5104 mx5104.mus~

// remove bad references from rndtreas.2da
COPY_EXISTING ~rndtres.2da~ ~override~
  REPLACE_TEXTUALLY ~\bZZZ3BC\b~  ~ZZZ6BC~
  REPLACE_TEXTUALLY ~\bshswd\b~   ~~                // remove null reference...
  REPLACE_TEXTUALLY ~\bSH_sswd\b~ ~SH_sswd Shsswrd~ // ... and then add it back. this also adds it new to HoW, where bad ref was removed outright
  REPLACE_TEXTUALLY ~\bLongCle\b~ ~LongClev~
//  REPLACE_TEXTUALLY ~\bs1-10~     ~~ // how, totlm
  BUT_ONLY

// thieving penalties for low dex
COPY_EXISTING ~skilldex.2da~ ~override~
  INSERT_2DA_ROW 0 5 "1 -15 -10 -10 -20"
  INSERT_2DA_ROW 1 5 "2 -15 -10 -10 -20"
  INSERT_2DA_ROW 2 5 "3 -15 -10 -10 -20"
  INSERT_2DA_ROW 3 5 "4 -15 -10 -10 -20"
  INSERT_2DA_ROW 4 5 "5 -15 -10 -10 -20"
  INSERT_2DA_ROW 5 5 "6 -15 -10 -10 -20"
  INSERT_2DA_ROW 6 5 "7 -15 -10 -10 -20"
  INSERT_2DA_ROW 7 5 "8 -15 -10 -10 -20"

// second level ranger stealth score
COPY_EXISTING ~skillrng.2da~ ~override~
  SET_2DA_ENTRY 2 1 2 ~21~
  BUT_ONLY_IF_IT_CHANGES

// add tooltips for multi-ability items
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_tooltips BEGIN
  amaunat => "-1     2664 -1"
  auril   => "-1    12066 -1"
  beltbon => "21422 12113 -1"
  bloodgf => "-1    21422 -1"
  cynicis => "-1     8945 12131"
  fistgf  => "-1    21422 -1"
  gullwyn => "-1    22177 -1"
  handgf  => "-1    21422 -1"
  kissgf  => "-1    21422 -1"
  lucky   => "-1    12048 -1"
  power   => "-1     2938 -1"
  quost   => "-1    15211  2693"
  stafbes => "-1    12026 16963"
  storm   => "-1    21422 -1"
  talongf => "-1    21422 -1"
  tonggf  => "-1    21422 -1"
END

ACTION_PHP_EACH cd_tooltips AS item => string BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME "%item%.itm" BEGIN
  
    APPEND ~tooltip.2da~ ~%item% %string%~

  END

END

// remove throwing daggers since they don't have a melee abil
COPY_EXISTING ~tooltip.2da~ ~override~
  REPLACE_TEXTUALLY ~^DAGG05 +.+$~ ~moradin -1 27265 -1~

CLEAR_IDS_MAP

/////                                                  \\\\\
///// dialogue fixes                                   \\\\\
/////                                                  \\\\\

// fixes for all variants
COMPILE ~iwdfixpack/dlg/all-fixes.d~ EVALUATE_BUFFER

ACTION_IF game_is_iwd THEN BEGIN // fixes for iwd without how & totlm

  COMPILE ~iwdfixpack/dlg/base-fixes.d~
          ~iwdfixpack/dlg/valesti.d~ 

END ELSE BEGIN // fixes for how & totlm games

  COMPILE ~iwdfixpack/dlg/how-fixes.d~

  ACTION_IF game_is_totlm THEN BEGIN // totlm fixes

    COMPILE ~iwdfixpack/dlg/totlm-fixes.d~
    
    ACTION_IF FILE_EXISTS ~iwdfixpack/languages/%LANGUAGE%/dplanar.tra~ BEGIN
    
      COMPILE ~iwdfixpack/dlg/dplanar.d~ USING ~iwdfixpack/languages/%LANGUAGE%/dplanar.tra~ // contact other plane

    END      

  END

END

/////                                                  \\\\\
///// scripting fixes                                  \\\\\
/////                                                  \\\\\

// corrections to player scripts - have to go through scrpdesc as they apparently have different
// file names in non-English versions of IWD
COPY_EXISTING ~scrpdesc.2da~ ~override~
  COUNT_2DA_ROWS ~3~ "rows"
  FOR (index = 1 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY "%index%" 1 3 "title"
    PATCH_IF (("%title%" = 17598) OR     // mage1
              ("%title%" = 17600) OR     // mage2
              ("%title%" = 17604) OR     // mage4
              ("%title%" = 17606)) BEGIN // cleric1
      READ_2DA_ENTRY "%index%" 0 3 "file"
      PATCH_IF ("%title%" = 17606) BEGIN
        INNER_ACTION BEGIN

          // cleric1.bs - used to be a magic stone reference; just false it
          COPY_EXISTING ~scripts/%file%.bs~ ~scripts~
            DECOMPILE_AND_PATCH BEGIN
              REPLACE_TEXTUALLY ~HaveSpell(0)~                    ~HaveSpell(CLERIC_MAGICAL_STONE)~
              REPLACE_TEXTUALLY ~Spell(NearestEnemyOf(Myself),0)~ ~Spell(NearestEnemyOf(Myself),CLERIC_MAGICAL_STONE)~
            END
            BUT_ONLY IF_EXISTS

        END
      END ELSE BEGIN
        INNER_ACTION BEGIN

          // missing spell ref; confirmed from BG
          COPY_EXISTING ~scripts/%file%.bs~ ~scripts~
            DECOMPILE_AND_PATCH BEGIN
              REPLACE_TEXTUALLY ~HaveSpell(0)~                    ~HaveSpell(WIZARD_LARLOCHS_MINOR_DRAIN)~
              REPLACE_TEXTUALLY ~Spell(NearestEnemyOf(Myself),0)~ ~Spell(NearestEnemyOf(Myself),WIZARD_LARLOCHS_MINOR_DRAIN)~
            END
            BUT_ONLY IF_EXISTS

        END
      END
    END
  END
  BUT_ONLY

// broken dead(object) triggers
ACTION_FOR_EACH file IN 2004chef 2100mirk 4001lk 4001ls1 4001v9 4003blst 4003udlt bamebd bcatkgob bcatkskl bchjol0 bchjol0
  bcjorn cajshah1 cajshah3 cajshah4 cajshah5 d2talon3 d2talon3 d2talon4 d2talon4 d4alb d4cleric d4fight1 d4hirit d4hisum
  d5girl d5hibap d5hitort d5yxung duhero3 duhero5 dummtalk eeeverar eeevetpl eepomsen efbstskl efdlgc efdlgcc efdlgf efdlgfc
  efdlgm efdlgmc efdlgpc efdlgpcc efdlgpf efdlgpfc efdlgpm efdlgpmc eftrolg eftwnchk ehaccali ehdamien eheverar ehhero1
  ehhroth idpbsprt iljorn iljornhl ilsahep1 ilsahhp1 ilsahp1 ilsahp2 ilsahup1 ilxactil keraksha kphermit ktmytos kuarund
  kuarund1 kuarundd ldbeorn lddrowcm ldfeng2 ldfgntg ldguell2 ldhark ldilmad ldmalvon ldmarkth ldperd ldpoque1 ldrebsal
  ldseth ldshik ldsimmal ldtarnel lwemmrh lwhjolo lwquin lwshar1 lwshar2 lwshar3 lwwarew nelurmst nwlurmst scseer secrie13
  secrie2 selurmst shelfc1 shelfc2 shelfcs1 shelfps1 shorcsh1 shsevhl1 shshdsd1 shshdwz1 shshdwz2 shshdwz3 shshthl1
  swlurmst tgacold tgverbd udnorlin udorogse vslysan vstherik wtguard wtkontik
BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~%file%.bcs~) THEN BEGIN

    COPY_EXISTING ~%file%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~Dead("[Yy][Ss][Ee][Ll][Ff])")~ ~StateCheck(Myself,STATE_REALLY_DEAD)~
        REPLACE_TEXTUALLY ~Dead("astSeenBy(Myself))")~    ~StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)~
        REPLACE_TEXTUALLY ~Dead("0\.0\.\([A-Z]+\)\])")~   ~StateCheck([0.0.\1],STATE_REALLY_DEAD)~
        REPLACE_TEXTUALLY ~Dead("layer\([1-6]\))")~       ~StateCheck(Player\1,STATE_REALLY_DEAD)~
        REPLACE_TEXTUALLY ~Dead("ENEMY\])")~              ~StateCheck([ENEMY],STATE_REALLY_DEAD)~
      END
      BUT_ONLY
  
  END

END

// corrupted scripts
ACTION_FOR_EACH file IN 4001v8 5001v1 ar1102 ar2006 BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~%file%.bcs~) THEN BEGIN
  
    COPY_EXISTING ~%file%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN END
  
  END

END

// items from random treasure table with differing charges need to be re-added with charges
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_rndtres_charges BEGIN
  wandtrp, 5, "Uligar"           => ar2004 // orctres
  arowtrn, 8, "Pileo'Stuff"      => ar4003 // de3tres
  trnbolt, 8, "Pileo'Stuff"      => ar4003 // de3tres
  boneam,  5, "SerratedSkeleton" => ar5003 // sh1tres
END

ACTION_PHP_EACH cd_rndtres_charges AS params => area BEGIN

  EXTEND_BOTTOM ~%area%.bcs~ ~iwdfixpack/baf/rndtres_charges.baf~ EVALUATE_BUFFER
  
END

// prevent minions from attacking king
COPY_EXISTING ~4001lmtr.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~AttackReevaluate(LastSeenBy(Myself),75)~ ~AttackReevaluate(NearestEnemyOf(Myself),75)~
  END
  BUT_ONLY

// villagers not leaving after liz king dead
EXTEND_TOP ~4001vil.bcs~ ~iwdfixpack/baf/4001vil.baf~

// cast spell if available
COPY_EXISTING ~5001ls1.bcs~ ~override~
              ~5001ls2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(0)~ ~HaveSpell(CLERIC_HOLD_PERSON)~
    REPLACE_TEXTUALLY ~Spell(\[ENEMY\.0\.0\.\([A-Z_]+\)\],0)~ ~Spell([ENEMY.0.0.\1],CLERIC_HOLD_PERSON)~
  END
  BUT_ONLY

// keep egenia safe
COPY_EXISTING ~ar4001.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("EGANIA_MOVE","GLOBAL",0)~ ~False()~
  END
  BUT_ONLY

// bandoth fixes
COPY_EXISTING ~ar6003.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("BANDOTH_QUEST","GLOBAL",2)~ ~SetGlobal("BANDOTH_QUEST","GLOBAL",3)~
    REPLACE_TEXTUALLY ~PartyHasItem("KALABAC")~ ~PartyHasItem("KALABAC") Global("BANDOTH_QUEST","GLOBAL",0)~
  END
  BUT_ONLY

// stop cockblocking the script - no or() without how
COPY_EXISTING ~ar6010.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    PATCH_IF game_is_iwd BEGIN
      REPLACE_TEXTUALLY ~True()~ ~False()~
      APPEND_FILE ~iwdfixpack/baf/ar6010.baf~
    END ELSE BEGIN
      REPLACE_TEXTUALLY ~True()~
        ~OR(3) !Global("AR6010_PLAY_SOUND","GLOBAL",0) !Global("PUZZLE_B_DISABLED","GLOBAL",0) !Global("PUZZLE_A_DISABLED","GLOBAL",0)~
    END
  END
  BUT_ONLY

// turns group 2 (fire giants) hostile if ilmadia is killed before sounding the alarm  
EXTEND_BOTTOM ~ar8011.bcs~ ~iwdfixpack/baf/ar8011.baf~ EVALUATE_BUFFER

// correct DV references
COPY_EXISTING ~d2talon3.bcs~ ~override~
              ~d2talon4.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~D2TALON1~ ~TalonitePriest1~ 
    REPLACE_TEXTUALLY ~D2TALON2~ ~TalonitePriestess2~ 
    REPLACE_TEXTUALLY ~D2TALON3~ ~TalonitePriestess3~ 
    REPLACE_TEXTUALLY ~D2TALON4~ ~TalonitePriest4~ 
  END
  BUT_ONLY  

// extra check to keep marchon and party together, enemy-ally-wise
EXTEND_TOP ~d4maratk.bcs~ ~iwdfixpack/baf/d4maratk.baf~

// false out actions to move to nonexistant resources
COPY_EXISTING ~d5hibap.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\([ %TAB%]Global("BAPTISM_PHASE","LOCALS",[0-9]+)\)~ ~False() \1~ // 
  END
  BUT_ONLY   

// yxunomei's priests not doing anything 1/3 (see also d5yuanp3.bcs, yuantip.cre)
COPY_EXISTING ~d5yuanp2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(!Global("YXUN_ATTACK","MYAREA",0)[ %TAB%%MNL%%LNL%%WNL%]+THEN[ %TAB%%MNL%%LNL%%WNL%]+RESPONSE #100[ %TAB%%MNL%%LNL%%WNL%]+\)Spell(LastSeenBy(Myself),CLERIC_DISPEL_MAGIC)~
                      ~Global("CDDispelOnce","LOCALS",0) \1 SetGlobal("CDDispelOnce","LOCALS",1) SpellNoDec(LastSeenBy(Myself),CLERIC_DISPEL_MAGIC)~

  END
  BUT_ONLY

// yxunomei's priests not doing anything 2/3 (see also d5yuanp2.bcs, yuantip.cre)
COPY_EXISTING ~d5yuanp3.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(!Global("YXUN_ATTACK","MYAREA",0)[ %TAB%%MNL%%LNL%%WNL%]+THEN[ %TAB%%MNL%%LNL%%WNL%]+RESPONSE #100[ %TAB%%MNL%%LNL%%WNL%]+\)Spell("YXUNOMEI",\([^)]+\))~
                      ~HaveSpell(\2) Global("CD\2","LOCALS",0) \1 SetGlobal("CD\2","LOCALS",1) Spell(Myself,\2)~

  END
  BUT_ONLY

// yxonemei casting cloudkill on herself
COPY_EXISTING ~d5yxun.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Spell(Myself,WIZARD_CLOUDKILL)~ ~Spell(NearestEnemyOf(Myself),WIZARD_CLOUDKILL)~
  END
  BUT_ONLY

// broken DV reference in a trap for the Snowdrift Inn
COPY_EXISTING ~ehdwfbox.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HideCreature("EHBEDD1",TRUE)~ ~HideCreature("EHINVDWF",TRUE)~
  END
  BUT_ONLY

// bel's iron golems go hostile
COPY_EXISTING ~eepoqcng.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(ForceSpell(Myself,INNATE_FLASH_DESTROY_SUMMON_BELHIFET)\)~
                      ~\1 ActionOverride("Iron_Golem_1",Enemy()) ActionOverride("Iron_Golem_2",Enemy())~
  END
  BUT_ONLY
  
// ghoul touch should match its description: see spwi218.spl, ghoult.itm, and scrl1c.itm
COPY_EXISTING ~kuorrick.bcs~ ~override~ // adjust scripts using ghoul touch to target caster instead
              ~shshdwz1.bcs~ ~override~
              ~wtkontik.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Spell(LastSeenBy(Myself),WIZARD_GHOUL_TOUCH)~ ~Spell(Myself,WIZARD_GHOUL_TOUCH)~ // kuorrick uses forcespell, but match still works
  END IF_EXISTS

// townperson can now open doors
COPY_EXISTING ~kutown2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OpenDoor("1811\.0863")~ ~OpenDoor("AR2100Door7")~
    REPLACE_TEXTUALLY ~OpenDoor("1155\.0574")~ ~OpenDoor("AR2100Door2")~
  END
  BUT_ONLY

// blind & unconscous gnomes should leave after malavon's death
COPY_EXISTING ~lddgnom3.bcs~ ~override~
              ~lddgnom4.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("MALVON_DEAD","GLOBAL",0)~ ~Global("MALAVON_DEAD","GLOBAL",0)~
  END
  BUT_ONLY

// beorn quest fix; see ar8005.bcs
// clear out guello once party re-enters shikata's area to prevent dupe guellos (use same destroy as other gnomes in area)
EXTEND_TOP    ~ldguell2.bcs~ ~iwdfixpack/baf/ldguell2.baf~

// llew purges materials for item upgrades  
COPY_EXISTING ~ldllew.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    APPEND_FILE ~iwdfixpack/baf/ldllew.baf~
    REPLACE_TEXTUALLY ~CDITEM~ ~HideUmb~
    APPEND_FILE ~iwdfixpack/baf/ldllew.baf~
    REPLACE_TEXTUALLY ~CDITEM~ ~HideBee~
  END

// marketh now uses invisibility potion
COPY_EXISTING ~ldmarkth.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~("YSELF,?","POTN10")~ ~("POTN10",Myself)~
  END
  BUT_ONLY

// nym purges materials for item upgrades
COPY_EXISTING ~ldnym.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    APPEND_FILE ~iwdfixpack/baf/ldllew.baf~
    REPLACE_TEXTUALLY ~CDITEM~ ~beetshld~
  END

// make floor traps more consistent
COPY_EXISTING ~ldtrap1.bcs~ ~override~
              ~ldtrap2.bcs~ ~override~
              ~uddsrma.bcs~ ~override~
              ~uddsrmb.bcs~ ~override~ 
              ~uddsrmc.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Entered(~ ~IsOverMe(~
  END  
  BUT_ONLY

// severed hand folks healing party
COPY_EXISTING ~shelfc1.bcs~  ~override~
              ~shelfc2.bcs~  ~override~
              ~shorcsh1.bcs~ ~override~
              ~shshdsd1.bcs~ ~override~
              ~shshdwz1.bcs~ ~override~
              ~shshdwz2.bcs~ ~override~
              ~shshdwz3.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(See([A-Za-z]*NearestEnemyOf(NearestEnemyOf(Myself)))\)~ ~\1 Allegiance(LastSeenBy(Myself),ENEMY)~
    REPLACE_TEXTUALLY ~See(\[ENEMY\])~ ~False()~
  END
  BUT_ONLY 

// shadowed clerics cannot use healing spells since they get blocked; can't get anything else to work :/
COPY_EXISTING ~shelfcs1.bcs~ ~override~ // shadowed elven cleric
              ~shelfps1.bcs~ ~override~ // shadowed elven priest
              ~shorcsh1.bcs~ ~override~ // shadowed orc shaman
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HaveSpell(CLERIC_CURE_\(LIGHT\|MODERATE\)_WOUNDS)~ ~False()~
  END
  BUT_ONLY  
  
// disable lich phylactery regions once destroyed
COPY_EXISTING ~udtrap3.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Wait(2)\)~ ~\1 TriggerActivation(Myself,FALSE)~
  END
  BUT_ONLY  

ACTION_IF game_is_iwd THEN BEGIN // fixes for iwd without how & totlm

  // lizardmen not going neutral
  COPY_EXISTING ~4001lkl.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~ChangeEnemyAlly(Myself,0)~ ~ChangeEnemyAlly(Myself,NEUTRAL)~
    END
    BUT_ONLY

  // get aspel's wuff to leave peacably if charmed, just like HoW games
  EXTEND_BOTTOM ~ar1010.bcs~ ~iwdfixpack/baf/ar1010.baf~
  
  // ldd guard tower shouldn't shoot invisible creatures; no OR() trigger in base IWD
  OUTER_SPRINT trigger ~IsOverMe([PC])~ OUTER_SPRINT stringdisplay ~DisplayString(Myself,~
  COMPILE ~iwdfixpack/baf/ldbat.baf~ EVALUATE_BUFFER
  OUTER_SPRINT trigger ~IsOverMe([0.0.SVIRFNEBLIN])~
  EXTEND_BOTTOM ~ldbat.bcs~ ~iwdfixpack/baf/ldbat.baf~ EVALUATE_BUFFER
  
END ELSE BEGIN // how, totlm fixes

  // more robust opening cutscene (not needed for IWD w/o HoW)
  COPY_EXISTING ~ar1006.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~IncrementChapter("CHPTXT1")[ %TAB%%LNL%%MNL%%WNL%]+MultiPlayerSync()[ %TAB%%LNL%%MNL%%WNL%]+StartCutSceneMode()[ %TAB%%LNL%%MNL%%WNL%]+ClearAllActions()[ %TAB%%LNL%%MNL%%WNL%]+MultiPlayerSync()[ %TAB%%LNL%%MNL%%WNL%]+StartCutScene("EHHROTHT")~
        ~IncrementChapter("CHPTXT1")~
    END
    BUT_ONLY
  EXTEND_TOP ~ar1006.bcs~ ~iwdfixpack/baf/ar1006.baf~

  // barbarians now use SPECIFIC for intra-clan fight instead of assigning bogus race values
  COPY_EXISTING ~bcatkgob.bcs~ ~override~
                ~bcatkskl.bcs~ ~override~
                ~bcfolskl.bcs~ ~override~
                ~bcsklfgt.bcs~ ~override~
                ~bctntfc3.bcs~ ~override~
                ~ilatkmel.bcs~ ~override~
                ~ilentgol.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\[0\.0\.GOBLIN]~   ~[0.0.0.0.TRIBE_WYRM]~
      REPLACE_TEXTUALLY ~\[0\.0\.SKELETON]~ ~[0.0.0.0.TRIBE_ELK]~
      REPLACE_TEXTUALLY ~\[0\.0\.BARBARIAN]~ ~[0.0.0.0.BARBARIAN]~
    END
    BUT_ONLY

  // high torturer not doing anything 1/2 (see hightort.cre) (HoW removes proper HaveSpell checks)
  COPY_EXISTING ~d5hitort.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\(THEN[ %TAB%%MNL%%LNL%%WNL%]+RESPONSE #100[ %TAB%%MNL%%LNL%%WNL%]+Spell(LastSeenBy(Myself),CLERIC_SYMBOL_OF_PAIN)\)~
                        ~HaveSpell(CLERIC_SYMBOL_OF_PAIN) \1~

    END
    BUT_ONLY

  // bad changescript reference
  COPY_EXISTING ~efatksa.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~"EFATKA"~ ~"EFATKC"~
    END
    BUT_ONLY

  // using movetoobject with coordinates
  COPY_EXISTING ~ilgemeff.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~MoveViewObject(~ ~MoveViewPoint(~
    END
    BUT_ONLY
 
  // correct DV reference
  COPY_EXISTING ~iljorn.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~BARBARIAN_0~ ~BARBARIAN_8~ 
    END
    BUT_ONLY  
  
  // ldd guard tower shouldn't shoot invisible creatures; no OR() trigger in base IWD
  OUTER_SPRINT trigger ~OR(2) IsOverMe([0.0.SVIRFNEBLIN]) IsOverMe([PC])~ OUTER_SPRINT stringdisplay ~TomsStringDisplayer(~
  COMPILE ~iwdfixpack/baf/ldbat.baf~ EVALUATE_BUFFER

  // broken variable name
  COPY_EXISTING ~lddgnom4.bcs~ ~override~
                ~ldilair.bcs~  ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("Y_TEAM_HOSTILE","LOCALM",0)~ ~Global("MY_TEAM_HOSTILE","LOCALS",0)~
    END
    BUT_ONLY

  // wrong group ID
  COPY_EXISTING ~ldseth.bcs~  ~override~
                ~ldthief.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~"MAP_GROUP_HOSTILE2"~ ~"MAP_GROUP_HOSTILE10"~
    END
    BUT_ONLY

  // bad changescript reference
  COPY_EXISTING ~lwalpw1.bcs~ ~override~
                ~lwalpw2.bcs~ ~override~
                ~lwalpw3.bcs~ ~override~
                ~lwalpwg.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~"LWAALPW\([0-9]\)"~ ~"LWALPW\1"~
    END
    BUT_ONLY
  
  // bad dv reference
  COPY_EXISTING ~lwkieran.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~See("LWMURD")~ ~See("Murdaugh")~
    END
    BUT_ONLY
  
  // stop dead purvis from continually trying to hide
  COPY_EXISTING ~lwprvatk.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\(Allegiance(Myself,ENEMY)[ %TAB%%LNL%%MNL%%WNL]+!StateCheck(Myself,STATE_INVISIBLE)[ %TAB%%LNL%%MNL%%WNL]+!LOS([PC],25)\)~
        ~StateCheck(Myself,STATE_REALLY_DEAD) \1~
    END
    BUT_ONLY

  // broken DV check in cutscene
  COPY_EXISTING ~lwprvfce.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~"PURVIS5"~ ~"PURVIS_5"~
    END
    BUT_ONLY  
    
  // prevent multiple rofas from Purvis
  OUTER_FOR (index = 1 ; index < 8 ; ++index) BEGIN
    EXTEND_BOTTOM ~lwpurv%index%.bcs~ ~iwdfixpack/baf/purvis.baf~
  END

  // broken variable check
  COPY_EXISTING ~lwpurv6.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~SetGlobal("W_MURDER","THURLO",0)~ ~SetGlobal("THURLOW_MURDER","GLOBAL",0)~
    END
    BUT_ONLY

  ACTION_IF game_is_totlm THEN BEGIN

    // beorn quest fix; see ar8005.bcs, ldguell2.bcs
    // set b_q to 3 once you leave shikata's area, like other two exit areas
    EXTEND_BOTTOM ~ar8005.bcs~   ~iwdfixpack/baf/ar8005.baf~

    // totlm portal in jackal cavern is flaky; see also cahideo.bcs
    COPY_EXISTING ~cahided.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~SetGlobal("OPENED_HIDE","MYAREA",1)[%TAB% %LNL%%MNL%%WNL%]+OpenDoor(Myself)~ ~~
      END
      BUT_ONLY

    // totlm portal in jackal cavern is flaky; see also cahided.bcs
    COPY_EXISTING ~cahideo.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~CutSceneId(Myself)~ ~CutSceneId(Player1)~
        REPLACE_TEXTUALLY ~EndCutSceneMode()~ ~SetGlobal("OPENED_HIDE","MYAREA",1) OpenDoor("AR9800_Hide1") EndCutSceneMode()~
      END
      BUT_ONLY

    // correcting DV references
    COPY_EXISTING ~cajshah5.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~JACKAL_11~ ~Greater_Jackal_1~ // replace non-existant JACKAL_11
        // a little harder, we dupe every jackal_12 block into Greater_Jackal_2 and Greater_Jackal_3 blocks
        REPLACE_TEXTUALLY ~\(IF[ %TAB%%LNL%%MNL%%WNL%]+HaveSpell([A-Z_]+)[ %TAB%%LNL%%MNL%%WNL%]+See("\)JACKAL_12\(")[ %TAB%%LNL%%MNL%%WNL%]+!Dead("\)JACKAL_12\(")[ %TAB%%LNL%%MNL%%WNL%]+HPPercentLT("\)JACKAL_12\(",60)[ %TAB%%LNL%%MNL%%WNL%]+THEN[ %TAB%%LNL%%MNL%%WNL%]+RESPONSE #100[ %TAB%%LNL%%MNL%%WNL%]+Spell("\)JACKAL_12\(",[A-Z_]+)[ %TAB%%LNL%%MNL%%WNL%]+Wait(3)[ %TAB%%LNL%%MNL%%WNL%]+Continue()[ %TAB%%LNL%%MNL%%WNL%]+END\)~
          ~\1Greater_Jackal_2\2Greater_Jackal_2\3Greater_Jackal_2\4Greater_Jackal_2\5
          \1Greater_Jackal_3\2Greater_Jackal_3\3Greater_Jackal_3\4Greater_Jackal_3\5~
      END
      BUT_ONLY
      
    // no teleport for dead creatures
    COPY_EXISTING ~cpcttele.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\([ %TAB%]!?Global("CRYPT_THING_TELEPORT","LOCALS",0)\)~ ~\1 !StateCheck(Myself,STATE_REALLY_DEAD)~
      END
      BUT_ONLY

    // double dispel magic messages due to (useless) self-cast
    COPY_EXISTING ~efbgaze.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~ReallyForceSpell(Myself,INNATE_BEHOLDER_GAZE)~ ~~
      END
      BUT_ONLY

    // revenant regen item fix (see ar9714.are, revnant.cre, rndrevn.cre x2)
    COPY_EXISTING ~eftrolg.bcs~ ~override/cdtrolg.bcs~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~REG1HP2~ ~CIREVE~
      END
      BUT_ONLY

    // fail-safe: move enemy() to end of cutscene to prevent player scripts kicking in and attacking hobart in cutscene
    COPY_EXISTING ~kerakrev.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~Enemy()~ ~~
        REPLACE_TEXTUALLY ~\(EndCutSceneMode()\)~ ~Enemy() \1~
      END
      BUT_ONLY

    // bump master_quest to 7 to signal full totlm completion
    COPY_EXISTING ~kertrnlw.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(TakePartyItemAll("RINGLUR")\)~ ~SetGlobal("Master_Quest","GLOBAL",7) \1~
      END
      BUT_ONLY

    // broken dv reference
    COPY_EXISTING ~secrie13.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~"SHELHAI\."~ ~"SHELHAI"~
      END
      BUT_ONLY
  
    // perseverance test; monsters don't spawn fast enough so lm goes directly into 'way to go' dialogue
    COPY_EXISTING ~selurmst.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(CreatureHidden(Myself)\)~ ~\1 !Global("CYSHAMB_DEAD","GLOBAL",0)~
      END
      BUT_ONLY
  
  END

END

/////                                                  \\\\\
///// area music fixes                                 \\\\\
/////                                                  \\\\\

// music.2da patched in music secion
// adds 5103, 5104 music tracks; mx3603 is already present
MKDIR ~music/mx5103~
MKDIR ~music/mx5104~
COPY ~iwdfixpack/music/mx5103.mus~  ~music~ 
     ~iwdfixpack/music/mx5104.mus~  ~music~
     ~iwdfixpack/music/mx5103a.acm~ ~music/mx5103~
     ~iwdfixpack/music/mx5104a.acm~ ~music/mx5104~

// areas with day music now have night music
ACTION_FOR_EACH area IN 
  1001 1004 1100 1201 2112 3000 3501 3503 3600 3601 4001 4005 5000 5001 6000 6001 6002 
  6003 6005 7000 7001 7004 8001 8004 8009 8012 9103 9201 9300 9400 9500 9501 9502 9603
BEGIN

  COPY_EXISTING ~ar%area%.are~ ~override~
    READ_LONG 0xbc song_off
    READ_LONG  (song_off + 0x00) day // copy day song...
    WRITE_LONG (song_off + 0x04) day // to night
    BUT_ONLY IF_EXISTS
  
END

// areas getting new and/or unused tracks
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_new_music BEGIN
  ar2116 => 11 
  ar3603 => ~%music3603%~ 
  ar5103 => ~%music5103%~
  ar5104 => ~%music5104%~
  ar8005 => 33
END

ACTION_PHP_EACH cd_new_music AS area => music BEGIN

  COPY_EXISTING ~%area%.are~ ~override~
    READ_LONG 0xbc song_off
    WRITE_LONG (song_off + 0x00) music
    WRITE_LONG (song_off + 0x04) music
    BUT_ONLY 

END

// add combat music for iwd finale
COPY_EXISTING ~ar1105.are~ ~override~
  READ_LONG 0xbc song_off
  WRITE_LONG (song_off + 0x08) 40
  WRITE_LONG (song_off + 0x0c) 40
  WRITE_LONG (song_off + 0x10) 40
  BUT_ONLY 

/////                                                  \\\\\
///// area fixes                                       \\\\\
/////                                                  \\\\\

// spawning elementals trapped in narrow tunnels
COPY_EXISTING ~ar8013.ini~ ~override~
  REPLACE_TEXTUALLY ~^\(spawn_point[ %TAB%]*=[ %TAB%]*\[\)0530\.0360:0\]~ ~\1625.506:0]~
  BUT_ONLY

// match actor animation information to its creature file
INCLUDE ~iwdfixpack/lib/area_anims.tpa~

// areas with existing area scripts, but not assigned
ACTION_FOR_EACH file IN ar1101 ar1102 ar1104 ar1106 ar1109 ar1200 ar1201 ar2001 ar2002 ar2003 ar2005
  ar2006 ar3001 ar3101 ar3201 ar3301 ar3401 ar3501 ar3502 ar3503 ar3600 ar3601 ar3602 ar3603 ar4002
  ar4004 ar5001 ar5002 ar5003 ar5102 ar5201 ar5202 ar5203 ar5204 ar5301 ar5302 ar5303 ar5401 ar5402
  ar5403 ar5404 ar5502 ar6001 ar6007 ar6009 ar6011 ar6014 ar7000 ar7002 ar8002 ar8014 ar8015
  ar8016 ar9103 ar9301 ar9501 ar9601 ar9800 ar9801
BEGIN

  ACTION_IF ((FILE_EXISTS_IN_GAME ~%file%.are~) AND (FILE_EXISTS_IN_GAME ~%file%.bcs~)) THEN BEGIN

    COPY_EXISTING ~%file%.are~ ~override~
      WRITE_ASCIIE 0x94 ~%SOURCE_RES%~ #8
      BUT_ONLY

  END

END

// several exterior areas flagged as dungeons
COPY_EXISTING ~ar1200.are~ ~override~ // easthaven (prologue) - missing caravan - exterior
              ~ar2000.are~ ~override~ // kuldahar pass
              ~ar2001.are~ ~override~ // kuldahar pass - goblin area
              ~ar3000.are~ ~override~ // vale of shadows - exterior
              ~ar3600.are~ ~override~ // temple of the forgotten god - exterior
              ~ar4000.are~ ~override~ // dragon's eye - exterior
              ~ar5000.are~ ~override~ // severed hand - exterior
              ~ar6000.are~ ~override~ // dorn's deep - exterior
              ~ar7000.are~ ~override~ // wyrm's tooth glacier - entry pass
              ~ar7005.are~ ~override~ // wyrm's tooth glacier - aquarium exterior
              ~ar9300.are~ ~override~ // burial isle
              ~ar9500.are~ ~override~ // gloomfrost
              ~ar9600.are~ ~override~ // sea of moving ice
              ~ar9700.are~ ~override~ // anauroch castle - outer courtyard (totl start area)
              ~ar9709.are~ ~override~ // anauroch castle - western battlements
              ~ar9710.are~ ~override~ // anauroch castle - eastern battlements
  WRITE_SHORT 0x48 (THIS BAND `BIT5) // remove the Dungeon flag
  BUT_ONLY IF_EXISTS

// trapped but detected containers, ar3501 fixed by HoW
ACTION_FOR_EACH file IN ar3501 ar3502 ar4005 ar6005 ar6006 ar9400 ar9714 BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~%file%.are~) THEN BEGIN

    COPY_EXISTING ~%file%.are~ ~override~
      // trapped containers
      READ_LONG  0x70 "cont_off"
      READ_SHORT 0x74 "cont_num"
      FOR (index = 0 ; index < cont_num ; index = index + 1) BEGIN
        READ_SHORT ("%cont_off%" + 0x30 + ("%index%" * 0xc0)) "trapped"
        READ_SHORT ("%cont_off%" + 0x32 + ("%index%" * 0xc0)) "detected"
        PATCH_IF (("%trapped%" = 1) AND ("%detected%" = 1)) BEGIN
          WRITE_SHORT ("%cont_off%" + 0x32 + ("%index%" * 0xc0)) 0
        END
      END
      // floor traps
      READ_SHORT 0x5a "info_num"
      READ_LONG  0x5c "info_off"
      FOR (index = 0 ; index < info_num ; index = index + 1) BEGIN
        READ_SHORT ("%info_off%" + 0x6c + ("%index%" * 0xc4)) "trapped"
        READ_SHORT ("%info_off%" + 0x6e + ("%index%" * 0xc4)) "detected"
        PATCH_IF (("%trapped%" = 1) AND ("%detected%" = 1)) BEGIN
          WRITE_SHORT ("%info_off%" + 0x6e + ("%index%" * 0xc4)) 0
        END
      END
      BUT_ONLY

  END

END

// cursor fix
COPY_EXISTING ~ar1201.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR cursor = 30 STR_VAR trig_name = "To1200" END
  BUT_ONLY

// random flag in unused bits
COPY_EXISTING ~ar2000.are~ ~override~
  WRITE_LONG 0x2c 0 // clear flags for old, unused area transition
  BUT_ONLY
  
// cursor fix
COPY_EXISTING ~ar2000.are~ ~override~
              ~ar2002.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR cursor = 30 STR_VAR trig_name = "To2001" END
  BUT_ONLY

// cursor fix
COPY_EXISTING ~ar2001.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR cursor = 30 STR_VAR trig_name = "To2000" END
  LPF ALTER_AREA_REGION INT_VAR cursor = 30 STR_VAR trig_name = "To2002" END
  BUT_ONLY

// add party required flag
COPY_EXISTING ~ar2100.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR trig_name = "To2109" END
  BUT_ONLY

// extra scripts give barkeep, patrons something to do if they go hostile
COPY_EXISTING ~ar2111.are~ ~override~
  FOR (index = 1 ; index < 10 ; ++index) BEGIN
    LPF ALTER_AREA_ACTOR STR_VAR actor_name = EVAL "Townsperson %index%" script_default = "kutowng" END
  END
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Whitcomb" script_default = "kuconlse" END
  BUT_ONLY

// wrong icon
COPY_EXISTING ~ar2115.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 12 STR_VAR cont_name = "Locked Crate" END
  BUT_ONLY

// removes extended night flag 
COPY_EXISTING ~ar3000.are~ ~override~ // Vale of Shadows
              ~ar9500.are~ ~override~ // Gloomfrost
  WRITE_SHORT 0x48 (THIS BAND `BIT6) 
  BUT_ONLY IF_EXISTS // ar9500 is HoW area

// ambient mismatch
COPY_EXISTING ~ar3001.are~ ~override~
  LPF ALTER_AREA_AMBIENT INT_VAR sounds = 5 STR_VAR ambient_name = "growls 1" END // only five ambient sounds, not six
  BUT_ONLY

// container icon
COPY_EXISTING ~ar3101.are~ ~override~
              ~ar3201.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon =  7 STR_VAR cont_name = "Sarcophagus 1" END
  BUT_ONLY

// container icon
COPY_EXISTING ~ar3301.are~ ~override~
              ~ar3501.are~ ~override~
              ~ar3502.are~ ~override~
              ~ar3503.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon =  7 STR_VAR cont_name = "Sarcophagus" END
  BUT_ONLY

// container icon
COPY_EXISTING ~ar3501.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon =  7 STR_VAR cont_name = "Trap Sarcophagus" END
  BUT_ONLY

// door cursor
COPY_EXISTING ~ar3502.are~ ~override~
  LPF ALTER_AREA_DOOR INT_VAR door_icon = 8 STR_VAR door_name = "AR3502Switch1" END
  BUT_ONLY
  
// Kresselack's chest in AR3503 should not show up as trapped
COPY_EXISTING ~ar3503.are~ ~override~ // Kresselack's Tomb, level 3
  LPF ALTER_AREA_CONTAINER INT_VAR trapped = 0 STR_VAR container_name = "Sarcophagus" END // technically untraps all, but none should be trapped anyway
  BUT_ONLY

// scripts don't do anything to non-party, so disable for non-party triggering
COPY_EXISTING ~ar3602.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR flag_trap_npcs = 0 STR_VAR region_name = "Trigger Point 6" END // script won't do anything to non-party members
  LPF ALTER_AREA_REGION INT_VAR flag_trap_npcs = 0 STR_VAR region_name = "Trigger Point 7" END // script won't do anything to non-party members
  LPF ALTER_AREA_REGION INT_VAR flag_trap_npcs = 0 STR_VAR region_name = "Trigger Point 8" END // script won't do anything to non-party members
  LPF ALTER_AREA_REGION INT_VAR flag_trap_npcs = 0 STR_VAR region_name = "Trigger Point 9" END // script won't do anything to non-party members
  BUT_ONLY

// lizard king, minions keep together EA-wise
COPY_EXISTING ~ar4001.are~ ~override~
  LPF ALTER_AREA_ACTOR STR_VAR script_race = "4001lkl" actor_name = "Lizard_King" END
  BUT_ONLY

// container icon, removal of time-traveling book
COPY_EXISTING ~ar4004.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon =  8 STR_VAR cont_name = "Cushions" END
  LPF DELETE_AREA_ITEM STR_VAR item_to_delete = book45 END // references future events
  BUT_ONLY

// container icons, missing generic dialogue for creatures; two traps missing scripts
COPY_EXISTING ~ar4005.are~ ~override~
  LPF ALTER_AREA_REGION STR_VAR region_name = "Poison Needle Confusion 3" door_script = GNT2017 END // trap has no script
  LPF ALTER_AREA_REGION STR_VAR region_name = "Poison Needle Damage 3" door_script = GNT2015 END // trap has no script
  LPF ALTER_AREA_ACTOR STR_VAR dlg_file = "dgenmond" actor_name = "Yuan-Ti Champion 1" END
  LPF ALTER_AREA_ACTOR STR_VAR dlg_file = "dgenmond" actor_name = "Yuan-Ti Champion 2" END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 5 STR_VAR cont_name = "Desk 2" END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Pillow 1" END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Pillow 2" END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Pillow 3" END
  LPF ALTER_AREA_CONTAINER INT_VAR coord_x = 554 coord_y = 421 STR_VAR container_name = "Chest 5" END // adjust chest activation point to prevent shuffling
  BUT_ONLY

COPY_EXISTING ~ar5000.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR cursor = 28 STR_VAR trig_name = "To5001" END // cursor fix
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Squirrel" script_default = "" END // blanks reference to non-existent script
  BUT_ONLY

// container icons
COPY_EXISTING ~ar5001.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 4" END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 6" END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 7" END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 8" END
  BUT_ONLY

// container icons, goblin has no script so does nothing
COPY_EXISTING ~ar5002.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 3" END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 4" END
  READ_LONG  0x54 cre_off
  READ_SHORT 0x58 cre_num
  FOR (index = 0 ; index < cre_num ; ++index) BEGIN
    READ_SHORT (cre_off + 0x20 + (0x110 * index)) x_coord
    READ_SHORT (cre_off + 0x22 + (0x110 * index)) y_coord
    PATCH_IF ((x_coord = 494) AND (y_coord = 1495)) BEGIN
      WRITE_ASCII (cre_off + 0x58 + (0x110 * index)) ~shgaflt~ #8
      WRITE_ASCII (cre_off + 0x70 + (0x110 * index)) ~gnmmgsg~ #8
    END
  END
  BUT_ONLY

// container icons
COPY_EXISTING ~ar5003.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 1" END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 2" END
  BUT_ONLY

// container icon
COPY_EXISTING ~ar5004.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 5 STR_VAR cont_name = "Container 2" END
  BUT_ONLY

// travel cursor, area type
COPY_EXISTING ~ar5101.are~ ~override~
  WRITE_SHORT 0x48 (THIS BOR (BIT0 + BIT1)) // add flags for outdoor and day/night
  LPF ALTER_AREA_REGION INT_VAR cursor = 28 STR_VAR trig_name = "To5004" END
  BUT_ONLY

// wrong orientation when entering
COPY_EXISTING ~ar5102.are~ ~override~
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 12 STR_VAR entry_name = "FR5104" END
  BUT_ONLY

// container icon
COPY_EXISTING ~ar5301.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 1" END
  BUT_ONLY

// container icon
COPY_EXISTING ~ar5302.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 5 STR_VAR cont_name = "Container 2" END
  BUT_ONLY

COPY_EXISTING ~ar5304.are~ ~override~
              ~ar5402.are~ ~override~
  WRITE_SHORT 0x48 (THIS BOR BIT1) // add flags day/night
  BUT_ONLY

// container icon
COPY_EXISTING ~ar5401.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 1" END
  BUT_ONLY

// found by mystery mike over at SHS
// monument can't disply info due to missing vertices & bounding box
COPY_EXISTING ~ar6001.are~ ~override~
  SET "new_vert" = 0
  READ_SHORT 0x5a trig_num
  READ_LONG  0x5c trig_off
  READ_LONG  0x7c vert_off
  READ_SHORT 0x80 vert_num
  FOR (index = 0 ; index < trig_num ; ++index) BEGIN
    READ_SHORT (trig_off + 0x2a + (index * 0xc4)) trig_vert_num
    WRITE_LONG (trig_off + 0x2c + (index * 0xc4)) (THIS + new_vert)
    READ_LONG  (trig_off + 0x2c + (index * 0xc4)) trig_vert_idx
    READ_LONG  (trig_off + 0x64 + (index * 0xc4)) string
    PATCH_IF ((trig_vert_num = 0) AND (string = 2326)) BEGIN // if monument and no vertices already present
      WRITE_SHORT (trig_off + 0x22 + (index * 0xc4)) 1470 // bounding box
      WRITE_SHORT (trig_off + 0x24 + (index * 0xc4)) 1126 // bounding box
      WRITE_SHORT (trig_off + 0x26 + (index * 0xc4)) 1931 // bounding box
      WRITE_SHORT (trig_off + 0x28 + (index * 0xc4)) 1564 // bounding box
      WRITE_SHORT (trig_off + 0x2a + (index * 0xc4)) 13   // vertices
      SET new_vert += 13
      INSERT_BYTES  (vert_off +        (0x04 * trig_vert_idx)) 0x34
        WRITE_SHORT (vert_off + 0x00 + (0x04 * trig_vert_idx)) 1488 // vertex 0 X
        WRITE_SHORT (vert_off + 0x02 + (0x04 * trig_vert_idx)) 1517 // vertex 0 Y
        WRITE_SHORT (vert_off + 0x04 + (0x04 * trig_vert_idx)) 1470 // vertex 1 X
        WRITE_SHORT (vert_off + 0x06 + (0x04 * trig_vert_idx)) 1365 // vertex 1 Y
        WRITE_SHORT (vert_off + 0x08 + (0x04 * trig_vert_idx)) 1508 // vertex 2 X
        WRITE_SHORT (vert_off + 0x0a + (0x04 * trig_vert_idx)) 1334 // vertex 2 Y
        WRITE_SHORT (vert_off + 0x0c + (0x04 * trig_vert_idx)) 1528 // vertex 3 X
        WRITE_SHORT (vert_off + 0x0e + (0x04 * trig_vert_idx)) 1210 // vertex 3 Y
        WRITE_SHORT (vert_off + 0x10 + (0x04 * trig_vert_idx)) 1688 // vertex 4 X
        WRITE_SHORT (vert_off + 0x12 + (0x04 * trig_vert_idx)) 1126 // vertex 4 Y
        WRITE_SHORT (vert_off + 0x14 + (0x04 * trig_vert_idx)) 1781 // vertex 5 X
        WRITE_SHORT (vert_off + 0x16 + (0x04 * trig_vert_idx)) 1150 // vertex 5 Y
        WRITE_SHORT (vert_off + 0x18 + (0x04 * trig_vert_idx)) 1837 // vertex 6 X
        WRITE_SHORT (vert_off + 0x1a + (0x04 * trig_vert_idx)) 1195 // vertex 6 Y
        WRITE_SHORT (vert_off + 0x1c + (0x04 * trig_vert_idx)) 1895 // vertex 7 X
        WRITE_SHORT (vert_off + 0x1e + (0x04 * trig_vert_idx)) 1247 // vertex 7 Y
        WRITE_SHORT (vert_off + 0x20 + (0x04 * trig_vert_idx)) 1891 // vertex 8 X
        WRITE_SHORT (vert_off + 0x22 + (0x04 * trig_vert_idx)) 1298 // vertex 8 Y
        WRITE_SHORT (vert_off + 0x24 + (0x04 * trig_vert_idx)) 1931 // vertex 9 X
        WRITE_SHORT (vert_off + 0x26 + (0x04 * trig_vert_idx)) 1349 // vertex 9 Y
        WRITE_SHORT (vert_off + 0x28 + (0x04 * trig_vert_idx)) 1908 // vertex 10 X
        WRITE_SHORT (vert_off + 0x2a + (0x04 * trig_vert_idx)) 1456 // vertex 10 Y
        WRITE_SHORT (vert_off + 0x2c + (0x04 * trig_vert_idx)) 1798 // vertex 11 X
        WRITE_SHORT (vert_off + 0x2e + (0x04 * trig_vert_idx)) 1538 // vertex 11 Y
        WRITE_SHORT (vert_off + 0x30 + (0x04 * trig_vert_idx)) 1664 // vertex 12 X
        WRITE_SHORT (vert_off + 0x32 + (0x04 * trig_vert_idx)) 1564 // vertex 12 Y
    END
  END
  WRITE_SHORT 0x80 (vert_num + new_vert)
  // adjust other offsets
  PATCH_FOR_EACH offset IN 0x54 0x5c 0x60 0x68 0x70 0x78 /* 0x7c */ 0x84 0x88 0x90 0xa0 0xa8 0xb0 0xb8 0xbc 0xc0 BEGIN
    READ_LONG offset val_off
    PATCH_IF (val_off > vert_off) BEGIN
      WRITE_LONG offset (val_off + (new_vert * 0x04))
    END
  END
  BUT_ONLY

// two creatures at same coordinates, remove dupe caged souls, generic dlg for spider, door cursor
COPY_EXISTING ~ar6002.are~ ~override~
  LPF ALTER_AREA_ACTOR INT_VAR x_coord = 2194 y_coord = 1264 STR_VAR actor_name = "Neo Orog 8" END
  LPF ALTER_AREA_ACTOR STR_VAR dlg_file = "dgenmond" actor_name = "Phase Spider" END
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = ~Orc Elite 12~ script_default = udorcar END // lacks any combat scripting
  LPF ALTER_AREA_DOOR INT_VAR door_icon = 8 STR_VAR door_name = "AR6002PuzzleDoorTrigger" END
  LPF REPLACE_AREA_ITEM STR_VAR old_item = "zzu6cs" new_item = "u1hax4b" END
  BUT_ONLY
  
// piles with bounding box
COPY_EXISTING ~ar6003.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR bounding_left = 0 bounding_top = 0 bounding_right = 0 bounding_bottom = 0 STR_VAR container_name = "Container 1" END
  BUT_ONLY

// container icons; bad party orientation on entrance
COPY_EXISTING ~ar6005.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR cursor = 22 STR_VAR region_name = "Tombs 4" END // using wrong cursor
  LPF ALTER_AREA_REGION INT_VAR info_point = 3019 STR_VAR region_name = "Statues 3" END // info region lacking string
  FOR (index = 1 ; index < 26 ; ++index) BEGIN
    LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = EVAL "Container %index%"  END
  END
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 9 STR_VAR entrance_name = "FR6004b" END
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 9 STR_VAR entrance_name = "FR6013b" END
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 7 STR_VAR entrance_name = "FR6004c" END
  LPF ALTER_AREA_ENTRANCE INT_VAR orient = 7 STR_VAR entrance_name = "FR6013c" END
  // random, magic ammo comes identified
  READ_SHORT 0x76 item_num
  READ_LONG  0x78 item_off
  FOR (index = 0 ; index < item_num ; ++index) BEGIN
    READ_ASCII (item_off +        (0x14 * index)) item
    PATCH_IF ("%item%" STRING_COMPARE_CASE "rngp4" = 0) BEGIN
      WRITE_LONG  (item_off + 0x10 + (0x14 * index)) (THIS BAND `BIT0) // removed idntified flag
    END
  END
  BUT_ONLY

// container icons, made key required
COPY_EXISTING ~ar6006.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Container 1"  END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Container 9"  END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Container 11" END
  LPF ALTER_AREA_DOOR INT_VAR lock_diff = 100 STR_VAR door_name = "AR6006Door11" END
  BUT_ONLY

// piles with bounding box
COPY_EXISTING ~ar6011.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR bounding_left = 0 bounding_top = 0 bounding_right = 0 bounding_bottom = 0 STR_VAR container_name = "Container 1" END
  BUT_ONLY

// container icon, door cursor, travel cursor
COPY_EXISTING ~ar6013.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR trig_name = "To6011" END
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Receptable" END
  LPF ALTER_AREA_DOOR INT_VAR door_icon = 2 STR_VAR door_name = "AR6013Forge1" END
  BUT_ONLY

// door shows up as trapped, but shouldn't
COPY_EXISTING ~ar7000.are~ ~override~
  LPF ALTER_AREA_DOOR INT_VAR flag_detectable = 0 STR_VAR door_name = AR7000Door1 END
  BUT_ONLY  
  
// door shows up as trapped, but shouldn't
COPY_EXISTING ~ar7002.are~ ~override~
  LPF ALTER_AREA_DOOR INT_VAR flag_detectable = 0 STR_VAR door_name = AR7002Door1 END
  BUT_ONLY  

// container icon, charges for blur deck
COPY_EXISTING ~ar7004.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Hut 2"  END
  LPF REPLACE_AREA_ITEM INT_VAR charges1 = 3 STR_VAR old_item = blrdeck new_item = blrdeck END
  BUT_ONLY

// move thieves so they can move, travel cursor, party-required flag
// bad team assignments in how; also see ldthief.bcs
COPY_EXISTING ~ar8001.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR cursor = 34 STR_VAR trig_name = "To8003" END
  LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR trig_name = "To8005" END
  // team assignment corrections
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Salamander 105"     script_specifics = "gnteam9m" END
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Salamander 110"     script_specifics = "gnteam2m" END
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Salamander 111"     script_specifics = "gnteam2m" END
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Tarnished Sentry 3" script_specifics = "gnteam2m" END
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Tarnished Sentry 4" script_specifics = "gnteam2m" END
  // thieves spawning on top of each other
  LPF ALTER_AREA_ACTOR INT_VAR x_coord = 1527 y_coord = 357 STR_VAR actor_name = "Thief_12" END
  LPF ALTER_AREA_ACTOR INT_VAR x_coord = 1555 y_coord = 367 STR_VAR actor_name = "Thief_13" END
  LPF ALTER_AREA_ACTOR INT_VAR x_coord = 1924 y_coord = 413 STR_VAR actor_name = "Thief_14" END
  LPF ALTER_AREA_ACTOR INT_VAR x_coord = 2160 y_coord = 369 STR_VAR actor_name = "Thief_15" END
  LPF ALTER_AREA_ACTOR INT_VAR x_coord = 2363 y_coord = 330 STR_VAR actor_name = "Thief_16" END
  LPF ALTER_AREA_ACTOR INT_VAR x_coord = 2532 y_coord = 444 STR_VAR actor_name = "Thief_17" END
  LPF ALTER_AREA_ACTOR INT_VAR x_coord = 2617 y_coord = 405 STR_VAR actor_name = "Thief_18" END
  LPF ALTER_AREA_ACTOR INT_VAR x_coord = 2688 y_coord = 369 STR_VAR actor_name = "Thief_19" END
  BUT_ONLY

// travel cursor
COPY_EXISTING ~ar8003.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR cursor = 34 STR_VAR trig_name = "To8001" END
  BUT_ONLY

// shouldn't be in two teams
COPY_EXISTING ~ar8004.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR info_point = 19258 STR_VAR region_name = "Stone 2" END // info region lacking string
  LPF ALTER_AREA_REGION INT_VAR info_point = 19258 STR_VAR region_name = "Stone 4" END // info region lacking string
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Sleeping Svirfneblin 1" script_race = gnteam3 END
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Sleeping Svirfneblin 2" script_race = gnteam3 END
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Sleeping Svirfneblin 3" script_race = gnteam3 END
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Sleeping Svirfneblin 4" script_race = gnteam3 END
  LPF ALTER_AREA_ACTOR STR_VAR actor_name = "Sleeping Svirfneblin 5" script_race = gnteam3 END
  BUT_ONLY

COPY_EXISTING ~ar8005.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR cursor = 22 STR_VAR region_name = "Myco 5" END // using wrong cursor
  BUT_ONLY

// container icon
COPY_EXISTING ~ar8006.are~ ~override~
  LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 3 STR_VAR cont_name = "Dresser 1"  END
  BUT_ONLY

// travel cursor
COPY_EXISTING ~ar8007.are~ ~override~
  LPF ALTER_AREA_REGION INT_VAR cursor = 28 STR_VAR trig_name = "To8006" END
  LPF ALTER_AREA_REGION INT_VAR info_point = 18532 STR_VAR region_name = "Banner 14" END // info region lacking string
  LPF ALTER_AREA_REGION INT_VAR cursor = 22 STR_VAR region_name = "Thieves Tools 2" END // using wrong cursor
  BUT_ONLY

// dialogue for gnomes, charges for oil of null effect
COPY_EXISTING ~ar8010.are~ ~override~
  LPF ALTER_AREA_DOOR STR_VAR door_name = "AR8010HideMe" door_script = "" END // blanks reference to non-existent script
  LPF REPLACE_AREA_ITEM INT_VAR charges1 = 1 STR_VAR old_item = pnull new_item = pnull END
  FOR (index = 13 ; index < 23 ; ++index) BEGIN
    LPF ALTER_AREA_ACTOR STR_VAR dlg_file = "dgnomebl" actor_name = EVAL "Blind Svirfneblin %index%" END
  END
  BUT_ONLY

// remove brother harken's left over voice floatmessage script
COPY_EXISTING ~ar8012.are~ ~override~
  LPF ALTER_AREA_ACTOR STR_VAR script_general = "" actor_name = "Brother Harken" END
  BUT_ONLY

ACTION_IF game_is_iwd THEN BEGIN // iwd w/o how or totlm

  // flag added in HoW; matching ar2100 flag added above
  COPY_EXISTING ~ar2109.are~ ~override~
    LPF ALTER_AREA_REGION INT_VAR flag_party_required = 1 STR_VAR trig_name = "To2100" END
    BUT_ONLY

  // skellies trapped in wall (fixed in how)
  COPY_EXISTING ~ar3201.are~ ~override~
    LPF ALTER_AREA_ACTOR INT_VAR x_coord = 858 y_coord = 806 STR_VAR actor_name = "Skeleton 1" END
    LPF ALTER_AREA_ACTOR INT_VAR x_coord = 920 y_coord = 795 STR_VAR actor_name = "Skeleton 2" END
    BUT_ONLY

END ELSE BEGIN // how, totlm fixes

  // one of the hard-difficulty-only spiders doesn't do anything since it lacks scripting
  COPY_EXISTING ~ar4001.are~ ~override~ // this spider only added in how
    LPF ALTER_AREA_ACTOR STR_VAR actor_name = ~NEW Sword Spider 5~ script_class = gnteam7m script_race = efatkmel END
    BUT_ONLY

  // container icon, ID'd item
  COPY_EXISTING ~ar9102.are~ ~override~
    LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 5"  END
    LPF REPLACE_AREA_ITEM INT_VAR charges1 = 1 STR_VAR old_item = clck13 new_item = clck13 END // un-IDs item
    BUT_ONLY

  // don't split party when coming up stairs
  COPY_EXISTING ~ar9107.are~ ~override~
    LPF ALTER_AREA_ENTRANCE INT_VAR x_coord = 509 y_coord = 570 STR_VAR entrance_name = FR9106 END 
    BUT_ONLY

  COPY_EXISTING ~ar9200.are~ ~override~
    LPF ALTER_AREA_AMBIENT INT_VAR sounds = 1 STR_VAR ambient_name = "Banner 1" END // ambient lists two sounds, but only one
    BUT_ONLY

  // ambient lists two sounds, but only one
  COPY_EXISTING ~ar9300.are~ ~override~
    LPF ALTER_AREA_AMBIENT INT_VAR sounds = 1 STR_VAR ambient_name = "main ambi" END
    BUT_ONLY

  // missing script, but no obvious replacement
  //COPY_EXISTING ~ar9500.are~ ~override~
  //  LPF ALTER_AREA_REGION INT_VAR flag_trap_npcs = 0 STR_VAR region_name = "FG Encounters" END // SCFROGT1 is blank, no suitable replacement
  
  // container icon, key required
  COPY_EXISTING ~ar9600.are~ ~override~
    LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 1"  END
    LPF ALTER_AREA_DOOR INT_VAR lock_diff = 100 STR_VAR door_name = "AR9600_Door1" END
    BUT_ONLY

  // remove dupe random item drops (extres4 also available in ar9200)
  COPY_EXISTING ~ar9601.are~ ~override~
    LPF REPLACE_AREA_ITEM INT_VAR charges1 = 1 charges2 = 1 STR_VAR old_item = extres4 new_item = extres1 END // 1/1 charges for ogien or cloakin 
    BUT_ONLY

  // container icon
  COPY_EXISTING ~ar9602.are~ ~override~
    LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 10 STR_VAR cont_name = "Container 4" END
    BUT_ONLY
  
  // container icon, charges for items
  COPY_EXISTING ~ar9603.are~ ~override~
    LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 8" END
    LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 9" END
    LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Container 10" END
    LPF REPLACE_AREA_ITEM INT_VAR charges2 = 3 STR_VAR old_item = power new_item = power END
    LPF REPLACE_AREA_ITEM INT_VAR charges1 = 1 STR_VAR old_item = harp new_item = harp END
    BUT_ONLY

  ACTION_IF game_is_totlm THEN BEGIN // totlm fixes

    // add tracking info for totlm areas
    OUTER_SET tracking9700 = RESOLVE_STR_REF (@119) 
    OUTER_SET tracking9709 = RESOLVE_STR_REF (@120) 
    OUTER_SET tracking9710 = RESOLVE_STR_REF (@121) 
    OUTER_SET tracking9711 = RESOLVE_STR_REF (@122) 
    OUTER_SET tracking9712 = RESOLVE_STR_REF (@123) 
    OUTER_SET tracking9714 = RESOLVE_STR_REF (@124) 
    OUTER_SET tracking9716 = RESOLVE_STR_REF (@125) 
    OUTER_SET tracking9717 = RESOLVE_STR_REF (@126) 
    OUTER_SET tracking9718 = RESOLVE_STR_REF (@127) 
    OUTER_SET tracking9800 = RESOLVE_STR_REF (@128) 
    OUTER_SET tracking9801 = RESOLVE_STR_REF (@129) 
    APPEND ~tracking.2da~ ~AR9700    %tracking9700%    50~ UNLESS ~^AR9700~ // Anauroch Castle - outer courtyard (TotL start area)
    APPEND ~tracking.2da~ ~AR9709    %tracking9709%    50~ UNLESS ~^AR9709~ // Anauroch Castle - western battlements
    APPEND ~tracking.2da~ ~AR9710    %tracking9710%    50~ UNLESS ~^AR9710~ // Anauroch Castle - eastern battlements
    APPEND ~tracking.2da~ ~AR9711    %tracking9711%    50~ UNLESS ~^AR9711~ // Anauroch Castle - inner courtyard
    APPEND ~tracking.2da~ ~AR9712    %tracking9712%    50~ UNLESS ~^AR9712~ // Anauroch Castle - upper story
    APPEND ~tracking.2da~ ~AR9714    %tracking9714%    50~ UNLESS ~^AR9714~ // Anauroch Castle - Watchknight Tomb
    APPEND ~tracking.2da~ ~AR9716    %tracking9716%    50~ UNLESS ~^AR9716~ // Anauroch Castle - dungeon level 1
    APPEND ~tracking.2da~ ~AR9717    %tracking9717%    50~ UNLESS ~^AR9717~ // Anauroch Castle - dungeon level 2
    APPEND ~tracking.2da~ ~AR9718    %tracking9718%    50~ UNLESS ~^AR9718~ // Anauroch Castle - dungeon level 3
    APPEND ~tracking.2da~ ~AR9800    %tracking9800%    50~ UNLESS ~^AR9800~ // Anauroch Castle - Jackal Clan caverns - level 1
    APPEND ~tracking.2da~ ~AR9801    %tracking9801%    50~ UNLESS ~^AR9801~ // Anauroch Castle - Jackal Clan caverns - level 2

    // only two harpy fiends should go away at CheckPartyAverageLevel(14,LESS_THAN)
    COPY_EXISTING ~ar9706.are~ ~override~
      SET harpy = 0
      READ_LONG  0x54 actor_off
      READ_SHORT 0x58 actor_num
      FOR (index = 0 ; index < actor_num ; ++index) BEGIN
        READ_ASCII (actor_off + 0x00 + (index * 0x110)) name 
        PATCH_IF ("%name%" STRING_COMPARE_CASE "HrpFnd_0" = 0) BEGIN
          WRITE_ASCIIE (actor_off + 0x00 + (index * 0x110)) ~HrpFnd_%harpy%~
          SET harpy += 1
        END
      END
      BUT_ONLY

    // scripts don't do anything to non-party, so disable for non-party triggering
    COPY_EXISTING ~ar9708.are~ ~override~
      LPF ALTER_AREA_REGION INT_VAR flag_trap_npcs = 0 STR_VAR region_name = "Fire Trap" END // script won't do anything to non-party members
      BUT_ONLY

    // container icon
    COPY_EXISTING ~ar9711.are~ ~override~
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Throne" END
      BUT_ONLY

    // container icon, creatures facing the wrong way
    COPY_EXISTING ~ar9712.are~ ~override~
      LPF ALTER_AREA_ACTOR INT_VAR orient = 8 STR_VAR actor_name = "Spectral Courtier M2" END
      LPF ALTER_AREA_ACTOR INT_VAR orient = 0 STR_VAR actor_name = "Spectral Courtier F2" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 6 STR_VAR cont_name = "Container 1" END
      BUT_ONLY

    // container icon, party orientation when entering
    // revenant regen item fix (see cdtrolfg.bcs, revnant.cre, rndrevn.cre x2)
    COPY_EXISTING ~ar9714.are~ ~override~
      FOR (index = 1 ; index < 25 ; ++index) BEGIN
        LPF ALTER_AREA_ACTOR STR_VAR script_class = "cdtrolg" actor_name = EVAL "Revenant %index%" END
      END
      LPF ALTER_AREA_ENTRANCE INT_VAR orient = 10 STR_VAR entry_name = "fr9713" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Erris's Sarcophogus" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Geddian's Sarcophogus" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Zierki's Sarcophogus" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Giles's Sarcophogus" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Plat Key Sarcophogus" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Gold Key Sarcophogus" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Elec Key Sarcophogus" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Silv Key Sarcophogus" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Cop Key Sarcophogus" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Bronze Key Sarcophogus" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 7 STR_VAR cont_name = "Treasure Sarcophogus" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 10 STR_VAR cont_name = "Corpse Shelf" END
      BUT_ONLY
  
    // travel cursor
    COPY_EXISTING ~ar9716.are~ ~override~
      LPF ALTER_AREA_REGION INT_VAR cursor = 28 STR_VAR trig_name = "To9717" END
      BUT_ONLY
    
    // travel cursor
    COPY_EXISTING ~ar9717.are~ ~override~
      LPF ALTER_AREA_REGION INT_VAR cursor = 28 STR_VAR trig_name = "To9718" END
      BUT_ONLY

    // give slimes a little extra space so they don't get stuck
    COPY_EXISTING ~ar9718.are~ ~override~
      LPF ALTER_AREA_ACTOR INT_VAR y_coord =  839 dest_y =  839 STR_VAR actor_name = "Olive Slime 4" END
      LPF ALTER_AREA_ACTOR INT_VAR x_coord = 1119 dest_x = 1119 STR_VAR actor_name = "Olive Slime 9" END
      BUT_ONLY
    
    // container icon, charges
    COPY_EXISTING ~ar9800.are~ ~override~
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Rack 1" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Rack 2" END
      LPF ALTER_AREA_CONTAINER INT_VAR cont_icon = 8 STR_VAR cont_name = "Book" END
      LPF REPLACE_AREA_ITEM INT_VAR charges1 = 10 STR_VAR old_item = ring03 new_item = ring03 END
      BUT_ONLY

    // move afatkmel below their normal bbcloud script so they use their clouds
    COPY_EXISTING ~ar9801.are~ ~override~
      LPF ALTER_AREA_ACTOR STR_VAR actor_name = ~Bombardier Beetle~ script_specifics = ~~ script_general = efatkmel END
      BUT_ONLY

    // minotaur trapped by door, unable to replicate but fixed anyway
    COPY ~iwdfixpack/bmp/ar9717sr.bmp~ ~override~
  
  END

END

/////                                                  \\\\\
///// find traps shenanigans                           \\\\\
/////                                                  \\\\\

// keep this after script, area fixes
COPY_EXISTING_REGEXP ~^.+\.are$~ ~override~
  READ_SHORT 0x5a trig_num
  READ_LONG  0x5c trig_off
  SET local = 0 
  FOR (index = 0 ; index < trig_num ; ++index) BEGIN
    READ_SHORT (trig_off + 0x20 + (index * 0xc4)) type
    PATCH_IF type = 0 BEGIN // ignore info, travel triggers
      READ_LONG  (trig_off + 0x60 + (index * 0xc4)) flags
      READ_SHORT (trig_off + 0x68 + (index * 0xc4)) detect_diff
      PATCH_IF (((flags & BIT3) = BIT3) AND (detect_diff != 100)) BEGIN 
        READ_ASCII   (trig_off + 0x7c + (index * 0xc4)) script_old
        WRITE_ASCIIE (trig_off + 0x7c + (index * 0xc4)) ~%SOURCE_RES%%local%~ #8 // writes e.g. ar30001
        WRITE_ASCII  (trig_off + 0x7c + (index * 0xc4)) ~c!~ #2                  // changes to  c!30001
        READ_ASCII   (trig_off + 0x7c + (index * 0xc4)) script_new
        SET local += 1
        DEFINE_ASSOCIATIVE_ARRAY cd_new_trap_scripts BEGIN ~%script_new%~,~%SOURCE_RES%_region%index%_%script_old%~ => ~%script_old%~ END
      END // actual trap check
    END // proximity trigger check
  END // end region loop   
  /* trapped containers do not show up with find traps
  READ_LONG  0x70 cont_off
  READ_SHORT 0x74 cont_num
  FOR (index = 0 ; index < cont_num ; ++index) BEGIN
    READ_SHORT (cont_off + 0x30 + (index * 0xc0)) trapped
    PATCH_IF trapped BEGIN // if trapped
      READ_SHORT (cont_off + 0x2c + (index * 0xc0)) detect_diff
      PATCH_IF detect_diff != 100 BEGIN 
        READ_ASCII   (cont_off + 0x48 + (index * 0xc0)) script_old
        WRITE_ASCIIE (cont_off + 0x48 + (index * 0xc0)) ~%SOURCE_RES%%local%~ #8 // writes e.g. ar30001
        WRITE_ASCIIE (cont_off + 0x48 + (index * 0xc0)) ~c!~ #2                  // changes to  c!30001
        READ_ASCII   (cont_off + 0x48 + (index * 0xc0)) script_new
        SET local += 1
        DEFINE_ASSOCIATIVE_ARRAY cd_new_trap_scripts BEGIN ~%script_new%~,~%SOURCE_RES%_cont%index%_%script_old%~ => ~%script_old%~ END
      END // actual trap check
    END // trapped check
  END // end container loop  
  */  
  READ_LONG  0xa4 door_num
  READ_LONG  0xa8 door_off
  FOR (index = 0 ; index < door_num ; ++index) BEGIN
    READ_SHORT (door_off + 0x70 + (index * 0xc8)) trapped
    PATCH_IF trapped BEGIN // if trapped
      READ_LONG  (door_off + 0x28 + (index * 0xc8)) flags
      READ_SHORT (door_off + 0x6c + (index * 0xc8)) detect_diff
      PATCH_IF (((flags & BIT3) = BIT3) AND (detect_diff != 100)) BEGIN 
        READ_ASCII   (door_off + 0x80 + (index * 0xc8)) script_old
        WRITE_ASCIIE (door_off + 0x80 + (index * 0xc8)) ~%SOURCE_RES%%local%~ #8 // writes e.g. ar30001
        WRITE_ASCIIE (door_off + 0x80 + (index * 0xc8)) ~c!~ #2                  // changes to  c!30001
        READ_ASCII   (door_off + 0x80 + (index * 0xc8)) script_new
        SET local += 1
        DEFINE_ASSOCIATIVE_ARRAY cd_new_trap_scripts BEGIN ~%script_new%~,~%SOURCE_RES%_door%index%_%script_old%~ => ~%script_old%~ END
      END // actual trap check
    END // trapped check
  END // end container loop   
  BUT_ONLY
    
ACTION_PHP_EACH cd_new_trap_scripts AS params => source BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%source%.bcs~ BEGIN
  
    COPY_EXISTING ~%source%.bcs~ ~override/%params_0%.bcs~
    EXTEND_BOTTOM ~%params_0%.bcs~ ~iwdfixpack/baf/sppr205.baf~ EVALUATE_BUFFER
    
  END

END

/////                                                  \\\\\
///// creature field fixes                             \\\\\
/////                                                  \\\\\

INCLUDE ~iwdfixpack/lib/creature.tpa~

// file, gender, sex, alignment, general, race, class, specifics => file // name
ACTION_PHP_EACH cd_iwdfixpack_item AS params => file BEGIN

  COPY_EXISTING ~%file%.cre~ ~override~
    PATCH_IF (IS_AN_INT params_2) BEGIN WRITE_BYTE 0x237 params_2 END // sex
    PATCH_IF (IS_AN_INT params_4) BEGIN WRITE_BYTE 0x2d9 params_4 END // general
    PATCH_IF (IS_AN_INT params_5) BEGIN WRITE_BYTE 0x2da params_5 END // race
    PATCH_IF (IS_AN_INT params_6) BEGIN WRITE_BYTE 0x2db params_6 END // class
    PATCH_IF (IS_AN_INT params_7) BEGIN WRITE_BYTE 0x2dc params_7 END // specifics
    PATCH_IF (IS_AN_INT params_1) BEGIN WRITE_BYTE 0x2dd params_1 END // gender
    PATCH_IF (IS_AN_INT params_3) BEGIN WRITE_BYTE 0x2e3 params_3 END // alignment
    BUT_ONLY IF_EXISTS

END
  
// changing low > high animations
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_anim_fixes BEGIN

  badson   => 0x6000 // Brother Adson
  bharken  => 0x6000 // Brother Harken
  talodead => 0x6000 // Talonite Priest
  sbethla  => 0x6010 // Priestess of Ilmater
  sincylia => 0x6010 // Sister Incylia  
  kalabac  => 0x6100 // Corpse
  idol     => 0x6200 // The Idol
  kieran   => 0x6200 // Kieran Nye
  nym      => 0x6301 // Nym
END

ACTION_PHP_EACH cd_anim_fixes AS file => anim BEGIN

  COPY_EXISTING ~%file%.cre~ ~override~
    WRITE_LONG 0x28 anim
    BUT_ONLY IF_EXISTS
    
END

/////                                                  \\\\\
///// soundset fixes                                   \\\\\
/////                                                  \\\\\

// soundset fixes
COPY_EXISTING ~ad1sklm.cre~ ~override~
              ~ad2sklm.cre~ ~override~
              ~ad2sklr.cre~ ~override~
              ~ad3sklm.cre~ ~override~
              ~ad3sklr.cre~ ~override~
              ~ad4sklr.cre~ ~override~
  WRITE_LONG 0x0ac `0
  WRITE_LONG 0x0b0 `0
  WRITE_LONG 0x0e0 `0
  WRITE_LONG 0x0e4 `0
  WRITE_LONG 0x128 `0
  WRITE_LONG 0x134 `0
  WRITE_LONG 0x144 `0
  WRITE_LONG 0x148 `0
  BUT_ONLY

// soundset fixes
COPY_EXISTING ~ms3hspi.cre~ ~override~
  WRITE_LONG 0x0ac 5903
  WRITE_LONG 0x0b0 5904
  WRITE_LONG 0x0c8 `0
  WRITE_LONG 0x0cc `0
  WRITE_LONG 0x0dc `0
  WRITE_LONG 0x0e0 5901
  WRITE_LONG 0x0e4 5902
  WRITE_LONG 0x0ec `0
  WRITE_LONG 0x0f0 `0
  WRITE_LONG 0x10c `0
  WRITE_LONG 0x110 `0
  WRITE_LONG 0x128 5908
  WRITE_LONG 0x134 5909
  WRITE_LONG 0x144 5905
  WRITE_LONG 0x148 5906
  BUT_ONLY

// soundset fixes
COPY_EXISTING ~msmcrwl.cre~ ~override~
  WRITE_LONG 0x0ac 5874
  WRITE_LONG 0x0b0 5875
  WRITE_LONG 0x0e0 5872
  WRITE_LONG 0x0e4 5873
  WRITE_LONG 0x128 5879
  WRITE_LONG 0x134 5880
  WRITE_LONG 0x144 5876
  WRITE_LONG 0x148 5877
  BUT_ONLY

// soundset fixes
COPY_EXISTING ~msmpspi.cre~ ~override~
  WRITE_LONG 0x0ac 5912
  WRITE_LONG 0x0b0 5913
  WRITE_LONG 0x0c8 `0
  WRITE_LONG 0x0cc `0
  WRITE_LONG 0x0dc `0
  WRITE_LONG 0x0e0 5910
  WRITE_LONG 0x0e4 5911
  WRITE_LONG 0x0ec `0
  WRITE_LONG 0x0f0 `0
  WRITE_LONG 0x10c `0
  WRITE_LONG 0x110 `0
  WRITE_LONG 0x128 5917
  WRITE_LONG 0x134 5918
  WRITE_LONG 0x144 5914
  WRITE_LONG 0x148 5915
  BUT_ONLY

// soundset fixes
COPY_EXISTING ~rdespiw.cre~ ~override~
  WRITE_LONG 0x0ac 5929
  WRITE_LONG 0x0b0 5930
  WRITE_LONG 0x0c8 `0
  WRITE_LONG 0x0cc `0
  WRITE_LONG 0x0dc `0
  WRITE_LONG 0x0e0 5928
  WRITE_LONG 0x0ec `0
  WRITE_LONG 0x0f0 `0
  WRITE_LONG 0x10c `0
  WRITE_LONG 0x128 5934
  WRITE_LONG 0x130 `0
  WRITE_LONG 0x134 5935
  WRITE_LONG 0x144 5931
  WRITE_LONG 0x148 5932
  BUT_ONLY

ACTION_IF !game_is_iwd THEN BEGIN // how, totlm fixes

  // soundset fixes
  COPY_EXISTING ~dlbonec.cre~ ~override~
    WRITE_LONG 0x0ac `0
    WRITE_LONG 0x0b0 `0
    WRITE_LONG 0x0e0 `0
    WRITE_LONG 0x0e4 `0
    WRITE_LONG 0x128 `0
    WRITE_LONG 0x134 `0
    WRITE_LONG 0x144 `0
    WRITE_LONG 0x148 `0
    WRITE_LONG 0x12c `0
    WRITE_LONG 0x138 `0
    WRITE_LONG 0x14c `0
    BUT_ONLY

  ACTION_IF game_is_totlm THEN BEGIN // totlm fixes
  
    // soundset fixes
    COPY_EXISTING ~direb.cre~ ~override~
      WRITE_LONG 0x0ac 5627
      WRITE_LONG 0x0b0 5628
      WRITE_LONG 0x0e0 5625
      WRITE_LONG 0x0e4 5626
      WRITE_LONG 0x128 5632
      WRITE_LONG 0x134 5633
      WRITE_LONG 0x144 5629
      WRITE_LONG 0x148 5630
      BUT_ONLY
  
    // soundset fixes
    COPY_EXISTING ~jkldog.cre~ ~override~
      WRITE_LONG 0x0ac 5521
      WRITE_LONG 0x0b0 5522
      WRITE_LONG 0x0e0 5519
      WRITE_LONG 0x0e4 5520
      WRITE_LONG 0x128 5526
      WRITE_LONG 0x134 5527
      WRITE_LONG 0x144 5523
      WRITE_LONG 0x148 5524
      BUT_ONLY
  
    // soundset fixes
    COPY_EXISTING ~musjelly.cre~ ~override~
      WRITE_LONG 0x0ac 5956
      WRITE_LONG 0x0b0 5957
      WRITE_LONG 0x0e0 5954 // 0x1742
      WRITE_LONG 0x0e4 5955
      WRITE_LONG 0x128 5962
      WRITE_LONG 0x134 5964
      WRITE_LONG 0x144 5958
      WRITE_LONG 0x148 5959
      BUT_ONLY
  
    // soundset fixes
    COPY_EXISTING ~ochjelly.cre~ ~override~
      WRITE_LONG 0x0ac 5966
      WRITE_LONG 0x0b0 5967
      WRITE_LONG 0x0e0 5963
      WRITE_LONG 0x0e4 5965
      WRITE_LONG 0x128 5971
      WRITE_LONG 0x134 5972
      WRITE_LONG 0x144 5968
      WRITE_LONG 0x148 5969
      BUT_ONLY
  
    // soundset fixes
    COPY_EXISTING ~olvslime.cre~ ~override~
                  ~rndoslm.cre~  ~override~
      WRITE_LONG 0x0ac 5947
      WRITE_LONG 0x0b0 5948
      WRITE_LONG 0x0e0 5945
      WRITE_LONG 0x0e4 5946
      WRITE_LONG 0x128 5952
      WRITE_LONG 0x134 5953
      WRITE_LONG 0x144 5949
      WRITE_LONG 0x148 5950
      BUT_ONLY

  END

END

// typo in tag name for beetle,fire attacks
COPY_EXISTING ~sounds.ini~ ~override~
  REPLACE_TEXTUALLY ~^tt1=~ ~att1=~
  BUT_ONLY

/////                                                  \\\\\
///// creature spellbook fixes                         \\\\\
/////                                                  \\\\\

// known spells have wrong level, type set
ACTION_FOR_EACH crefile IN
  albion eldathyf eldathyn higharch ms6yuan msmyuan rdeyuane sevsoul shadsoul shatsoul 
  yuaewax yuaewbl yuaewbo yuaewsw yuanwax yuanwbi yuanwsw yuaxdin yubldin yuchamp yuswdin
BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%crefile%.cre~ BEGIN

    COPY_EXISTING ~%crefile%.cre~ ~override~
      READ_LONG  0x308 "known_off"
      READ_LONG  0x30c "known_num"
      FOR (index = 0 ; index < known_num ; index = index + 1) BEGIN
        READ_ASCII ("%known_off%" +        ("%index%" * 0x0c)) "resref"
        PATCH_IF ("%resref%" STRING_COMPARE_REGEXP "spin99[789]" = 0) BEGIN
          WRITE_SHORT ("%known_off%" + 0x08 + ("%index%" * 0x0c)) 0 // level
          WRITE_SHORT ("%known_off%" + 0x0a + ("%index%" * 0x0c)) 2 // innate
        END
      END
      BUT_ONLY

  END
  
END

// known spells have wrong level set
COPY_EXISTING ~bandoth.cre~  ~override~
              ~wightimb.cre~ ~override~
  READ_LONG  0x308 "known_off"
  READ_LONG  0x30c "known_num"
  FOR (index = 0 ; index < known_num ; index = index + 1) BEGIN
    READ_ASCII ("%known_off%" +        ("%index%" * 0x0c)) "resref"
    PATCH_IF ("%resref%" STRING_COMPARE_REGEXP "SPWI1[0-9][0-9]" = 0) BEGIN
      WRITE_SHORT ("%known_off%" + 0x08 + ("%index%" * 0x0c)) 0 // level
    END ELSE
    PATCH_IF ("%resref%" STRING_COMPARE_REGEXP "SPWI5[0-9][0-9]" = 0) BEGIN
      WRITE_SHORT ("%known_off%" + 0x08 + ("%index%" * 0x0c)) 4 // level
    END ELSE
    PATCH_IF ("%resref%" STRING_COMPARE_REGEXP "SPWI6[0-9][0-9]" = 0) BEGIN
      WRITE_SHORT ("%known_off%" + 0x08 + ("%index%" * 0x0c)) 5 // level
    END
  END
  BUT_ONLY

// known spells have incorrect arcane/divine flags
COPY_EXISTING ~drowsor.cre~ ~override~
              ~drowspe.cre~ ~override~
              ~oswald.cre~  ~override~
              ~udsorc.cre~  ~override~
              ~udspell.cre~ ~override~
  READ_LONG  0x308 "known_off"
  READ_LONG  0x30c "known_num"
  FOR (index = 0 ; index < known_num ; index = index + 1) BEGIN
    READ_ASCII ("%known_off%" +        ("%index%" * 0x0c)) "resref"
    PATCH_IF ("%resref%" STRING_COMPARE_REGEXP "SPWI[1-9][0-9][0-9]" = 0) BEGIN
      WRITE_SHORT ("%known_off%" + 0x0a + ("%index%" * 0x0c)) 1 // arcane
    END ELSE
    PATCH_IF ("%resref%" STRING_COMPARE_REGEXP "SPPR[1-9][0-9][0-9]" = 0) BEGIN
      WRITE_SHORT ("%known_off%" + 0x0a + ("%index%" * 0x0c)) 0 // divine
    END
  END
  BUT_ONLY

// high torturer not doing anything 2/2 (see d5hitort.bcs)
COPY_EXISTING ~hightort.cre~ ~override~
  ADD_KNOWN_SPELL     ~sppr714~ #6 ~priest~
  ADD_MEMORIZED_SPELL ~sppr714~ #6 ~priest~
  BUT_ONLY
  
// have spell flagged as already cast
COPY_EXISTING ~larrel.cre~   ~override~
              ~skelblaz.cre~ ~override~
  READ_LONG 0x318 "mem_off"
  READ_LONG 0x31c "mem_num"
  FOR (index = 0 ; index < mem_num ; ++index) BEGIN
    WRITE_SHORT (mem_off + 0x08 + (index * 0xc)) 1 // spell memorized not already cast
  END
  BUT_ONLY

// presio's AS1 should be dimension door
COPY_EXISTING ~presio.cre~ ~override~
  PATCH_FOR_EACH roffset IN 0x308 0x318 BEGIN
    READ_LONG (roffset       ) offset
    READ_LONG (roffset + 0x04) number
    FOR (index = 0 ; index < number ; ++index) BEGIN
      READ_ASCII (offset +        (index * 0x0c)) "resref"
      PATCH_IF ("%resref%" STRING_COMPARE_CASE "sppr402" = 0) BEGIN
        WRITE_ASCII (offset +        (index * 0x0c)) "spwi402"
      END
    END
  END
  BUT_ONLY

// yxunomei's priests not doing anything 3/3 (see also d5yuanp2.bcs, d5yuanp3.bcs)
COPY_EXISTING ~yuantip.cre~ ~override~
  ADD_KNOWN_SPELL     ~sppr313~ #2 ~priest~
  ADD_KNOWN_SPELL     ~sppr414~ #3 ~priest~
  ADD_MEMORIZED_SPELL ~sppr313~ #2 ~priest~
  ADD_MEMORIZED_SPELL ~sppr414~ #3 ~priest~
  BUT_ONLY

// creatures with errors in their mem tables
// just nuke the tables, rebuild below (dlfznbne in totl block)
ACTION_FOR_EACH crefile IN bandoth dlfznbne drowsor drowspe oswald yuanwax yuaxdin BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%crefile%.cre~ BEGIN

    COPY_EXISTING ~%crefile%.cre~ ~override~
      READ_LONG  0x310 mem_off
      READ_LONG  0x314 mem_num
      READ_LONG  0x318 known_off
      READ_LONG  0x31c known_num
      FOR (index = 0 ; index < mem_num ; ++index) BEGIN
        WRITE_SHORT (mem_off + 0x02 + (index * 0x10)) 0 // num spells memorizable
        WRITE_SHORT (mem_off + 0x04 + (index * 0x10)) 0 // num spells currently memorizable
        WRITE_LONG  (mem_off + 0x08 + (index * 0x10)) 0 // table idx
        WRITE_LONG  (mem_off + 0x0c + (index * 0x10)) 0 // number of spells
      END
      DELETE_BYTES known_off (known_num * 0x0c)
      // fix offsets after inserted effects
      PATCH_FOR_EACH offset IN 0x320 0x324 0x32c BEGIN
        READ_LONG offset offset_val
        PATCH_IF (known_off < offset_val) BEGIN
          WRITE_LONG offset (offset_val - (known_num * 0x0c))
        END
      END
      BUT_ONLY

  END
  
END

// rebuild mem table
COPY_EXISTING ~bandoth.cre~  ~override~
  ADD_MEMORIZED_SPELL SPWI112 #0 WIZARD #5
  ADD_MEMORIZED_SPELL SPWI118 #0 WIZARD #5
  ADD_MEMORIZED_SPELL SPWI205 #1 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI304 #2 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI402 #3 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI504 #4 WIZARD #4
  ADD_MEMORIZED_SPELL SPWI599 #4 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI608 #5 WIZARD #2
  BUT_ONLY

// rebuild mem table
COPY_EXISTING ~drowsor.cre~  ~override~
  ADD_MEMORIZED_SPELL SPWI112 #0 WIZARD #4
  ADD_MEMORIZED_SPELL SPWI114 #0 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI212 #1 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI305 #2 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI308 #2 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI404 #3 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI406 #3 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI413 #3 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI417 #3 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI511 #4 WIZARD #1
  BUT_ONLY

// rebuild mem table
COPY_EXISTING ~drowspe.cre~  ~override~
  ADD_MEMORIZED_SPELL SPWI112 #0 WIZARD #3
  ADD_MEMORIZED_SPELL SPWI211 #1 WIZARD #2
  ADD_MEMORIZED_SPELL SPWI212 #1 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI214 #1 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI305 #2 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI317 #2 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI408 #3 WIZARD #1
  BUT_ONLY

// rebuild mem table
COPY_EXISTING ~oswald.cre~  ~override~
  ADD_MEMORIZED_SPELL SPWI101 #0 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI105 #0 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI115 #0 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI118 #0 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI201 #1 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI206 #1 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI217 #1 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI302 #2 WIZARD #1
  ADD_MEMORIZED_SPELL SPWI304 #2 WIZARD #2
  ADD_MEMORIZED_SPELL SPWI417 #3 WIZARD #2
  ADD_MEMORIZED_SPELL SPWI511 #4 WIZARD #1
  BUT_ONLY

// rebuild mem table
COPY_EXISTING ~yuanwax.cre~  ~override~
              ~yuaxdin.cre~  ~override~
  ADD_MEMORIZED_SPELL SPIN998 #0 INNATE #1
  ADD_MEMORIZED_SPELL SPIN999 #0 INNATE #1
  BUT_ONLY

ACTION_IF !game_is_iwd THEN BEGIN // how, totlm fixes

  // have spell flagged as already cast
  COPY_EXISTING ~drowpm.cre~   ~override~
    READ_LONG 0x318 "mem_off"
    READ_LONG 0x31c "mem_num"
    FOR (index = 0 ; index < mem_num ; ++index) BEGIN
      WRITE_SHORT (mem_off + 0x08 + (index * 0xc)) 1 // spell memorized not already cast
    END
    BUT_ONLY
  
  // rebuild mem table
  COPY_EXISTING ~dlfznbne.cre~  ~override~
    ADD_MEMORIZED_SPELL SPWI220 #1 WIZARD #1
    ADD_MEMORIZED_SPELL SPWI318 #2 WIZARD #1
    ADD_MEMORIZED_SPELL SPWI404 #3 WIZARD #1
    BUT_ONLY

END

/////                                                  \\\\\
///// creature inventory fixes                         \\\\\
/////                                                  \\\\\

// expiring items
ACTION_FOR_EACH crefile IN kaylessa rshorcb sdgobbw1 sdgobbw2 sdorcbw1 sdorcbw2 skelbow BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%crefile%.cre~ BEGIN

    COPY_EXISTING ~%crefile%.cre~ ~override~
      READ_LONG 0x324 item_off
      READ_LONG 0x328 item_num
      FOR (index = 0 ; index < item_num ; ++index) BEGIN
        WRITE_SHORT (item_off + 0x08 + (index * 0x14)) 0 // no expiry
      END
      BUT_ONLY

  END

END

// weapons exist but not equipped
ACTION_FOR_EACH crefile IN belhif digby dolan doogal gaspar hjollder hjolldrh hrpinf jemel karrl kieran msmcrwl
  orrick pomab purvis purvish quinn rawleigh roald tiernon towniem1 towniem2 towniem3 towniem4 towniem5 whitcomb 
  BEGIN
  
  // equip weapon
  COPY_EXISTING ~%crefile%.cre~ ~override~
    LPF cd_equip_weapon END
    BUT_ONLY IF_EXISTS

END

// have incorrect equipped weapon reference
COPY_EXISTING ~aldwin.cre~   ~override~
  REPLACE_CRE_ITEM ~sw1h08~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP // replaces undroppable sw1h14
  BUT_ONLY
  
COPY_EXISTING ~bearcav.cre~ ~override~ // cave bear
              ~kubear.cre~  ~override~ // cave bear
  LPF cd_equip_item STR_VAR item = "s1-10" slot = "weapon" END // has s1-10 in inventory, no weapon
  BUT_ONLY

COPY_EXISTING ~beholder.cre~ ~override~ // beholder
              ~beholdh.cre~  ~override~ // beholder
              ~mcs1.cre~     ~override~ // crypt servant
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~blank~ END // remove blank item reference
  BUT_ONLY IF_EXISTS // beholders are totlm

// add new construct item to constructs (see cdconstr.itm)
COPY_EXISTING ~bronsen.cre~  ~override~ // bronze sentry
              ~bknight.cre~  ~override~ // black knight
              ~cyanimp1.cre~ ~override~ // animated plate
              ~cyanimp2.cre~ ~override~ // animated plate
              ~cyanimp3.cre~ ~override~ // animated plate
              ~dlsgem.cre~   ~override~ // icasaracht's soul gem
              ~idol.cre~     ~override~ // the idol
              ~keanimph.cre~ ~override~ // animated plate
              ~psentry.cre~  ~override~ // bronze sentry
              ~sentry.cre~   ~override~ // cryshal sentry
              ~tarnsen.cre~  ~override~ // tarnished sentry
              ~voice.cre~    ~override~ // the voice of durdel anatha
              ~wbishop.cre~  ~override~ // white bishop
  ADD_CRE_ITEM ~cdconstr~ #0 #0 #0 ~NONE~ ~LRING RRING AMULET BELT BOOTS GLOVES CLOAK ARMOR HELMET~
  IF_EXISTS
  
COPY_EXISTING ~bronsen.cre~ ~override~ // bronze sentry
              ~psentry.cre~ ~override~ // bronze sentry
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ax1h04 END // already has ax1h01
  BUT_ONLY IF_EXISTS // psentry is how
  
COPY_EXISTING ~es8erth.cre~  ~override~ // earth elemental
              ~es12erth.cre~ ~override~ // earth elemental
              ~es16erth.cre~ ~override~ // earth elemental
              ~es20erth.cre~ ~override~ // earth elemental
              ~es24erth.cre~ ~override~ // earth elemental
              ~es8fire.cre~  ~override~ // fire elemental
              ~es12fire.cre~ ~override~ // fire elemental
              ~es16fire.cre~ ~override~ // fire elemental
              ~es20fire.cre~ ~override~ // fire elemental
              ~es24fire.cre~ ~override~ // fire elemental
              ~es8watr.cre~  ~override~ // water elemental
              ~es12watr.cre~ ~override~ // water elemental
              ~es16watr.cre~ ~override~ // water elemental
              ~es20watr.cre~ ~override~ // water elemental
              ~es24watr.cre~ ~override~ // water elemental
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = immune2 END // delete unassigned immunity item
  BUT_ONLY

COPY_EXISTING ~ettin.cre~ ~override~ // ettin
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = b1-10 END // already has b2-16 equipped
  BUT_ONLY

// rings/amulets
COPY_EXISTING ~ghost.cre~   ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "ghost" slot = "jewelry" END // move from weapon to ring
  BUT_ONLY

// no weapons
COPY_EXISTING ~ghost.cre~    ~override~
              ~highbapt.cre~ ~override~
              ~highritu.cre~ ~override~
              ~highsumm.cre~ ~override~
              ~hightort.cre~ ~override~
              ~yuantip.cre~  ~override~
  ADD_CRE_ITEM ~b1-6~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
  BUT_ONLY
  
// giants need helmets
COPY_EXISTING ~giantfg.cre~  ~override~ // frost giant guard
              ~giantfi2.cre~ ~override~ // fire giant
              ~giantfir.cre~ ~override~ // fire giant
              ~giantfn.cre~  ~override~ // frost giant
              ~giantfro.cre~ ~override~ // frost giant
              ~gorg.cre~     ~override~ // frost giant guard
              ~joril.cre~    ~override~ // joril
  ADD_CRE_ITEM ~helm01~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~HELMET~
  BUT_ONLY
  
COPY_EXISTING ~histach.cre~ ~override~ // histachii
  LPF cd_equip_item STR_VAR item = "ring95" slot = "jewelry" END // assign standard undead ring
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = b1-12 END // already has mum1-12 equipped
  BUT_ONLY

COPY_EXISTING ~kaorog.cre~  ~override~ // Neo Orog, has sw2h01 equipped
              ~kaoroge.cre~ ~override~ // Neo Orog Marauder, has sw2h01 equipped
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~ax1h04~ END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~bow05~  END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~sw1h04~ END
  BUT_ONLY

COPY_EXISTING ~lskarmsw.cre~ ~override~ // armored skeleton, has sw1h04 (iwd) or blun06 (how) eqipped
              ~skarmsw.cre~  ~override~ // armored skeleton, has sw1h04 eqipped
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~ax1h04~ END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~bow05~  END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~sw2h01~ END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~hamm01~ END
  ADD_CRE_ITEM ~ring95~ #0 #0 #0 ~NONE~ ~LRING RRING AMULET~ // add as equipped jewelry
  BUT_ONLY

// have ring in quickslot/inventory
COPY_EXISTING ~malavon.cre~  ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "ringmal" slot = "jewelry" END // move from quickslot to ring
  BUT_ONLY

COPY_EXISTING ~malavons.cre~ ~override~ // malavon (simulacrum)
              ~msk1.cre~     ~override~ // skeleton
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~misc50~ END // remove skull
  BUT_ONLY

COPY_EXISTING ~manticor.cre~ ~override~ // manticor
              ~mordeswd.cre~ ~override~ // mordenkainen's sword
              ~rldmycr.cre~  ~override~ // red myconid, has b4-32 assigned
              ~rudorog.cre~  ~override~ // neo orog avenger, has hq2hswd
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~ax1h04~ END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~bow05~  END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~sw1h04~ END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~sw2h01~ END
  BUT_ONLY
  
// summoned lizardmen using weapons without animations; see ms2lizm.itm
COPY_EXISTING ~ms2lizm.cre~ ~override~ // Lizard Man (monster summoning)
              ~ss1liz3.cre~ ~override~ // Lizard Man (summon shadow monsters)
              ~ss1liz4.cre~ ~override~ // Lizard Man (summon shadow monsters)
              ~ss1liz5.cre~ ~override~ // Lizard Man (summon shadow monsters)
  REPLACE_CRE_ITEM ms2lizm #0 #0 #0 NONE WEAPON1 EQUIP
  BUT_ONLY
  
// summoned tough lizardmen using weapons without animations; see ms3tliz.itm
COPY_EXISTING ~ms3tliz.cre~ ~override~ // Tough Lizard Man (monster summoning)
              ~ss2liz5.cre~ ~override~ // Tough Lizard Man (summon shadow monsters)
              ~ss2liz6.cre~ ~override~ // Tough Lizard Man (summon shadow monsters)
              ~ss2liz7.cre~ ~override~ // Tough Lizard Man (summon shadow monsters)
  REPLACE_CRE_ITEM ms3tliz #0 #0 #0 NONE WEAPON1 EQUIP
  BUT_ONLY

COPY_EXISTING ~ms5jzom.cre~  ~override~ // ju-ju zombie (summon)
              ~ms7mumy.cre~  ~override~ // mummy (summon)
              ~rndstrol.cre~ ~override~ // snow troll (others dont' have immune1)
              ~trollice.cre~ ~override~ // ice troll (others dont' have immune1)
              ~trollsno.cre~ ~override~ // snow troll (others dont' have immune1)
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~immune1~ END // delete immunity item
  BUT_ONLY IF_EXISTS // rndstrol is how
  
// salamander aura visuals
COPY_EXISTING ~ms6salc.cre~ ~override~
  ADD_CRE_ITEM ~CSALRING~ #0 #0 #0 ~NONE~ ~RING~
  BUT_ONLY

// salamander aura visuals  
COPY_EXISTING ~ms6salf.cre~ ~override~
  ADD_CRE_ITEM ~FSALRING~ #0 #0 #0 ~NONE~ ~RING~
  BUT_ONLY

COPY_EXISTING ~orcsham.cre~ ~override~ // orc shaman
  LPF cd_equip_item STR_VAR item = "misc07" slot = "inventory" END // move gold to inventory
  BUT_ONLY

// has armor in cloak/quickslots/inventory
COPY_EXISTING ~orrick.cre~  ~override~
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "clck16" slot = "armor" END // move from cloak to armor
  REPLACE_CRE_ITEM ~wand04~ #10 #0 #0 ~NONE~ ~QITEM2~ // add back with charges
  BUT_ONLY

// add charges
COPY_EXISTING ~oswald.cre~ ~override~
  REPLACE_CRE_ITEM ~amul01~ #5 #0 #0 ~NONE~ ~AMULET~ // add back with charges
  LPF cd_equip_item STR_VAR item = "potn13" END // has 3/5 potions assigned
  BUT_ONLY

COPY_EXISTING ~sdelfbw1.cre~  ~override~ // shadowed elven archer
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~p1-4~ END // already has shlgbow2 equipped
  BUT_ONLY

// equip sword
COPY_EXISTING ~shadow.cre~ ~override~
  REPLACE_CRE_ITEM ~shadow1~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP
  LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "immune1" slot = "jewelry" END // move from quickslot to ring
  BUT_ONLY

// add transparency to buff shadows
COPY_EXISTING ~shadowb.cre~ ~override~
  LPF cd_equip_item STR_VAR item = "ring95" slot = "jewelry" END // assign standard undead ring
  ADD_CRE_ITEM ~trans~ #0 #0 #0 ~NONE~ ~LRING RRING AMULET~ // add as ring/amulet
  BUT_ONLY

COPY_EXISTING ~shelfsw3.cre~  ~override~ // shadowed elven swordsman
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~shcomlb2~ END // already has shlswrd2 equipped
  BUT_ONLY

// assign fsalring item
COPY_EXISTING ~shikata.cre~  ~override~
  LPF cd_equip_item INT_VAR STR_VAR item = "fsalring" slot = "jewelry" END // assign missing cosmetics
  BUT_ONLY

COPY_EXISTING ~svirfneb.cre~ ~override~ // Svirfneblin
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~ax1h04~ END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~bow05~  END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~sw1h04~ END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~sw2h01~ END
  LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~hamm01~ END
  BUT_ONLY

COPY_EXISTING ~tanarri.cre~ ~override~ // yxunomei
  LPF cd_equip_item STR_VAR item = "potn14" slot = quickslot END // not assigned
  BUT_ONLY

ACTION_IF !game_is_iwd THEN BEGIN

  // has armor in cloak/quickslots/inventory
  COPY_EXISTING ~alpheus.cre~  ~override~
                ~alpheusd.cre~ ~override~
                ~presapp.cre~  ~override~
    LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "clck10" slot = "armor" END // move from cloak to armor
    BUT_ONLY

  // have ring in quickslot/inventory
  COPY_EXISTING ~alpheusd.cre~  ~override~
    LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "lwring" slot = "jewelry" END // move from inventory to ring
    BUT_ONLY

  // has shield in armor slot, armor not assigned
  COPY_EXISTING ~angaar.cre~  ~override~
                ~beornen.cre~ ~override~
    LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "shld03" slot = "shield" END // move shield from armor to shield slot...
    LPF cd_equip_item STR_VAR item = "chan01" slot = "armor" END // ...so that we can add unassigned armor to slot
    BUT_ONLY

  // two ice golems use alternate weapons, so we just add the regular weapon in a slot to provide poison/disease immunity
  COPY_EXISTING ~dlgolem.cre~ ~override~ // ice golem sentry
                ~sagolem.cre~ ~override~ // ice golem sentry
    ADD_CRE_ITEM ~icegolem~ #0 #0 #0 ~NONE~ ~LRING RRING AMULET BELT BOOTS GLOVES CLOAK ARMOR HELMET~

  // have ring in quickslot/inventory
  COPY_EXISTING ~dlsgem.cre~  ~override~
    LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "ring91" slot = "jewelry" END // move from quickslot to ring
    BUT_ONLY

  // one snow troll missing item for knockdown/revive sequence
  COPY_EXISTING ~dlsnotr.cre~ ~override~
    ADD_CRE_ITEM ~reg1hp2~ #0 #0 #0 NONE ~LRING RRING~ // missing don't die item, which might help
    BUT_ONLY

  COPY_EXISTING ~druidee.cre~ ~override~ // druid earth elemental transformation
                ~druidwe.cre~ ~override~ // druid water elemental transformation
    LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = immune2 END // delete unassigned immunity item
    LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = b4-32 END   // already has elemental weapon
    BUT_ONLY
    
  COPY_EXISTING ~druidfe.cre~ ~override~ // druid fire elemental transformation
    LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = immune2 END // delete unassigned immunity item
    LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = fele1-8 END // already has elemental weapon
    BUT_ONLY

  // keep mebdinga alive until she speaks 1/2; see dmebd.dlg
  COPY_EXISTING ~mebding.cre~ ~override~
    ADD_CRE_ITEM ~min1hp~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~AMULET~
    BUT_ONLY

  // move and equip sword
  COPY_EXISTING ~murdaugh.cre~ ~override~
    LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "ulswd3d" slot = "weapon" END // move from quickslot to weapon
    LPF cd_equip_weapon END
    BUT_ONLY

  // has armor in cloak/quickslots/inventory
  COPY_EXISTING ~saeguard.cre~ ~override~
    LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "shark" slot = "armor" END // move from cloak to armor
    BUT_ONLY

  // has armor in cloak/quickslots/inventory
  COPY_EXISTING ~sagrdm.cre~   ~override~ // equippable items in inventory: shark2.itm
                ~sarguard.cre~ ~override~ // Equippable items in inventory: shark2.itm
    LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "shark2" slot = "armor" END // move from cloak/quickslot to armor
    BUT_ONLY

  // have incorrect equipped weapon reference
  COPY_EXISTING ~sawar.cre~   ~override~
    LPF cd_equip_weapon END // has s1-4 in weapon2 slot, but weapon1 is selected
    BUT_ONLY

  // add charges
  COPY_EXISTING ~seercut.cre~ ~override~
    REPLACE_CRE_ITEM ~aport~ #1 #0 #0 ~NONE~ ~QITEM1~ // add back with charges
    REPLACE_CRE_ITEM ~adis~  #1 #0 #0 ~NONE~ ~QITEM2~ // add back with charges
    BUT_ONLY
  
  // add charges
  COPY_EXISTING ~tiernon.cre~ ~override~
    REPLACE_CRE_ITEM ~tiernon~ #1 #1 #3 ~NONE~ ~INV7~ // add back with charges
    BUT_ONLY

  // has armor in cloak/quickslots/inventory
  COPY_EXISTING ~vaarglan.cre~ ~override~
                ~vaarglnd.cre~ ~override~
    LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "clck16" slot = "armor" END // move from cloak to armor
    BUT_ONLY
  
  // have ring in quickslot/inventory
  COPY_EXISTING ~vexing.cre~ ~override~
    LPF cd_equip_item STR_VAR item = "immune2" slot = "belt" END // jewelry already full
    BUT_ONLY
  
  // have ring in quickslot/inventory
  COPY_EXISTING ~werewlf.cre~ ~override~
    LPF cd_equip_item STR_VAR item = "immune1" slot = "jewelry" END // assign to jewelry
    BUT_ONLY

  // has armor in cloak/quickslots/inventory
  COPY_EXISTING ~xactile.cre~  ~override~ // Equippable items in inventory: SHARK3.itm
    LPF DELETE_CRE_ITEM_V9 STR_VAR item_to_delete = ~s1-8~ END // remove generic weapon
    LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "blun05" slot = "weapon" END // move from quickslot to weapon
    LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "shark3" slot = "armor" END // move from cloak to armor
    BUT_ONLY

  ACTION_IF game_is_totlm THEN BEGIN // totlm fixes
  
    // equip item
    COPY_EXISTING ~corcat.cre~ ~override~
      LPF cd_equip_item STR_VAR item = "spawn" slot = "quickslot" END // assign to jewelry
      BUT_ONLY
      
    // have ring in quickslot/inventory
    COPY_EXISTING ~deddog.cre~  ~override~
                  ~hrpinf.cre~  ~override~ //: equippable items in inventory: immune2.itm
                  ~hrpmat.cre~  ~override~ //: equippable items in inventory: immune2.itm
                  ~jkldog.cre~  ~override~ //: equippable items in inventory: immune2.itm
                  ~jklgtr.cre~  ~override~ //: equippable items in inventory: immune2.itm
                  ~rikasha.cre~ ~override~ //: equippable items in inventory: immune2.itm
      LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "immune2" slot = "jewelry" END // move from inventory to equipped
      BUT_ONLY
    
    // add item referenced in scripting
    COPY_EXISTING ~gbard.cre~ ~override~
      REPLACE_CRE_ITEM ~spawn~ #0 #0 #0 ~NONE~ ~QITEM1~ // replace unused ciigen with spawn (used in scripts)
      BUT_ONLY
  
    // xbow but no bolts
    COPY_EXISTING ~hobart.cre~  ~override~
                  ~hobart2.cre~ ~override~
                  ~hobarth.cre~ ~override~
                  ~hoggle.cre~  ~override~
      ADD_CRE_ITEM ~bolt01~ #20 #0 #0 ~NONE~ ~QUIVER1 QUIVER2 QUIVER3~
    
    // equip item
    COPY_EXISTING ~hobartf.cre~  ~override~
                  ~lordm.cre~    ~override~
                  ~rakshasa.cre~ ~override~
                  ~rakshinv.cre~ ~override~
      LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "ciraksh" slot = "jewelry" END // move from quickslot to jewelry
      BUT_ONLY
  
    // have ring in quickslot/inventory
    COPY_EXISTING ~hrpfnd.cre~ ~override~ //: equippable items in inventory: immune1.itm
                  ~jklwar.cre~ ~override~ //: equippable items in inventory: immune1.itm
      LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "immune1" slot = "jewelry" END // move from quickslot to jewelry
      BUT_ONLY
    
    // have ring in quickslot/inventory
    COPY_EXISTING ~jklldr.cre~ ~override~ //: equippable items in inventory: immune3.itm
      LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "immune3" slot = "jewelry" END // move from quickslot to jewelry
      BUT_ONLY

    // equip item
    COPY_EXISTING ~olvslime.cre~ ~override~
                  ~rndoslm.cre~  ~override~
      LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "ciigen" slot = "jewelry" END // move from inventory to jewelry
      BUT_ONLY

    // equip item (see also ar9714.are, rndrevn.cre, cdtrolg.bcs)
    COPY_EXISTING ~revnant.cre~ ~override~
                  ~rndrevn.cre~ ~override~
      LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "cireve" slot = "jewelry" END // move from inventory to jewelry
      BUT_ONLY
  
    // rings/amulets
    COPY_EXISTING ~rndghos.cre~ ~override~
      LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "ghost" slot = "jewelry" END // move from weapon to jewelry
      ADD_CRE_ITEM    ~b1-6~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP // so we can assign actual weapon
      BUT_ONLY

    // equip items
    COPY_EXISTING ~trgbard.cre~  ~override~
      LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "ciigen" slot = "gloves" END // move from quickslot to gloves (jewelry slots full)
      LPF cd_equip_item INT_VAR move = 1 STR_VAR item = "seeall" slot = "boots" END  // move from quickslot to boots (jewelry slots full)
      BUT_ONLY
  
  END

END

/////                                                  \\\\\
///// misc creature fixes                              \\\\\
/////                                                  \\\\\

ACTION_FOR_EACH summon IN // no xp, gold, corpse, or items from summoned creatures
  ad1sklm ad2sklm ad2sklr ad2zomb ad3sklm ad3sklr ad3zomb ad4sklr ad4zomb ad5zomb as1bear as1wolf 
  as2cave as2dire as3polr as3wint berserk bknight es12erth es12fire es16erth es16fire es20erth 
  es20fire es24erth es24fire es8erth es8fire esmerth gisbomb gisborb lghast lghoul lm1hwaxy lm1hwmsy 
  lm1hwpoy lm2hwlay lm2hwnay lm2hwtey lskarmms lskarmsw lwight lzombie lzombiej ms1fb ms1goba 
  ms1gobm ms2bbtl ms2gobb ms2gobe ms2lizm ms3borb ms3ghl ms3hspi ms3orog ms3tliz ms4ghst ms4ogr 
  ms4yeti ms5gspi ms5jzom ms5mino ms6crwl ms6pspi ms6salc ms6salf ms7bgrd ms7umbh msmcrwl msmpspi 
  msmsalc msmsalf ss1gob1 ss1gob2 ss1gob3 ss1liz3 ss1liz4 ss1trl6 ss1trl7 ss1trl8 ss2gob1 ss2gob2 
  ss2gob3 ss2liz5 ss2liz6 ss2liz7 ss3trl7 ss3trl8 ss3umb8 ss3umb9 sshambsumdoom sumshad sumstlk trolly 
BEGIN

  COPY_EXISTING ~%summon%.cre~ ~override~
    WRITE_LONG 0x14 0 // no xp
    WRITE_LONG 0x1c 0 // no gold
    READ_LONG 0x324 item_off 
    READ_LONG 0x328 item_num
    WRITE_BYTE 0x10 (THIS BOR BIT1) // set bit 1 (no corpse)
    FOR (index = 0 ; index < item_num ; ++index) BEGIN
      WRITE_LONG (item_off + 0x10 + (index * 0x14)) (THIS BOR BIT3) // adds undroppable flag
    END
    BUT_ONLY IF_EXISTS
  
END

// mismatched ACs; map natural onto effective
ACTION_FOR_EACH file IN
  aldwin alpheus angaar beornen conlan digby dolan doogal emmerich emmrchh hjollder hjolldrh ilmadia iquinva jemel karrl kieran
  mercthf mercwar1 mercwar2 murdaugh orrick pshar purvis purvish quinn rawleigh roald skelserr vaarglan vexing whitcomb
BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN

    COPY_EXISTING ~%file%.cre~  ~override~
      READ_SHORT  0x46 ac // natural ac
      WRITE_SHORT 0x48 ac // effective ac
      BUT_ONLY
  END

END

// tough lizerdmen should still be tough in their tooltips
COPY_EXISTING ~belhif.cre~   ~override~ // also fix Belhifet's tooltip while we're here
              ~lm2hwla.cre~  ~override~
              ~lm2hwlay.cre~ ~override~
              ~lm2hwna.cre~  ~override~
              ~lm2hwnay.cre~ ~override~
              ~lm2hwte.cre~  ~override~
              ~lm2hwtey.cre~ ~override~
              ~ms3tliz.cre~  ~override~
              ~rdeliztl.cre~ ~override~
              ~ss2liz5.cre~  ~override~
              ~ss2liz6.cre~  ~override~
              ~ss2liz7.cre~  ~override~
  READ_LONG  0x08 name
  WRITE_LONG 0x0c name
  BUT_ONLY

// remove gold
COPY_EXISTING ~biknight.cre~ ~override~
              ~pomimg.cre~   ~override~
  WRITE_LONG 0x1C 0 // gold
  BUT_ONLY

// add generic override script for PCs
COPY_EXISTING ~charbase.cre~ ~override~
  WRITE_ASCII 0x248 cdplayer

// allegiance fix
COPY_EXISTING ~dver.cre~ ~override~
  WRITE_BYTE 0x2d8 128 // allegiance
  BUT_ONLY

// coloring changes
COPY_EXISTING ~ecogre.cre~ ~override~
              ~ghereg.cre~ ~override~
              ~ms4ogr.cre~ ~override~
              ~ogre.cre~   ~override~
  WRITE_BYTE 0x2f 16 // skin color
  BUT_ONLY

// generic dlg file if non-hostile
COPY_EXISTING ~giantfi2.cre~ ~override~
              ~giantfir.cre~ ~override~
              ~orc2004.cre~  ~override~
              ~orca2004.cre~ ~override~
              ~uhlk8010.cre~ ~override~
  WRITE_ASCII 0x334 ~DGENMOND~ // dialogue
  BUT_ONLY

// maiden ilmadia shouldn't have 1 HD
COPY_EXISTING ~ilmadia.cre~ ~override~
  WRITE_BYTE 0x234 12
  BUT_ONLY

// coloring changes
COPY_EXISTING ~iquinva.cre~ ~override~
  WRITE_BYTE 0x2e 66 // major color

// allegiance, invalid XP
COPY_EXISTING ~kalabac.cre~ ~override~
  WRITE_LONG 0x014   0 // orig XP was -10
  WRITE_BYTE 0x2d8 128 // allegiance
  BUT_ONLY

// coloring changes
COPY_EXISTING ~kaylessa.cre~ ~override~
  WRITE_BYTE 0x2c 27 // metal color

// wrong avatar
COPY_EXISTING ~ktgst1.cre~ ~override~
  WRITE_SHORT 0x28 58136 // animation

// coloring changes
COPY_EXISTING ~lehland.cre~  ~override~
              ~sdelfwz1.cre~ ~override~
              ~sdogre.cre~   ~override~
              ~shdelfpt.cre~ ~override~
  WRITE_BYTE 0x2f 105 // skin color
  BUT_ONLY

// add perm corpse flag so party sees bodies
COPY_EXISTING ~lizdead.cre~  ~override~
              ~talodead.cre~ ~override~
  WRITE_BYTE 0x10 THIS | 4
  BUT_ONLY

// coloring changes
COPY_EXISTING ~lysanbar.cre~ ~override~
  WRITE_BYTE 0x2D 37
  BUT_ONLY

// coloring changes
COPY_EXISTING ~malavons.cre~ ~override~
  WRITE_BYTE 0x2E 40
  BUT_ONLY
  
// frost salamanders missing fire vulnerability
COPY_EXISTING ~ms6salc.cre~ ~override~
              ~msmsalc.cre~ ~override~
              ~sal8013.cre~ ~override~
  WRITE_BYTE 0x59 "-50"
  WRITE_BYTE 0x5e "-50"
  BUT_ONLY

// another resistance fix
COPY_EXISTING ~msmsalc.cre~ ~override~
  WRITE_BYTE 0x5a 100 // cold reistance for frost sala
  BUT_ONLY

// neo orog generals shouldn't set chief_dead var; interferes with saablic tan
COPY_EXISTING ~neoorogg.cre~ ~override~
  WRITE_ASCII 0x27e ~~ #32
  BUT_ONLY

// missing name
COPY_EXISTING ~wbishop.cre~ ~override~
  WRITE_LONG 0x08 17753
  BUT_ONLY

ACTION_IF !game_is_iwd THEN BEGIN // how, totlm fixes
  
  // standardize to 100
  COPY_EXISTING ~cyshamb.cre~ ~override~ // shambling mound
                ~shmblr.cre~  ~override~ // shambling mound
                ~sshamb.cre~  ~override~ // shambling mound
    WRITE_BYTE 0x5b 100 // resist electricity
    BUT_ONLY IF_EXISTS // only sshamb in how
  
  // standardize to 100
  COPY_EXISTING ~cyiceg.cre~   ~override~ // ice golem sentry
                ~dlgolem.cre~  ~override~ // ice golem sentry
                ~golemic.cre~  ~override~ // ice golem sentry
                ~rndigolm.cre~ ~override~ // ice golem sentry
                ~sagolem.cre~  ~override~ // ice golem sentry
    WRITE_BYTE 0x5a 100 // resist cold
    WRITE_BYTE 0x5f 100 // resist magic cold
    BUT_ONLY IF_EXISTS // cyiceg is totlm

  // all other resists 100
  COPY_EXISTING ~copspir.cre~  ~override~ // planar spirit
    WRITE_BYTE 0x5d 100 // resist magic
    BUT_ONLY
  
  // should start ENEMY like her companions
  COPY_EXISTING ~pshar.cre~ ~override~
    WRITE_BYTE 0x2d8 255 // ENEMY
    BUT_ONLY
 
  // no gold
  COPY_EXISTING ~tiernon.cre~  ~override~
    WRITE_LONG 0x1C 0 // gold
    BUT_ONLY

  ACTION_IF game_is_totlm THEN BEGIN // totlm fixes

    // equip item (see also ar9714.are, rndrevn.cre, revnant.cre, cdtrolg.bcs)
    COPY_EXISTING ~rndrevn.cre~ ~override~
      WRITE_ASCII 0x250 ~cdtrolg~ #8
      BUT_ONLY
  
    // all spectral X shouldn't have corpses
    COPY_EXISTING ~spcook.cre~   ~override~
                  ~spcourtf.cre~ ~override~
                  ~spcourtm.cre~ ~override~
      WRITE_BYTE 0x10 THIS | 2
      BUT_ONLY
    
    // no gold
    COPY_EXISTING ~stnnui.cre~   ~override~
      WRITE_LONG 0x1C 0 // no gold
      BUT_ONLY
  
  END

END

/////                                                  \\\\\
///// mass item fixes                                  \\\\\
/////                                                  \\\\\

// gender, sex, alignment, general, race, class fixes
INCLUDE ~iwdfixpack/lib/item.tpa~

OUTER_SPRINT text_match_speed @134

// file, magical, enchant, weight, price, mspeed, prof => file
ACTION_PHP_EACH cd_iwdfixpack_item AS params => file BEGIN

  COPY_EXISTING ~%file%.itm~ ~override~
    PATCH_IF (IS_AN_INT params_1) BEGIN
      PATCH_IF (params_1 = 0)   BEGIN WRITE_BYTE 0x18 THIS & `BIT6 END // remove magical flag
      PATCH_IF (params_1 = 1)   BEGIN WRITE_BYTE 0x18 THIS | BIT6  END // add magical flag
    END  
    PATCH_IF (IS_AN_INT params_2) BEGIN WRITE_LONG 0x60 params_2     END // enchanment
    PATCH_IF (IS_AN_INT params_3) BEGIN WRITE_LONG 0x4c params_3     END // weight
    PATCH_IF (IS_AN_INT params_4) BEGIN WRITE_LONG 0x34 params_4     END // price
    PATCH_IF (IS_AN_INT params_6) BEGIN WRITE_SHORT 0x1c params_6    END // proficiency
    PATCH_IF (IS_AN_INT params_5) BEGIN LPF cd_weapon_speed INT_VAR speed = params_5 END END // weapon speed
    BUT_ONLY IF_EXISTS

END   

// global item effects not being applied due to incorrect timing mode
// all of these are for cosmetic effects, except:
// aldeth (immunity to effect and string)
// ciigen (imunity to panic)
// immagmsl (immunity to magic missile)
// ring95 & stupid (immunity to petrification)
// ubswd5b (fire resistance)
// xbow02 (attacks per round)
ACTION_FOR_EACH file IN
  aldeth ax1h04 ax1h05 ax1h06 axlizman axyuanti bclaw bess bloodgf blun01 blun04 blun06 blyuanti bow02
  bow05 bow06 bow08 bow09 bow99 bownon bring cattac1 cibosst cifade ciigen clown corny csalring dagg04
  dagg05 daggshit dazer debian fayr fistgf fsalring gasp ghost ghost2 gsleep halb01 halbrd01 hamm01
  handgf helm03 helm04 helm07 hq2hswd hqhalb hqhxbow hqmace hqmstar hqsbow iax1h01 iblun04 ibow03
  ihamm01 immagmsl immune1 immune2 immune3 kresssw labelt lalizman lucky maul mslizman nalizman
  nomagic peaceke pikeman polizman ranclub ring05 ring09 ring91 ring95 sash sbowebu sceptre shade
  shadows shcomlb2 stone stupid sw1h08 sw1h14 sw1h99 sw2h01 sw2h01b sw2h02 sw2h05 swyuanti telizman
  tongue u2ham2a u2ham3a u2ham4a u2ham4b u2ham5a ubswd5b uhxbw2a usswd2a usswd2b usswd3a usswd3b
  usswd3c usswd4a usswd4b usswd5a usswd5b utswd1a utswd2a utswd2b utswd2c utswd3a utswd3b utswd4a
  utswd4b utswd5a utswd5b wclub wolfwi2 xbow01 xbow02 xbow03 xclub xu2ham3 xusswd3 zzs6sc
BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~%file%.itm~) THEN BEGIN

    COPY_EXISTING ~%file%.itm~ ~override~
      LPF ALTER_EFFECT INT_VAR check_headers = 0 timing = 2 power = 0 END
      BUT_ONLY

  END

END

// global effects can be disspelled
ACTION_FOR_EACH file IN aldeth bclaw labelt lucky misc72 shadows tongue BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~%file%.itm~) THEN BEGIN

    COPY_EXISTING ~%file%.itm~ ~override~
      LPF ALTER_EFFECT INT_VAR check_headers = 0 resist_dispel = 0 END
      BUT_ONLY

  END

END

// set regular item effects to 0 power
ACTION_FOR_EACH file IN
  arow06 arow08 arow09 arow15 ax1h06 behwep bolt03 bolt05 clck07 clck08 cynicis dart03 dart04 dbolt
  dntshd2 dobone fbolt gasp hamm03 jhoswd3 poq2-16 potn13 potn26 potn27 revent1 ring20 schlum1
  scrl03 scrl04 scrl05 scrl06 scrl08 scrl09 scrl15 shadless shamme1 shamme2 shamme3 shille
  shillel sirine sw1h06 sw1h11 tiernon ubull4a vampire wisp wolfwi1
BEGIN

  COPY_EXISTING ~%file%.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_target = 2 power = 0 END
    BUT_ONLY IF_EXISTS

END

// remove durations from permanent effects
ACTION_FOR_EACH file IN
  acidooz4 bknight cwreve extheal gberry goodber ipotn08 jellgr1 potn08 potn13 potn33 umstr2a
  umstr3a umstr3c umstr4b umstr5a vexed2 wand05 wand4ca zzj6sp
BEGIN

  COPY_EXISTING ~%file%.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_timing = 1 duration = 0 END
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_timing = 9 duration = 0 END
    BUT_ONLY IF_EXISTS

END

// identify prior to use
ACTION_FOR_EACH file IN
  amulbra beltbon bishop bknight blrdeck boneam bootman braceip cone helmsh holding jasper labelt mantlecs
  mirror2 moonbow ogien revenan ring03 rogue sring stomper vexed2 vexed3 virgin xain
BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~%file%.itm~) THEN BEGIN

    COPY_EXISTING ~%file%.itm~ ~override~
      LPF ALTER_ITEM_HEADER INT_VAR identify = 1 type = 3 END
      BUT_ONLY
  END

END

// items using bg timing (round = 6) instead of iwd (round = 7)
ACTION_FOR_EACH file IN
  dazer potn02 potn03 potn04 potn05 potn06 potn07 potn09 potn11 potn12 potn14 potn18 potn19 potn21
  potn22 potn23 potn24 potn28 potn29 potn30 potn31 potn33 potn35 potn38 potn41 potn42 potn44 potn45
  potn46 potn48 revent1 ring03 scrl07 shadow1 sirine sirine1 spidhu1 spidwr1 u1hax4a u2hax5a ulswd3b 
  ulswd5b umstr2a umstr4b usswd3a utswd3a vampire wand04 wand08 wand10 wolfva1 xclub
BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~%file%.itm~) THEN BEGIN

    COPY_EXISTING ~%file%.itm~ ~override~
      LPF CONVERT_BG_IWD_DURATION END
      BUT_ONLY
  END
 
END

// extraneous unknown (0) headers
ACTION_FOR_EACH potion IN potn27 potn28 potn29 potn30 potn31 potn32 potn33 potn34 potn35 potn36 potn37 potn38 potn39
  potn40 potn41 potn42 potn43 potn44 potn45 potn46 ption2k ption2l ption2m ption2n ption41 uspot1a uspot2a uspot2b
  uspot3a uspot3b uspot3c uspot4a uspot4b uspot4b uspot4c uspot5a uspot5b BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%potion%.itm~ BEGIN
    
    COPY_EXISTING ~%potion%.itm~ ~override~
      LPF DELETE_ITEM_HEADER END
      BUT_ONLY

  END

END

ACTION_FOR_EACH item IN // remove breakable flags
  2haxe amaunat auril ax1h01 axemino axlizman axyuanti bael bess biron biteme blckswd bloodgf 
  blun02 blun04 blun08 blyuanti cairn cattac1 celebra cynicis dagg01 dagg06 dagg07 dagg08 
  daggshit days diver doves ehdag erevain fanggf fayr firekis fistgf fkiller flailsk gsleep 
  gullwyn halb01 halbrd01 hamm01 handgf hellpik hq2hswd hqaxe hqbswrd hqdagg hqhalb hqhamm 
  hqlswrd hqmace hqsswrd iax1h01 iblun04 ihamm01 intrces isw1h07 j2haxe jhoswd2 jhoswd3 
  kinetic kissgf kreswrd kris lalizman lonesom longclev lshand mae maul mslizman myrloch 
  nalizman nymdagg peasrew pikeman poker polizman power ranax ranbswd reaver redemt relian 
  serrate shlswrd2 shmace2 shsswrd slayer sper01 sper04 sw1h01 sw1h04 sw1h07 sw1h12 sw1h17 
  sw2h01 sw2h01b sw2h05 swyuanti tasloiil telizman tomb tonggf tongue turodah u1hax1a u1hax2a 
  u1hax3a u1hax3b u1hax4a u1hax4b u2ham2a u2ham3a u2ham4a u2ham4b u2ham5a u2hax1a u2hax2a 
  u2hax3a u2hax3b u2hax4a u2hax4b u2hax5a ubswd1a ubswd2a ubswd2b ubswd3a ubswd3b ubswd4a 
  ubswd4b ubswd4c ubswd5a ubswd5b udagg1a udagg1b udagg2a udagg2b udagg3a udagg3b udagg3c 
  udagg4a udagg4b udagg5a ulswd1a wsword xu2ham3 xvartil young zz14in zz36dgd zz57pj zzc8hb 
  zze6pe zzg7ts zzj6sp zzn6gc zzu6cs zzz6bc 
BEGIN

  COPY_EXISTING ~%item%.itm~ ~override~ // Hell's Bane +4
    LPF ALTER_ITEM_HEADER INT_VAR flag_break = 0 END // should not be flagged as breakable
    BUT_ONLY IF_EXISTS

END

// adding portrait icons
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_item_icons BEGIN
  auril   =>  35 // cursed, breath of auril
  biteme  =>  16 // fire resistance, Spear of Kerish
  boot03  =>  25 // cold resistance, boots of north
  boot05  =>  17 // electrical resistance, boots of grounding
  brac06  =>  21 // strength, guantlets of ogre strength
  bwtalis =>  25 // cold resistance, black wolf talisman
  clck09  =>  25 // cold resistance, robe of cold resistance
  clck10  =>  16 // fire resistance, robe of fire resistance
  clck11  =>  27 // electrical resistance, robe of electrical resistance
  dragarm =>  24 // acid resistance, black dragon scale
  elfboot =>  25 // cold resistance, elven sewn boots
  elfclck =>  25 // cold resistance, elven sewn cloak
  elfglov =>  25 // cold resistance, elven sewn gloves
  erevain =>  24 // acid resistance, erevain's broad sword
  kresssw =>  25 // cold resistance, kresselack's sword
  kreswrd =>  25 // cold resistance, kresselack's sword
  mourner =>  35 // cursed, mourner's armor
  nymshld =>  16 // fire resistance, nym's rhino beetle shield
  ogi     =>  21 // strength, ogi-luc's great robe
  ring04  =>  35 // cursed, ring of clumsiness
  sbowebu =>  25 // cold resistance, short bow of ebullience
  tongue  =>  16 // fire resistance, salamander's tongue
  u2ham5a =>  16 // fire resistance, demon's breath
  ubswd4c =>  16 // fire resistance, conflagaration
  ubswd5b =>  16 // fire resistance, incinerator
  udagg4b =>  16 // fire resistance, mage dagger +3
  ulswd3a =>  16 // fire resistance, flaming long sword +2
  uring3a =>  16 // fire resistance, ring of resistance
  ushld3a =>  16 // fire resistance, reinforced large shield +2
  vexed   =>  25 // cursed, vexed
  zzc8hb  =>  16 // fire resistance, hell's bane
  zzf6al  =>  16 // fire resistance, alamion
END

ACTION_PHP_EACH cd_item_icons AS item => icon BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME "%item%.itm" BEGIN

    COPY_EXISTING "%item%.itm" override
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = icon timing = 2 END
      BUT_ONLY

  END

END

// stuff needs fire & cold portrait icons
ACTION_FOR_EACH item IN bloodgf fanggf fistgf handgf king kissgf tonggf BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME "%item%.itm" BEGIN

    COPY_EXISTING "%item%.itm" override
      PATCH_FOR_EACH icon IN 25 16 BEGIN
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = icon timing = 2 END
      END
      BUT_ONLY

  END

END

// potions should bypass user's MR and spell protections; skipping potions that target non-user, e.g. potion of explosions
COPY_EXISTING ~adisease.itm~ ~override~ // mummy's tea
//            ~coffee.itm~   ~override~ // berduskan black brew
              ~elfwine.itm~  ~override~ // elven healing wine
              ~extheal.itm~  ~override~ // potion of extra healing
              ~philter.itm~  ~override~ // philter of purification
              ~pnull.itm~    ~override~ // oil of null effect
              ~potn02.itm~   ~override~ // potion of fire resistance
              ~potn03.itm~   ~override~ // potion of hill giant strength
              ~potn04.itm~   ~override~ // potion of frost giant strength
              ~potn05.itm~   ~override~ // potion of fire giant strength
              ~potn06.itm~   ~override~ // potion of cloud giant strength
              ~potn07.itm~   ~override~ // potion of storm giant strength
//            ~potn08.itm~   ~override~ // potion of healing
              ~potn09.itm~   ~override~ // potion of heroism
              ~potn10.itm~   ~override~ // potion of invisibility
              ~potn11.itm~   ~override~ // potion of invulnerability
              ~potn12.itm~   ~override~ // potion of stone giant strength
              ~potn14.itm~   ~override~ // oil of speed
              ~potn15.itm~   ~override~ // red potion
              ~potn16.itm~   ~override~ // violet potion
              ~potn17.itm~   ~override~ // elixir of health
              ~potn18.itm~   ~override~ // potion of absorption 
              ~potn19.itm~   ~override~ // potion of agility
              ~potn20.itm~   ~override~ // antidote
              ~potn21.itm~   ~override~ // potion of clarity
              ~potn22.itm~   ~override~ // potion of cold resistance
              ~potn23.itm~   ~override~ // oil of speed
              ~potn24.itm~   ~override~ // potion of defense
              ~potn25.itm~   ~override~ // potion of healing
              ~potn28.itm~   ~override~ // potion of fortitude
              ~potn29.itm~   ~override~ // potion of genius
              ~potn30.itm~   ~override~ // potion of infravision
              ~potn31.itm~   ~override~ // potion of insulation
              ~potn32.itm~   ~override~ // antidote
              ~potn33.itm~   ~override~ // potion of magic blocking
              ~potn34.itm~   ~override~ // potion of magic protection
              ~potn35.itm~   ~override~ // potion of magic shielding
              ~potn36.itm~   ~override~ // potion of master thievery
              ~potn37.itm~   ~override~ // potion of mind focusing
              ~potn38.itm~   ~override~ // potion of mirrored eyes
              ~potn39.itm~   ~override~ // potion of perception
              ~potn40.itm~   ~override~ // potion of invulnerability
              ~potn41.itm~   ~override~ // potion of power
              ~potn42.itm~   ~override~ // potion of regeneration 
              ~potn43.itm~   ~override~ // potion of insight 
              ~potn44.itm~   ~override~ // potion of strength
              ~potn45.itm~   ~override~ // potion of freedom
              ~potn46.itm~   ~override~ // potion of stone form
              ~potnclr1.itm~ ~override~ // potion of clear purpose
  LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_resist_dispel = 1 resist_dispel = 3 power = 0 END
  BUT_ONLY

/////                                                  \\\\\
///// usability fixes                                  \\\\\
/////                                                  \\\\\

// cleric-only spell scrolls
ACTION_FOR_EACH item IN
  scrl58 scrl59 scrl62 scrl63 sppr112x sppr201c sppr203c sppr208c sppr211c sppr212c sppr218x
  sppr303c sppr304c sppr307c sppr308c sppr313c sppr314c sppr315x sppr316x sppr319x sppr324x
  sppr325x sppr404c sppr415x sppr416x sppr417x sppr418x sppr419x sppr504c sppr513x sppr514x
  sppr515x sppr516x sppr518x sppr610x sppr611x sppr612x sppr712c sppr714c sppr718x sppr721x 
  sppr722x
BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME "%item%.itm" BEGIN

    COPY_EXISTING "%item%.itm" override
      WRITE_BYTE 0x1e THIS | BIT6       // unusable by bard
      WRITE_BYTE 0x1e THIS & `BIT7      // usable by cleric
      WRITE_BYTE 0x1f 0b00111000        // usable by cm, ct, cr, fc, fmc; unusable by f, fd, fm
      WRITE_BYTE 0x20 THIS | 0b01111111 // leave elf alone, otherwise unusable
      WRITE_BYTE 0x21 THIS | BIT6       // unusable by druid
      BUT_ONLY

  END

END

// druid-only spell scrolls
ACTION_FOR_EACH item IN
  sppr105c sppr113x sppr216x sppr217x sppr318x sppr320x sppr321x sppr322x sppr323x sppr412c
  sppr420x sppr421x sppr422x sppr423x sppr510c sppr512c sppr517x sppr606c sppr608c sppr613x
  sppr704c sppr705c sppr707c sppr719x sppr720x
BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME "%item%.itm" BEGIN

    COPY_EXISTING "%item%.itm" override
      WRITE_BYTE 0x1e THIS | 0b11000000 // unusable by bard, cleric
      WRITE_BYTE 0x1f 0b11101011        // usable by d, fd; unusable by cm, ct, f, fm, fc, fmc
      WRITE_BYTE 0x20 THIS | 0b01111111 // leave elf alone, otherwise unusable
      WRITE_BYTE 0x21 THIS & `BIT6      // usable by druid
      BUT_ONLY

  END

END
  
COPY_EXISTING ~u1ham4b.itm~ ~override~
  WRITE_BYTE 0x20 (THIS | BIT0) // add fmt flag
  BUT_ONLY

// wand of corrosion should be bard/mage only
COPY_EXISTING ~wandcor.itm~ ~override~ // Wand of Corrosion
  WRITE_LONG 0x1e (BIT7 + BIT9 + BIT10 + BIT11 + BIT12 + BIT14 + BIT17 + BIT20 + BIT21 + BIT22 + BIT30)
  BUT_ONLY

COPY_EXISTING ~zza7ch.itm~ ~override~
  WRITE_BYTE 0x1f THIS | 0x10 // adds f/d flag (consistent w/ other hammers)
  BUT_ONLY

// divine spell restrictions by alignment implemented only in HoW/TotLM
ACTION_IF !game_is_iwd THEN BEGIN // how, totlm fixes

  // should be usable by trueclass clerics
  COPY_EXISTING ~sppr717x.itm~ ~override~ // destruction
    WRITE_BYTE 0x1e THIS & `BIT7
    BUT_ONLY

  // spells not castable by evil characters
  COPY_EXISTING ~scrl61.itm~   ~override~ // cure critical wounds
                ~sppr324x.itm~ ~override~ // holy smite
                ~sppr504c.itm~ ~override~ // raise dead
                ~sppr607c.itm~ ~override~ // heal
                ~sppr712c.itm~ ~override~ // resurrection
    WRITE_BYTE 0x1e THIS | BIT1
    BUT_ONLY
  
  // spells not castable by good characters
  COPY_EXISTING ~sppr112x.itm~ ~override~ // cause light wounds
                ~sppr218x.itm~ ~override~ // cause moderate wounds
                ~sppr315x.itm~ ~override~ // cause disease
                ~sppr325x.itm~ ~override~ // unholy blight
                ~sppr416x.itm~ ~override~ // cause serious wounds
                ~sppr417x.itm~ ~override~ // cloud of pestilence
                ~sppr418x.itm~ ~override~ // poison
                ~sppr513x.itm~ ~override~ // cause critical wounds
                ~sppr515x.itm~ ~override~ // slay living
                ~sppr717x.itm~ ~override~ // destruction
    WRITE_BYTE 0x1e THIS | BIT2
    BUT_ONLY
  
  // spells not castable by neutral (good/evil) characters
  COPY_EXISTING ~sppr417x.itm~ ~override~ // cloud of pestilence
                ~sppr418x.itm~ ~override~ // poison
                ~sppr513x.itm~ ~override~ // cause critical wounds
                ~sppr712c.itm~ ~override~ // resurrection
                ~sppr717x.itm~ ~override~ // destruction
    WRITE_BYTE 0x1e THIS | BIT3
    BUT_ONLY
  
  // spells not castable by lawful characters
  COPY_EXISTING ~sppr415x.itm~ ~override~ // blood rage
    WRITE_BYTE 0x1e THIS | BIT4
    BUT_ONLY

END

/////                                                  \\\\\
///// scrolls/items that cast spells                   \\\\\
/////                                                  \\\\\

// cast spell opcodes should have zero power; let actual spells handle power
ACTION_FOR_EACH file IN
  amaunat amulbar beltbon blast bloodgf blrdeck boneam braceip cloakin cone cynicis dntshd2 fanggf
  fistgf gullwyn handgf harp helmsh jasper jhoswd3 kinetic kissgf lucky mantlecs mantlehf mhorn 
  mirror2 nature ogien power quost revenan rogue scarab sceptre scrl1g scrl56 scrl58 scrl59 scrl61 
  scrl62 scrl63 sppr112x sppr113x sppr216x sppr217x sppr218x sppr315x sppr316x sppr318x sppr319x 
  sppr320x sppr321x sppr322x sppr323x sppr324x sppr325x sppr415x sppr416x sppr417x sppr418x sppr419x 
  sppr420x sppr421x sppr422x sppr423x sppr513x sppr514x sppr515x sppr516x sppr517x sppr518x sppr519x 
  sppr610x sppr611x sppr612x sppr613x sppr717x sppr718x sppr719x sppr720x sppr721x sppr722x spwi223a 
  spwi319x spwi422x spwi423x spwi424x spwi517x spwi518x spwi519x spwi618x spwi619x spwi620x spwi710x 
  spwi711x spwi805x spwi806x spwi807x spwi808x stafbes staffbes stafhmg stomper storm talongf
  tiernon tonggf ulring vexed3 virgin wandarm wandtrp xstafhmg zz36dgd
BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~%file%.itm~) THEN BEGIN

    COPY_EXISTING ~%file%.itm~ ~override~
      LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 146 power = 0 END
      LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 148 power = 0 END
      BUT_ONLY

  END

END

// scroll range fixes
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_scroll_ranges BEGIN
  scprism =>  25 // prismatic spray
  scrl03  =>  30 // protection from acid
  scrl04  =>  30 // protection from cold
  scrl05  =>  30 // protection from electricity
  scrl06  =>  30 // protection from fire
  scrl07  =>  30 // protection from magic
  scrl08  =>  30 // protection from poison
  scrl09  =>  30 // protection from undead
  scrl15  =>  30 // protection from petrification
  scrl1h  => 100 // haste
  scrl2d  =>  50 // animate dead
  scshds  =>  50 // shades
  scshro  => 100 // shroud of flame
END

ACTION_PHP_EACH cd_scroll_ranges AS item => scroll_range BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME "%item%.itm" BEGIN

    COPY_EXISTING "%item%.itm" override
      LPF ALTER_ITEM_HEADER INT_VAR header = 1 range = %scroll_range% END
      BUT_ONLY

  END

END

// protection from X scrolls should bypass MR 
COPY_EXISTING ~scrl03.itm~ ~override~ // protection from acid
              ~scrl04.itm~ ~override~ // protection from cold
              ~scrl05.itm~ ~override~ // protection from electricity
              ~scrl06.itm~ ~override~ // protection from fire
              ~scrl08.itm~ ~override~ // protection from poison
              ~scrl09.itm~ ~override~ // protection from undead
              ~scrl15.itm~ ~override~ // protection from petrification
  LPF ALTER_EFFECT INT_VAR resist_dispel = 3 END
  BUT_ONLY

// change unID'd name to match other pro scrolls
COPY_EXISTING ~scrl05.itm~ ~override~
  WRITE_LONG 0x08 18094
  BUT_ONLY

COPY_EXISTING ~scrl09.itm~ ~override~ // protection from undead
  LPF ALTER_EFFECT INT_VAR match_duration = 600 duration = 3600 END // should last 12 hours
  BUT_ONLY

COPY_EXISTING ~scrl15.itm~ ~override~ // protection from petrification
  LPF ALTER_EFFECT INT_VAR match_duration = 1800 duration = 3600 END // should last 12 hours
  BUT_ONLY

// ghoul touch should match its description: see spwi218.spl, ghoult.itm, and kuorrick.bcs et al
COPY_EXISTING ~scrl1c.itm~ ~override~ // also need to adjust spell scroll to target self instead of others
  LPF ALTER_ITEM_HEADER INT_VAR header = 1 target = 5 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 146 target = 1 END
  BUT_ONLY

// ghost armor should be castable on others
COPY_EXISTING ~scrl1t.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 146 target = 2 END
  BUT_ONLY

// can't learn vocalize due to errors in file
COPY_EXISTING ~scrl3g.itm~ ~override~
  READ_ASCII  0x3a "item_icon"
  LPF ALTER_ITEM_HEADER INT_VAR location = 3 STR_VAR icon = EVAL "%item_icon%" END
  LPF ALTER_EFFECT INT_VAR target = 1 END // vocalize is caster-only 
  BUT_ONLY
  
// scrolls with non-caster targets for spell learning ability
COPY_EXISTING ~scrl70.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR header = 2 target = 5 END // change second header to target caster
  BUT_ONLY

// wrong target for prayer from scroll
COPY_EXISTING ~sppr313c.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR target = 5 END
  BUT_ONLY

ACTION_IF !game_is_iwd THEN BEGIN  

  // referring to non-existent spell
  COPY_EXISTING ~sppr113x.itm~ ~override~
    LPF ALTER_EFFECT STR_VAR match_resource = sppr113a resource = sppr113 END // scroll did nothing
    BUT_ONLY

END

/////                                                  \\\\\
///// weapon animations                                \\\\\
/////                                                  \\\\\

COPY_EXISTING ~2haxe.itm~   ~override~
              ~j2haxe.itm~  ~override~
              ~lonesom.itm~ ~override~
              ~u2hax1a.itm~ ~override~
              ~u2hax2a.itm~ ~override~
              ~u2hax3a.itm~ ~override~
              ~u2hax3b.itm~ ~override~
              ~u2hax4a.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR type = 1 anim_overhead = 65 anim_backhand = 35 anim_thrust = 0 END
  BUT_ONLY

COPY_EXISTING ~axlizman.itm~ ~override~
              ~axyuanti.itm~ ~override~
              ~blyuanti.itm~ ~override~
              ~lalizman.itm~ ~override~
              ~mslizman.itm~ ~override~
              ~nalizman.itm~ ~override~
              ~polizman.itm~ ~override~
              ~swyuanti.itm~ ~override~
              ~telizman.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR type = 1 anim_overhead = 34 anim_backhand = 33 anim_thrust = 33 END
  BUT_ONLY

COPY_EXISTING ~dagg04.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR type = 1 anim_overhead = 10 anim_backhand = 25 anim_thrust = 65 END
  LPF ALTER_ITEM_HEADER INT_VAR range = 1 END
  BUT_ONLY

COPY_EXISTING ~hamm03.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR type = 1 anim_overhead = 50 anim_backhand = 50 anim_thrust = 0 END
  BUT_ONLY

COPY_EXISTING ~presdag.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR type = 1 anim_overhead = 0 anim_backhand = 20 anim_thrust = 80 END
  BUT_ONLY

COPY_EXISTING ~sper02.itm~ ~override~
              ~sper03.itm~ ~override~
              ~whtash.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR type = 1 anim_overhead = 0 anim_backhand = 0 anim_thrust = 100 END
  BUT_ONLY

/////                                                  \\\\\
///// other item fixes                                 \\\\\
/////                                                  \\\\\

// power, dispel issues
COPY_EXISTING ~adisease.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 power = 0 resist_dispel = 0 END
  BUT_ONLY

// power, dispel issues
COPY_EXISTING ~amul01.itm~   ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 power = 0 resist_dispel = 0 END
  PATCH_IF !game_is_iwd BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 206 power = 0 resist_dispel = 2 END // how/totlm
  END 
  BUT_ONLY

// targeting fixes for cast spell
COPY_EXISTING ~amulbra.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR target = 4 END
  BUT_ONLY

// power levels on equipped effects
COPY_EXISTING ~antimag.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_headers = 0 power = 0 END
  BUT_ONLY

// icon changes
COPY_EXISTING ~arhand.itm~ ~override~
  WRITE_ASCII 0x3a ~iarow11~ #8
  WRITE_ASCII 0x58 ~carow11~ #8
  LPF ALTER_ITEM_HEADER STR_VAR icon = "iarow11" END
  BUT_ONLY

// Armor of Penance should use the proper identified name and reduce the wearer's dexterity by 1, as per the description
COPY_EXISTING ~armpen.itm~ ~override~ // Armor of Penance +5
  SAY 0x0c @130
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 15 target = 1 timing = 2 parameter1 = "-1" END // Dexterity Modifier
  BUT_ONLY

// remove strength bonus
COPY_EXISTING ~arow05.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR flag_strength = 0 END
  BUT_ONLY

// remove damage, thac0 bonus
COPY_EXISTING ~arow08.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR thac0_bonus = 0 damage_bonus = 0 END
  BUT_ONLY

// arrows from shadowed creatures aren't magical but bypass PFNM
COPY_EXISTING ~arow1-6.itm~ ~override~ // Arrow
  LPF ALTER_ITEM_HEADER INT_VAR projectile = 6 END
  BUT_ONLY
  
// summoned tough lizardmen using weapons without animations; see ms3tliz.cre et al
COPY_EXISTING ~b1-12.itm~ ~override/ms3tliz.itm~
  WRITE_ASCII 0x22 CL #2 // club animation

// belhifet's weapon not doing poison effect due to bad timing; strings are wrong; extend range
COPY_EXISTING ~behwep.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR range = 2 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 25 timing = 1 END
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 139 END // delete string opcodes
  FOR (index = 0 ; index < 2 ; ++index) BEGIN
    LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 139 target = 2 timing = 1 resist_dispel = 2
      probability1 = (49 + (index * 51))      // 49, 100
      probability2 = ( 0 + (index * 50))      // 0, 50
      parameter1   = (4389 + (index * 10273)) // diseased, poisoned
    END
  END
  BUT_ONLY

// cosmetic: armor should be red on paperdoll, avatar
COPY_EXISTING ~blood.itm~  ~override~ // Bathed-in-Blood
  LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 7 match_parameter2 = 5 parameter1 = 47 END // armor: dark red (was 33 magenta)
  LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 7 match_parameter2 = 0 parameter1 = 47 END // belt: dark red (was 33 magenta)
  BUT_ONLY

// sound file wrong
COPY_EXISTING ~bolt03.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR target = 2 STR_VAR match_resource = eff_m22c resource = eff_m22b END // sound doesn't exist, use different zap sound
  BUT_ONLY
  
// movement changes which should *not* be blocked by free action
COPY_EXISTING ~boot01.itm~   ~override~ // boots of speed
              ~bootdriz.itm~ ~override~ // boots of speed (unused drizzt copy)
              ~bootfox.itm~  ~override~ // boots of the fox
              ~ciboot.itm~   ~override~ // undroppable, used to adjust minotaur speed
              ~move20.itm~   ~override~ // used to slow down norl to 20%
              ~sppr203.spl~  ~override~ // chant, effect to slow down caster
              ~sppr415.spl~  ~override~ // blood rage
              ~sppr517.spl~  ~override~ // animal rage
              ~sqrlspd.itm~  ~override~ // squirrel gang rise up
  LPF ALTER_EFFECT INT_VAR match_opcode = 126 opcode = 266 END
  BUT_ONLY IF_EXISTS // sqrlspd/sppr415/sppr517 is how; ciboot is totlm

// remove haste icon for movement speed increases
COPY_EXISTING ~bootfox.itm~ ~override~
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 142 END
  BUT_ONLY

// icon corrections
COPY_EXISTING ~bow01.itm~ ~override~             
              ~bow03.itm~ ~override~
  WRITE_ASCIIE 0x3a ~i%SOURCE_RES%~ #8
  LPF ALTER_ITEM_HEADER STR_VAR icon = EVAL ~i%SOURCE_RES%~ END
  BUT_ONLY

// extraneous ApR effect on attack ability, other than bow08 should have this as equipped effect
COPY_EXISTING ~bow08.itm~   ~override~
              ~bow09.itm~   ~override~ // ApR
              ~bownon.itm~  ~override~ // ApR
              ~idart01.itm~ ~override~ // ApR
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 1 END
  BUT_ONLY

// add back ApR as equipped effect
COPY_EXISTING ~bow09.itm~   ~override~ // ApR
              ~bownon.itm~  ~override~ // ApR
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 1 target = 1 parameter1 = 2 parameter2 = 1 timing = 2 END
  BUT_ONLY

// undroppable items should be undroppable
COPY_EXISTING ~bring.itm~ ~override~
  WRITE_BYTE 0x18 THIS & `BIT2
  BUT_ONLY

// use different bullet ptojectile from bullet +1
COPY_EXISTING ~bull03.itm~ ~override~ // bullet +2
  LPF ALTER_ITEM_HEADER INT_VAR projectile = 20 END // bullet, heavy
  BUT_ONLY

// min level interfering with hp bonus?
COPY_EXISTING ~bwtalis.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_headers = 0 dicesize = 0 END
  BUT_ONLY

// sound, visual bypassing MR
COPY_EXISTING ~carrio1.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 resist_dispel = 1 END
  BUT_ONLY  

// create new construct immunity item; see bknight.cre et al
COPY_EXISTING ~ring95.itm~ ~override/cdconstr.itm~ // create new immunity item for constructs
  LPF DELETE_EFFECT END // delete all existing effects

// golems, constructs should be immune to poison and disease; rounded out in immunity batches
COPY_EXISTING ~cdconstr.itm~ ~override~ // new item for construct immunities
              ~ironman.itm~  ~override~ // iwd iron golem weapon
              ~icegolem.itm~ ~override~ // ice golem weapon
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 287 target = 1 timing = 2 END // immunity to backstab
  LPF CLONE_EFFECT INT_VAR match_opcode = 287 opcode = 261 parameter2 = 25 END // immunity to poison
  LPF CLONE_EFFECT INT_VAR match_opcode = 287 opcode = 261 parameter2 = 78 END // immunity to disease
  IF_EXISTS

// additional construct immunities 
COPY_EXISTING ~cdconstr.itm~ ~override~ // new item for construct immunities
  PATCH_FOR_EACH op IN 3 5 24 39 45 93 109 175 BEGIN // berserk, charm, fear, sleep, stun, fatigue, paralyzation, hold
    LPF CLONE_EFFECT INT_VAR match_opcode = 261 match_parameter2 = 25 parameter2 = op END // immunity to x
  END
  IF_EXISTS

// mithril chain allows thieving, wonky spellcasting disable
COPY_EXISTING ~chan06.itm~ ~override~
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 60 END
  FOR (index = 0 ; index < 2 ; ++index) BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 144 target = 1 parameter2 = index timing = 2 END
  END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 145 target = 1 timing = 2 END
  BUT_ONLY

COPY_EXISTING ~clbhand.itm~ ~override~ // composite long bow of the hand
  LPF ALTER_ITEM_HEADER INT_VAR damage_bonus = 3 END // missing +1 from being composite long bow
  BUT_ONLY

// robe of electrical resistance not providing electrical resistance
COPY_EXISTING ~clck11.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 29 target = 1 parameter1 = 20 timing = 2 END
  BUT_ONLY

// seems unused, but fix anyway
COPY_EXISTING ~clck18.itm~ ~override~ 
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 0 target = 1 parameter1 = 1 parameter2 = BIT3 timing = 2 END // +1 AC vs slashing
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 33 target = 1 parameter1 = 1  timing = 2 END // +1 save vs. death
  BUT_ONLY

// make the caravan contract a quest item
COPY_EXISTING ~contract.itm~ ~override~
  WRITE_LONG 0x18 (THIS BOR BIT0) // set the plot critical flag
  BUT_ONLY

// effects bypassing save
COPY_EXISTING ~ctouch.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 savingthrow = BIT0 END
  BUT_ONLY

// darts of stunning last 10 seonds too long
COPY_EXISTING ~dart03.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 45 duration = 35 END
  BUT_ONLY

// portrait can be dispelled
COPY_EXISTING ~dart04.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 142  resist_dispel = 0 END
  BUT_ONLY

// slow sould have power level
COPY_EXISTING ~days.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 40 power = 3 END
  BUT_ONLY

// dazer has wrong percentage on one effect, so do manually - IF_EXISTS since xclub is a totlm copy
COPY_EXISTING ~dazer.itm~ ~override~
              ~xclub.itm~ ~override~ // undroppable version used by spectral hero
  LPF ALTER_EFFECT INT_VAR check_globals = 0 header_type = 1 probability1 = 4 END // one effect has prob=6, others all prob=5
  BUT_ONLY IF_EXISTS

// MR, power issues; prevent stacking; golems immune
COPY_EXISTING ~decasta.itm~ ~override~ // darts of bone
  LPF ALTER_EFFECT INT_VAR match_target = 2 resist_dispel = 1 power = 2 END // drain subject to MR
  LPF CLONE_EFFECT INT_VAR match_opcode = 233 opcode = 206 parameter2 = 55 timing = 0 resist_dispel = 0 STR_VAR resource = decasta insert = first END // golems immune
  BUT_ONLY

// play sound can be dispelled
COPY_EXISTING ~elfwine.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 174 resist_dispel = 0 END
  BUT_ONLY

// try to bring flame blade damage inline with descript, damage shouldn't bypass resistance
COPY_EXISTING ~fblade.itm~ ~override~ // Flame Blade
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 parameter1 = 4 resist_dispel = 1 END // adjust to +4 fire damage, fix MR
  LPF CLONE_EFFECT INT_VAR match_opcode = 12 parameter1 = 2 STR_VAR insert = last END // add 2 fire damage as last effect
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 12 opcode = 206 parameter2 = 2 timing = 0 STR_VAR insert = below resource = fblade END // add block for non-undead
  LPF CLONE_EFFECT INT_VAR match_opcode = 206 parameter2 = 3 STR_VAR insert = first END // add fire-dwelling block at top of stack
  BUT_ONLY
  
// fire elemental attack has random damage save
COPY_EXISTING ~fele1-8.itm~ ~override~ // fire elemental attack (normal)
              ~felem.itm~   ~override~ // fire elemental attack (shapeshift)
  LPF ALTER_EFFECT INT_VAR STR_VAR match_opcode = 12 savingthrow = 0 savebonus = 0 END
  BUT_ONLY IF_EXISTS // felem is how item

// string bypasses MR
COPY_EXISTING ~fkiller.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 139 resist_dispel = 1 END
  BUT_ONLY

// ghast attack missing strength bonus, some effects not dispellable/bypassing MR
COPY_EXISTING ~ghast1.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR flag_strength = 1 END
  LPF ALTER_EFFECT INT_VAR check_globals = 0 resist_dispel = 1 END
  BUT_ONLY

// unparalyzed sound too late, no str bonus, stuff bypassing MR/not dispelling, half-elves not getting paralyzation resistance
COPY_EXISTING ~ghoul1.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR flag_strength = 1 END
  LPF ALTER_EFFECT INT_VAR resist_dispel = 1 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 match_timing = 4 duration = 30 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 206 resist_dispel = 0 END // reset resist/dispel
  LPF CLONE_EFFECT INT_VAR match_opcode = 206 match_parameter2 = 15 parameter2 = 19 probability1 = 69 END // half-elf 70% chance to resist outright
  BUT_ONLY

// ghoul touch should match its description: see spwi218.spl, scrl1c.itm, and kuorrick.bcs et al
COPY_EXISTING ~ghoult.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 30 duration = 42 END // bump up to six rounds
  BUT_ONLY

// hp bonus should have 0 power
COPY_EXISTING ~giving.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 17 power = 0 END
  BUT_ONLY

// range, thac0 bonus incorrect
COPY_EXISTING ~gsleep.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR range = 1 thac0_bonus = 3 END
  BUT_ONLY

// needs electricity, fire, cold portrait icons
COPY_EXISTING ~helm04.itm~ ~override~
  PATCH_FOR_EACH icon IN 25 17 16 BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = icon timing = 2 END
  END
  BUT_ONLY

// assign unique, unused bam to hunting helm; icon matches lore in descript
COPY_EXISTING ~helmhun.itm~ ~override~
  WRITE_ASCII 0x3a ~IHELM06~ #8 // inventory icon
  WRITE_ASCII 0x22 ~H0~ #2 // paperdoll animation ID
  LPF ALTER_EFFECT INT_VAR check_globals = 1 check_headers = 0 match_opcode = 7 match_parameter2 = 53 parameter1 = 67 END
  LPF ALTER_EFFECT INT_VAR check_globals = 1 check_headers = 0 match_opcode = 7 match_parameter2 = 48 parameter1 = 13 END
  LPF ALTER_EFFECT INT_VAR check_globals = 1 check_headers = 0 match_opcode = 7 match_parameter2 = 52 parameter1 = 49 END
  BUT_ONLY

// should require ID before use
COPY_EXISTING ~hlydeck.itm~ ~override~ // Holy Chaos Deck
  LPF ALTER_ITEM_HEADER INT_VAR identify = 1 END
  BUT_ONLY

// power levels for holdfast's entangle
COPY_EXISTING ~holdfst.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 154 power = 1 END
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 126 power = 1 END
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 174 power = 1 END

// jester bag produces 20 oils of speed
COPY_EXISTING ~holding.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 122 parameter1 = 1 STR_VAR match_resource = potn23 END
  BUT_ONLY

// hq long bow, composite long bow icons swapped to match normal version icon swap 
COPY_EXISTING ~hqcbow.itm~ ~override~ // High Quality Composite Long Bow
  WRITE_ASCII 0x3a ~ibow12~ #8 
  LPF ALTER_ITEM_HEADER STR_VAR icon = ibow12 END
  BUT_ONLY

// high quality halberd is just called "halberd" in game
COPY_EXISTING ~hqhalb.itm~ ~override~
  SAY 0x08 @133
  SAY 0x0c @133
  BUT_ONLY

// hq long bow, composite long bow icons swapped to match normal version icon swap  
COPY_EXISTING ~hqlbow.itm~ ~override~ // High Quality Long Bow
  WRITE_ASCII 0x3a ~ibow11~ #8 
  LPF ALTER_ITEM_HEADER STR_VAR icon = ibow11 END
  BUT_ONLY

// add back ApR as equipped effect
COPY_EXISTING ~idart01.itm~ ~override~ // ApR
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 1 target = 1 parameter1 = 3 parameter2 = 1 timing = 2 END
  BUT_ONLY 

COPY_EXISTING ~kaybow.itm~ ~override~ // kaylessa's composite long bow of the hand
  LPF ALTER_ITEM_HEADER INT_VAR damage_bonus = 4 END // missing +1 from being composite long bow
  BUT_ONLY
  
// name Kaylessa's Armor 
COPY_EXISTING ~kaychai.itm~ ~override~ // Kaylessa's Armor
  SAY 0x0c @117
  BUT_ONLY

// move cold resistance to equipped effect
COPY_EXISTING ~kresssw.itm~ ~override~ // cold resistance
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 28 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 28 target = 1 parameter1 = 10 timing = 2 END
  BUT_ONLY

// string always occurs, instead of on 20% of hits
COPY_EXISTING ~kris.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 probability1 = 19 END // 139 is 100, rest are 20; correct all to 19
  BUT_ONLY

// remove charm, stun immunity from free action; have to remove all 261s and then re-add
COPY_EXISTING ~labelt.itm~  ~override~
              ~ring09.itm~  ~override~
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 261 END
  PATCH_FOR_EACH addme IN 175 158 157 154 126 109 40 BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 261 target = 1 timing = 2 parameter2 = "%addme%" END
  END
  BUT_ONLY

COPY_EXISTING ~lxbowbm.itm~ ~override~ // bren muller's crossbow
  LPF ALTER_ITEM_HEADER INT_VAR damage_bonus = 3 END // 1 damage short for +3 lt xbow
  BUT_ONLY

// haste should bypass spell protections
COPY_EXISTING ~mage06.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_headers = 0 power = 0 resist_dispel = 0 END
  BUT_ONLY

COPY_EXISTING ~mhorn.itm~ ~override~ // the merry shorthorn
  LPF ALTER_EFFECT STR_VAR match_resource = shorn resource = s_horn END
  BUT_ONLY

// icon corrections
COPY_EXISTING ~misc75.itm~ ~override~
  WRITE_ASCIIE 0x3a ~i%SOURCE_RES%~ #8
  LPF ALTER_ITEM_HEADER STR_VAR icon = EVAL ~i%SOURCE_RES%~ END
  BUT_ONLY

// remove min level from cast spell
COPY_EXISTING ~moonbow.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 146 dicesize = 0 END
  BUT_ONLY

// remove power, MR checks from sound effect
COPY_EXISTING ~philter.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 174 power = 0 resist_dispel = 0 END
  BUT_ONLY

// full plate +1 doesn't disable thieving button; cosmetic: armor should be dark gray on paperdoll, avatar
COPY_EXISTING ~plat05.itm~ ~override~
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 144 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 144 target = 1 parameter2 = 0 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 144 target = 1 parameter2 = 1 timing = 2 END
  LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 7 match_parameter2 = 5 parameter1 = 65 END // armor: very dark gray (was 29 red-tinted black)
  LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 7 match_parameter2 = 4 parameter1 = 16 END // straps: silverish gold (was 23 light copper)
  LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 7 match_parameter2 = 0 parameter1 = 65 END // belt: very dark gray (was 29 red-tinted black)
  BUT_ONLY

// should use protection from fire icon, not protection from fire/cold
COPY_EXISTING ~potn02.itm~ ~override~ // potion of fire resistance
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 142 match_parameter2 = 26 parameter2 = 16 END
  BUT_ONLY

// wrong damage, displays 'gulp' when thrown, power/dispel for sound effect
COPY_EXISTING ~potn13.itm~ ~override~
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 12 END
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 139 END
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 174 power = 0 resist_dispel = 0 timing = 1 END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 12 target = 2 parameter2 = 0x00080000 timing = 1 resist_dispel = 1 dicenumber = 2 dicesize = 6 END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 12 target = 2 parameter2 = 0x00080000 timing = 1 resist_dispel = 1 dicenumber = 3 dicesize = 6 savingthrow = BIT1 END
  BUT_ONLY

// effect with wrong dispel, power wrong
COPY_EXISTING ~potn20.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 power = 0 resist_dispel = 3 END
  BUT_ONLY

// should use protection from cold icon, not protection from fire/cold
COPY_EXISTING ~potn22.itm~ ~override~ // potion of cold resistance
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 142 match_parameter2 = 26 parameter2 = 25 END
  BUT_ONLY 

// extraneous header, power/MR for sound
COPY_EXISTING ~potn27.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 174 power = 0 resist_dispel = 0 END
  BUT_ONLY

// blocking levels 1-9 instead of 1-5; also tweak descript in GTU for dispel
COPY_EXISTING ~potn33.itm~ ~override~
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 102 END
  FOR (index = 1 ; index < 6 ; ++index) BEGIN
    LPF ADD_ITEM_EFFECT INT_VAR opcode = 102 target = 1 parameter1 = index resist_dispel = 1 duration = 35 END
  END
  BUT_ONLY

// extraneous header, power for immunities; description not even close to accurate, add some cosmetic/auditory cues
COPY_EXISTING ~potn34.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 600 duration = 70 END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 141 target = 1 parameter2 = 14 timing = 4 duration = 1 resist_dispel = 1 END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 233 target = 1 parameter2 = 6 timing = 4 duration = 1 resist_dispel = 1 END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 174 target = 1 timing = 4 duration = 1  resist_dispel = 1 STR_VAR resource = eff_m02 END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 174 target = 1 timing = 4 duration = 70 resist_dispel = 1 STR_VAR resource = eff_e04 END
  BUT_ONLY

// duration is 3 hours = 15 turns = 150 rounds
COPY_EXISTING ~potn36.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 1260 duration = 1050 END
  BUT_ONLY

// extraneous header, power for immunities
COPY_EXISTING ~potn38.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 206 resist_dispel = 3 END
  BUT_ONLY

// extraneous header, no save for sound effect
COPY_EXISTING ~potn40.itm~ ~override~
  WRITE_LONG 0x54 20729 // cursed potion of invulnerability not using the 'murky' identified description
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 174 savingthrow = BIT4 END
  BUT_ONLY

// extraneous header; portrait icon duration wrong, remove charm & stun immunity from free action
COPY_EXISTING ~potn45.itm~ ~override~
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 261 END
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 142 duration = 700 END // portrait icon is wrong
//  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_timing =   1 duration =   0 END // remove durations from perm effects
  PATCH_FOR_EACH addme IN 175 158 157 154 126 109 40 BEGIN
    LPF ADD_ITEM_EFFECT INT_VAR opcode = 261 target = 1 power = 4 resist_dispel = 1 duration = 700 parameter2 = "%addme%" END
  END
  BUT_ONLY

// assign potion of null effect a unique, unused icon
COPY_EXISTING ~pnull.itm~  ~override~
  WRITE_ASCII 0x3a ~ipotn49~ #8 // inventory icon
  LPF ALTER_ITEM_HEADER STR_VAR icon = ipotn49 END // extended header icon
  BUT_ONLY

// range, enchantment, min str
COPY_EXISTING ~presdag.itm~ ~override~
  WRITE_SHORT 0x26 3 // min str
  LPF ALTER_ITEM_HEADER INT_VAR range = 0 END
  BUT_ONLY

// icon
COPY_EXISTING ~presrob.itm~ ~override~
  WRITE_ASCII 0x3a ~iclck15~ #8
  BUT_ONLY

// kill target prevents visuals, fix header for snow maiden's reaver
COPY_EXISTING ~reaver.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 1 dicesize = 0 dicenumber = 0 END
  LPF CLONE_EFFECT  INT_VAR match_opcode = 13 multi_match = 1 STR_VAR insert = last END // clone to last effect
  LPF DELETE_EFFECT INT_VAR match_opcode = 13 multi_match = 1 END                       // delete original
  BUT_ONLY

// dispelling one spell twice, one spell wrong
COPY_EXISTING ~redemt.itm~ ~override~
  LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 254 END
  PATCH_FOR_EACH addme IN spin108 sppr204 sppr405 spwi104 spwi316 spwi507 BEGIN
    LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 254 target = 2 parameter2 = 2 STR_VAR resource = EVAL "%addme%" END
  END
  BUT_ONLY

// missing saves for sound, visual; prevent portrait icon for creatures who can't be charmed
COPY_EXISTING ~ring03.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 savingthrow = BIT3 power = 2 END
  LPF CLONE_EFFECT INT_VAR parameter2 = 8 match_opcode = 5 opcode = 206 power = 0 timing = 0 duration = 0 resist_dispel = 0 STR_VAR insert = first resource = EVAL "%SOURCE_RES%" END
  BUT_ONLY

// wrong power for equipped fx
COPY_EXISTING ~ringmal.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_headers = 0 power = 0 END
  BUT_ONLY

// summoned lizardmen using weapons without animations; see ms2lizm.cre et al
COPY_EXISTING ~s1-12.itm~ ~override/ms2lizm.itm~
  WRITE_ASCII 0x22 HB #2 // halberd animation

// make un-petrify stuff destroy the gorgon ring, otherwise victim will just re-petrify on load; see spwi614.spl
COPY_EXISTING ~scrlpet.itm~  ~override~ // stone to flesh scroll
              ~scrlpet2.itm~ ~override~ // stone to flesh scroll
  LPF CLONE_EFFECT INT_VAR match_opcode = 43 opcode = 112 timing = 1 STR_VAR resource = gorgon END
  BUT_ONLY IF_EXISTS

// timing modes for effects, add round of putting on oil
COPY_EXISTING ~serpsca.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_timing = 4 timing = 1 duration = 0 END
  LPF ADD_ITEM_EFFECT INT_VAR insert_point = 0 opcode = 165 target = 1 duration = 7 END
  BUT_ONLY

// missing its +3 damage bonus
COPY_EXISTING ~serrate.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR damage_bonus = 3 END
  BUT_ONLY
  
// limited effect has permanent timing
COPY_EXISTING ~shadless.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_timing = 1 duration = 0 END // op 233
  BUT_ONLY

// shadows tried to play 'disease' string on themselves ratehr than target
COPY_EXISTING ~shadow1.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_check_globals = 0 match_opcode = 139 target = 2 END
  BUT_ONLY

// wrong prof for shillelagh (IWD only), wrong thac0 bonus (all)
COPY_EXISTING ~shillel.itm~ ~override~
  WRITE_SHORT 0x26 0 // min str
  PATCH_IF game_is_iwd BEGIN
    WRITE_SHORT 0x1c 26 // type as staff to pick up staff prof, per IWD description
  END
  LPF ALTER_ITEM_HEADER INT_VAR thac0_bonus = 1 END
  BUT_ONLY

// wrong min strength
COPY_EXISTING ~shld09.itm~ ~override~
  WRITE_SHORT 0x26 0
  BUT_ONLY

// wrong min strength, penalizes missile AC
// bitch queen's envoy blocks *all* elementals, not just water 1/2; see also specifics.ids and general bulk creature changes
COPY_EXISTING ~shldbch.itm~ ~override~
  WRITE_SHORT 0x26 12
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 0 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 0 target = 1 parameter1 = 3 timing = 2 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 100 parameter1 = 114 parameter2 = 6 END // specifics = WATER_ELEMENTAL
  BUT_ONLY
  
// give shadowed plate a non-generic name
COPY_EXISTING ~shplate.itm~ ~override~ // Shadowed Plate +3
  SAY 0x0c @118 // "Shadowed Plate"
  BUT_ONLY

// sound can be blocked
COPY_EXISTING ~spirit.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 174 power = 0 END
  BUT_ONLY

// needs acid, electricity, fire, cold
COPY_EXISTING ~swanarm.itm~ ~override~
  PATCH_FOR_EACH icon IN 25 24 17 16 BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = icon timing = 2 END
  END
  BUT_ONLY
  
// 'dead' trolls should not be killed by poison/disease; shouldn't save against anything
COPY_EXISTING ~trollde.itm~ ~override~ // effects rounded out in immnity batches
  LPF ALTER_EFFECT INT_VAR match_opcode = 89 parameter2 = 1 END // set missile resistance to 100, not increment
  LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 0 opcode = 261 target = 1 parameter2 = 25 timing = 2 END // immunity to poison
  LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 0 opcode = 261 target = 1 parameter2 = 78 timing = 2 END // immunity to disease
  PATCH_FOR_EACH op IN 2 4 11 46 75 77 79 81 161 162 267 BEGIN // cure sleep, berserk, poison, stun, blind, feeblemind, disease, deafness, fear, paralysis, confusion
    LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 0 opcode = op target = 1 timing = 1 END // cure any poison currently in effect
  END  
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 261 opcode = 238 parameter1 = 20 parameter2 = 1 STR_VAR insert = last END // set all saves to 20
  LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 0 opcode =  58 target = 1 parameter2 =  0 timing = 1 END // dispel all ongoing effects
  BUT_ONLY

// no save for one sound effect
COPY_EXISTING ~trnbolt.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 savingthrow = BIT0 END
  BUT_ONLY

// convert phasing damage from disease to straight magical damage
COPY_EXISTING ~u1ham5a.itm~ ~override~ // War Hammer of Phasing +3
              ~u2ham4b.itm~ ~override~ // War Hammer of Phasing +2
              ~u2hax4a.itm~ ~override~ // Two-Handed Axe of Greater Phasing +2
              ~ubswd2a.itm~ ~override~ // Phasing Bastard Sword +1
              ~ubswd4b.itm~ ~override~ // Bastard Sword of Greater Phasing +3
              ~udagg1b.itm~ ~override~ // Phase Dagger
              ~umstr3c.itm~ ~override~ // Morning Star of Lesser Phasing +2
              ~usswd2b.itm~ ~override~ // Short Sword of Lesser Phasing +1   
  LPM cd_convert_phasing 
  BUT_ONLY

// hammer has random heal
COPY_EXISTING ~u1ham5b.itm~  ~override~ // war hammer +4: defender: 20% heal
  LPF DELETE_EFFECT INT_VAR match_opcode = 17 END // delete random heal
  BUT_ONLY

// remove min level on stun effect
COPY_EXISTING ~u1hax2a.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_dicesize = 6 dicesize = 0 END // op 45 and 139
  BUT_ONLY

// poison icon expires too early
COPY_EXISTING ~u1hax3a.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 duration = 100 END 
  BUT_ONLY

// hammer range
COPY_EXISTING ~u2ham3a.itm~ ~override~
              ~u2ham4a.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR range = 1 END
  BUT_ONLY

// probabilities overlap
COPY_EXISTING ~u2hax4a.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 12 match_dicesize = 4 probability1 = 40 probability2 = 16 END // gets adjusted in bulk probability fixes
  BUT_ONLY

// change berserk type
COPY_EXISTING ~u2hax5a.itm~ ~override~ // Foe's Fate +4
              ~udart2a.itm~ ~override~ // Berserker Darts
              ~young.itm~   ~override~ // Young Rage +5
  LPF ALTER_EFFECT INT_VAR match_opcode = 3 parameter2 = 1 END
  BUT_ONLY IF_EXISTS // young is an how item

// chance of +damage completely overlaps chance of berserk
COPY_EXISTING ~u2hax5a.itm~  ~override~ // foe's fate: 25% berserk, 25% electric damage
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 probability1 = 50 probability2 = 26 END // these will get adjusted to correct 25-49 in bulk patch
  BUT_ONLY

// hammer/berserker darts/arrows should require ID
COPY_EXISTING ~uarow2a.itm~ ~override~ // hammer arrow
              ~udart1a.itm~ ~override~ // hammer darts
              ~udart2a.itm~ ~override~ // berserker darts
  WRITE_SHORT 0x42 15 // lore 
  BUT_ONLY

// remove min level on stun effect, missing their 1d10 crushing damage
COPY_EXISTING ~uarow3b.itm~ ~override~ // hammer arrows +1
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_dicesize = 6 dicesize = 0 END // op 45 and 139
  LPF ADD_ITEM_EFFECT INT_VAR type = 2 opcode = 12 target = 2 dicenumber = 1 dicesize = 10 timing = 1 END
  BUT_ONLY

// disease shouldn't have a save
COPY_EXISTING ~udagg4a.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR savingthrow = 0 END
  BUT_ONLY
  
// blindness is correct 25%, other effects (string, icon) are at 10%
COPY_EXISTING ~udart3b.itm~  ~override~ // blinding darts +2: 25% blind
  LPF ALTER_EFFECT INT_VAR check_globals = 0 header_type = 2 probability1 = 24 END
  BUT_ONLY

// remove min level on visuals
COPY_EXISTING ~uhalb3c.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 139 dicesize = 0 END
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 233 dicesize = 0 END
  BUT_ONLY

COPY_EXISTING ~uhxbw2a.itm~ ~override~ // finest heavy crossbow
              ~xbow01.itm~  ~override~ // heavy crossbow
  WRITE_SHORT 0x26 11 // min str
  BUT_ONLY

// huge longbow should be damage type missile not piercing
COPY_EXISTING ~ulbow1a.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 4 damage_type = 4 END
  BUT_ONLY

COPY_EXISTING ~ulbow2b.itm~ ~override~
  WRITE_SHORT 0x26 12 // min strength
  BUT_ONLY
  
// hold fast only holds humanoids, but plays ancillary effects on everyone; add hold icon; fix sound target; add save on visuals
COPY_EXISTING ~ulswd4a.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 savingthrow = BIT0 END // op233 missing save
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 insert_point = 0 opcode = 206 target = 2 power = 0 parameter2 = 6 duration = 1 STR_VAR resource = ulswd4a END
  LPF CLONE_EFFECT INT_VAR match_opcode = 175 opcode = 142 parameter2 = 13 END // add hold icon
  LPF ALTER_EFFECT INT_VAR match_opcode = 175 parameter1 = 0 parameter2 = 2 END // target this against all (humanoids now immune due to new 206)
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 match_target = 1 target = 2 END // held sound being applied to wielder, not target
  BUT_ONLY

// Morning Star of the Gods should grant two extra level 1 spells, not one
COPY_EXISTING ~umstr4a.itm~ ~override~ // morning star of the gods
  LPF ALTER_EFFECT INT_VAR match_opcode = 62 match_parameter2 = BIT0 parameter1 = 2 END
  BUT_ONLY

// remove min level on slow effect
COPY_EXISTING ~umstr4b.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 40 dicesize = 0 END
  BUT_ONLY

// remove min level on stun effect
COPY_EXISTING ~umstr5a.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_dicesize = 6 dicesize = 0 END // op 45 and 139
  BUT_ONLY

// item is actually correct, but using wrong description
COPY_EXISTING  ~ushld2b.itm~ ~override~
  WRITE_LONG 0x54 14639
  BUT_ONLY

// shadowed leathers are usable by only rangers and thieves
COPY_EXISTING ~usltr01.itm~   ~override~ // Shadowed Studded Leather +1
              ~usltr3a.itm~   ~override~ // Shadowed Studded Leather +2
              ~usltr5a.itm~   ~override~ // Shadowed Studded Leather +4
  WRITE_LONG 0x1e (THIS BAND `(BIT9 + BIT10)) // removes cleric-thief, cleric-ranger flags
  BUT_ONLY

// needs fire, electrcity icons
COPY_EXISTING ~usltr4a.itm~ ~override~
              ~usltr5a.itm~ ~override~
  PATCH_FOR_EACH icon IN 17 16 BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = icon timing = 2 END
  END
  BUT_ONLY

// extraneous header, power on sound
COPY_EXISTING ~uspot1a.itm~ ~override~
              ~uspot2a.itm~ ~override~
              ~uspot2b.itm~ ~override~
              ~uspot3a.itm~ ~override~
              ~uspot3b.itm~ ~override~
              ~uspot3c.itm~ ~override~
              ~uspot4a.itm~ ~override~
              ~uspot4b.itm~ ~override~
              ~uspot4c.itm~ ~override~
              ~uspot5a.itm~ ~override~
              ~uspot5b.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 174 power = 0 resist_dispel = 2 END
  BUT_ONLY

// one more effect for 4b
COPY_EXISTING ~uspot4b.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 power = 0 resist_dispel = 2 END // op233
  BUT_ONLY
  
// supposed to be elctric damage 50%, stun 25%, but was actually 40% and 20%  
COPY_EXISTING ~utswd5a.itm~  ~override~ // static two handed sword +4: 50% electric damage, 25% stun, fixed elsewhere
  LPF ALTER_EFFECT INT_VAR header_type = 1                   probability1 = 50 probability2 = 74 END // stun effects
  LPF ALTER_EFFECT INT_VAR header_type = 1 match_opcode = 12 probability1 =  0 probability2 = 49 END // reset electrical damage
  BUT_ONLY

// inconsistent saves, power, range, duration incorect for wand of fear
COPY_EXISTING ~wand02.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR check_check_globals = 0 range = 100 END
  LPF ALTER_EFFECT INT_VAR match_duration = 100 duration = 105 END
  LPF ALTER_EFFECT INT_VAR check_globals = 0 power = 4 savingthrow = BIT0 savebonus = 2 END // ops 233, 141
  BUT_ONLY

// inconsistent saves, power; wrong icon
COPY_EXISTING ~wand04.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 174 power = 4 savingthrow = BIT3 savebonus = `3 END
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 233 power = 4 savingthrow = BIT3 savebonus = `3 END
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 142 parameter2 = 44 END
  BUT_ONLY

// wand of frost crashes game when used
COPY_EXISTING ~wand06.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR projectile = 105 END // re-use winter wolf ice projectile
  LPF ALTER_EFFECT INT_VAR parameter1 = 4 dicesize = 5 dicenumber = 4 END // should be 2x 4d6 damage; simulated as 2x 4d5+4 for the "ones counted as twos" rolling
  BUT_ONLY

// summons shouldn't have power levels
COPY_EXISTING ~wand10.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR power = 0 END 
  BUT_ONLY

// misordering prevents evasion (how, totlm); add lore; claim 6d8 damage, actually does 6d6; power level
COPY_EXISTING ~wand11.itm~ ~override~
  WRITE_SHORT 0x42 50
  LPF ALTER_ITEM_HEADER INT_VAR target = 4 END // change to target area, not actor
  LPF ALTER_EFFECT  INT_VAR match_opcode = 12 power = 5 dicesize = 8 END
  PATCH_IF !game_is_iwd BEGIN
    LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 206 END
    LPF ADD_ITEM_EFFECT INT_VAR insert_point = 0 opcode = 206 target = 2 parameter2 = 63 resist_dispel = 2 STR_VAR resource = "wand11" END
  END
  BUT_ONLY

// seems unused, fix anyway
COPY_EXISTING ~wand12.itm~ ~override~
  WRITE_SHORT 0x42 60 // lore
  LPF ALTER_ITEM_HEADER INT_VAR identify = 1 END // require ID to use
  BUT_ONLY

// wrong type of damage for mag missiles
COPY_EXISTING ~wandmis.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 12 parameter2 = (64 << 16) END // normal, magic damage
  BUT_ONLY

// match power to lightning bolt spell
COPY_EXISTING ~wandrea.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 power = 3 END
  BUT_ONLY

// power levels; don't throw in mass patch since it should be disspellable
COPY_EXISTING ~wandtrp.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_globals = 0 power = 0 resist_dispel = 3 END
  BUT_ONLY

// dagger range
COPY_EXISTING ~withery.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR range = 0 END
  BUT_ONLY

// thac0/damage bonus not working vs. lawful; can fix damage at least
COPY_EXISTING ~zz05we.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 insert_point = 999 opcode = 206 target = 2 parameter2 = 60 STR_VAR resource = zz05we END // block the following damage effect from non-lawfuls
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 insert_point = 999 opcode = 12 target = 2 parameter1 = 1 timing = 1 END // add 1 point of crushing damage
  BUT_ONLY

// pale justice has its own icon
COPY_EXISTING ~zz57pj.itm~ ~override~
  WRITE_ASCII 0x3a ~izz57pj~ #8
  LPF ALTER_ITEM_HEADER STR_VAR icon = "izz57pj" END
  BUT_ONLY

// misery's herald not causing horror on hit
COPY_EXISTING ~zzm5mh.itm~ ~override~
  WRITE_BYTE 0x19 THIS | BIT1 // add cold-iron flag
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 target = 2 probability1 = 10 opcode = 290 parameter2 = 1 STR_VAR resource = "zzm5mh" END // undead immunity
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 target = 2 power = 2 resist_dispel = 1 duration = 70 probability1 = 10 savingthrow = BIT0
    opcode = 24 END // panic
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 target = 2 power = 2 resist_dispel = 1 duration = 70 probability1 = 10 savingthrow = BIT0
    opcode = 142 parameter2 = 36 END // panic portrait icon
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 target = 2 power = 2 resist_dispel = 1 duration =  0 probability1 = 10 savingthrow = BIT0
    opcode = 139 parameter1 = 14007 timing = 1 END // display panic string
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 target = 2 power = 2 resist_dispel = 1 duration =  0 probability1 = 10 savingthrow = BIT0
    opcode = 174 timing = 1 STR_VAR resource = "eff_m07" END
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 target = 2 power = 2 resist_dispel = 1 duration =  0 probability1 = 10 savingthrow = BIT0
    opcode = 233 timing = 1 parameter2 = 4 END // play visual
  BUT_ONLY

ACTION_IF game_is_iwd THEN BEGIN // iwd w/o how or totlm

  // fix for Girdle of Beatification (fixed in HoW)
  COPY_EXISTING ~beltbea.itm~   ~override~
    LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 130 parameter1 = 1 END
    BUT_ONLY

END ELSE BEGIN // how, totlm fixes
  
  // min strength; save is fixed in items vasting spells
  COPY_EXISTING ~amaunat.itm~ ~override~
    WRITE_SHORT 0x26 11 // min strength
    BUT_ONLY
  
  // probability, duration fixes
  COPY_EXISTING ~bess.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR check_globals = 0 probability1 = 9 END
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_duration = 30 duration = 16 END
    BUT_ONLY
  
  // probability fixes
  COPY_EXISTING ~bflaoil.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 206 STR_VAR resource = bflaoil END
    BUT_ONLY
  
  // strings not bypassing MR, add round of putting on oil
  COPY_EXISTING ~chance.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 139 resist_dispel = 3 END
    LPF ADD_ITEM_EFFECT INT_VAR insert_point = 0 opcode = 165 target = 1 duration = 7 END
    LPF ADD_ITEM_EFFECT INT_VAR opcode = 206 target = 1 power = 2 resist_dispel = 3 duration = 21 STR_VAR resource = spwi209 END
    LPF ADD_ITEM_EFFECT INT_VAR opcode = 206 target = 1 power = 2 resist_dispel = 3 duration = 21 STR_VAR resource = lucky END
    LPF ADD_ITEM_EFFECT INT_VAR opcode = 238 target = 1 power = 2 resist_dispel = 3 duration = 21 parameter1 = 1 END // +1 saves
    PATCH_FOR_EACH op IN 59 90 91 92 BEGIN
      LPF ADD_ITEM_EFFECT INT_VAR opcode = op target = 1 power = 2 resist_dispel = 3 duration = 21 parameter1 = 5 END // +5 thieving
    END

  // council letter has extraneous magical header
  COPY_EXISTING  ~cletter.itm~ ~override~
    LPF DELETE_ITEM_HEADER INT_VAR header_type = 3 END
    BUT_ONLY

  // MR, saves, power issues; prevent stacking; golems immune
  COPY_EXISTING ~dobone.itm~ ~override~ // darts of bone
    LPF ALTER_EFFECT INT_VAR check_globals = 0 power = 6 resist_dispel = 1 savingthrow = BIT2 END // fix MR, saves, power
    LPF CLONE_EFFECT INT_VAR match_opcode = 44 opcode = 254 timing = 0 duration = 0 parameter1 = 0 STR_VAR resource = dobone insert = first END // prevent stacking
    LPF CLONE_EFFECT INT_VAR match_opcode = 254 opcode = 206 parameter2 = 55 savingthrow = 0 resist_dispel = 0 END // golems immune to str drain
    BUT_ONLY

  // broken tiernon shield missing missile damage reduction
  COPY_EXISTING ~dntshd1.itm~ ~override~
    LPF CLONE_EFFECT INT_VAR match_opcode = 88 opcode = 89 parameter2 = 13 END // add missile damage reduction
    BUT_ONLY

  // fixed shield adds 0% missile reduction on 1/day ability; ac bonus can't be dispelled, power wrong
  COPY_EXISTING ~dntshd2.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 0 resist_dispel = 3 power = 1 END
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 89 parameter1 = 15 END // missile damage reduction set to 0, not 15
    BUT_ONLY
  
  // remove power, MR checks from summons
  COPY_EXISTING ~garrow.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 259 power = 0 resist_dispel = 3 END
    BUT_ONLY
  
  // undroppable items should be undroppable
  COPY_EXISTING ~gasp.itm~  ~override~
    WRITE_BYTE 0x18 THIS & `BIT2
    BUT_ONLY
    
  // blocks haste, but should be blocking hold person
  COPY_EXISTING ~gvalor1.itm~ ~override~ // gauntlets of valor
    LPF ALTER_EFFECT STR_VAR match_resource = spwi305 resource = spwi306 END 
    BUT_ONLY

  // helm of shouting missing its listed AC bonus
  COPY_EXISTING ~helmsh.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 0 target = 1 parameter1 = 1 timing = 2 END
    BUT_ONLY
  
  // bad sound ref
  COPY_EXISTING ~jhoswd3.itm~ ~override~
    READ_LONG   0x64 "abil_off"
    READ_SHORT  0x68 "abil_num"
    READ_LONG   0x6a "fx_off"
    FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN // looks through headers
      READ_SHORT ("%abil_off%" + 0x1e + ("%index%" * 0x38)) "abil_fx_num"
      READ_SHORT ("%abil_off%" + 0x20 + ("%index%" * 0x38)) "abil_fx_idx"
      FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN // searches through fx for slay effect
        READ_SHORT ("%fx_off%" +        (("%abil_fx_idx%" + "%index2%") * 0x30)) "opcode"
        READ_ASCII ("%fx_off%" + 0x14 + (("%abil_fx_idx%" + "%index2%") * 0x30)) "sound"
        PATCH_IF (("%opcode%" = 174) AND ("%sound%" STRING_COMPARE_CASE "eff_m12" = 0)) BEGIN // display icon
          WRITE_ASCII ("%fx_off%" + 0x14 + (("%abil_fx_idx%" + "%index2%") * 0x30)) "eff_m12a" #8
        END
      END
    END
    BUT_ONLY

  // lance of disruption hurting wielder (see spitm06.spl)
  COPY_EXISTING ~kinetic.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 timing = 2 STR_VAR resource = "spitm06" END
    BUT_ONLY
  
  // remove strength bonus for throwing dagger
  COPY_EXISTING ~lover.itm~ ~override~
    LPF ALTER_ITEM_HEADER INT_VAR flag_strength = 0 END
    BUT_ONLY

  // luck fixes, 2/4 (see spwi209.spl (all, how-only) and chance.itm)
  COPY_EXISTING ~lucky.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 timing = 2 STR_VAR resource = spwi209 END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 timing = 2 STR_VAR resource = chance END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 238 target = 1 timing = 2 parameter1 = 1 END // +1 saves
    PATCH_FOR_EACH op IN 59 90 91 92 BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = op target = 1 timing = 2 parameter1 = 5 END // +5 thieving
    END

  // missing save vs. wand bonus
  COPY_EXISTING ~mirror2.itm~ ~override~ // Mirror of Black Ice Amulet
    LPF CLONE_EFFECT INT_VAR match_opcode = 37 opcode = 34 END
    BUT_ONLY
  
  // power levels, sound bypassing MR, add miscast icon
  COPY_EXISTING ~moonbla.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 60 power = 3 END
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 174 resist_dispel = 1 END
    LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 60 opcode = 142 parameter2 = 60 END
    BUT_ONLY
  
  // remove power, MR checks from summons
  COPY_EXISTING ~oblood.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 259 power = 0 resist_dispel = 3 END
    BUT_ONLY

  // pestilent dawn does not actually make bearer immune to disease
  COPY_EXISTING ~pest.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 169 target = 1 parameter2 =  7 timing = 2 END // prevent disease icon
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 169 target = 1 parameter2 = 43 timing = 2 END // prevent nausea icon
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 261 target = 1 parameter2 = 78 timing = 2 END // immunity to disease
    BUT_ONLY
  
  // misordering prevents evasion (evasion not in base IWD)
  COPY_EXISTING ~potn26.itm~ ~override~
    LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 206 END
    LPF ADD_ITEM_EFFECT INT_VAR insert_point = 0 opcode = 206 target = 2 parameter2 = 63 resist_dispel = 2 STR_VAR resource = "potn26" END
    BUT_ONLY

  // cast spells at power 0; power set in spell itself
  COPY_EXISTING ~quost.itm~   ~override~
                ~stafbes.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR header_type = 3 power = 0 END
    BUT_ONLY
  
  // wrong ability range, no min lev for charisma bonus
  COPY_EXISTING ~sceptre.itm~ ~override~
    LPF ALTER_ITEM_HEADER INT_VAR header = 3 range = 40 END
    LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 6 dicesize = 0 END
    BUT_ONLY
  
  // change off generic icon, has random +10 to-hit
  COPY_EXISTING ~sekolah.itm~ ~override~
    WRITE_ASCII 0x3a ~ibolt05~ #8
    WRITE_ASCII 0x58 ~cbolt05~ #8
    LPF ALTER_ITEM_HEADER STR_VAR icon = "ibolt05" thac0_bonus = 0 END
    BUT_ONLY
  
  // stuff needs fire, acid
  COPY_EXISTING ~shark.itm~  ~override~
                ~shark2.itm~ ~override~
                ~shark3.itm~ ~override~
    PATCH_FOR_EACH icon IN 24 16 BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = icon timing = 2 END
    END
    BUT_ONLY
  
  // wrong range for spell
  COPY_EXISTING ~stafbes.itm~ ~override~
    LPF ALTER_ITEM_HEADER INT_VAR header = 3 range = 100 END
    BUT_ONLY

  COPY_EXISTING ~storm.itm~ ~override~ // storm bow
    LPF ALTER_ITEM_HEADER INT_VAR damage_bonus = 2 END // as a short bow, shouldn't get long bow's +1 damage
    BUT_ONLY

  // missing resists & icons - sound issue fixed in items casting spells section
  COPY_EXISTING ~talongf.itm~ ~override~
    PATCH_FOR_EACH fx IN 85 84 30 28 BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = fx target = 1 parameter1 = 10 timing = 2 END
    END
    PATCH_FOR_EACH icon IN 25 16 BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 142 target = 1 parameter2 = icon timing = 2 END
    END
    BUT_ONLY

  // save is vs. death, not vs. spell; should block effects on failed save
  COPY_EXISTING ~thrym.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR match_savingthrow = BIT0 savingthrow = BIT2 END 
    LPF ALTER_EFFECT INT_VAR match_opcode = 261 opcode = 290 parameter2 = 0 STR_VAR resource = thrym END 
    BUT_ONLY
  
  // missing magical cold resistance for vexed
  COPY_EXISTING ~vexed.itm~  ~override~
                ~vexed2.itm~ ~override~
                ~vexed3.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 85 target = 1 parameter1 = 100 parameter2 = 1 timing = 2 END
    BUT_ONLY

  // wailing of virgins guarding against fire storm, not symbol of hopelessness
  COPY_EXISTING ~virgin.itm~ ~override~
    LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 206 END
    PATCH_FOR_EACH addme IN spwi420 spwi411 spwi205 sppr715 spin134 BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 timing = 2 STR_VAR resource = EVAL "%addme%" END
    END
    BUT_ONLY

  // Will-o-Wisp should have a range of 6 to allow for proper cone targeting
  COPY_EXISTING ~wisp.itm~ ~override~ // Ring of the Will-o-Wisp
    LPF ALTER_ITEM_HEADER INT_VAR range = 6 END
    BUT_ONLY
  
  // probabilities wrong for protections
  COPY_EXISTING ~wranged.itm~ ~override~
    LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 206 END
    LPF ADD_ITEM_EFFECT INT_VAR type = 2 opcode = 206 target = 2 parameter2 = 52 resist_dispel = 2
      probability1 =  33 probability2 =  0 STR_VAR resource = "spin137" END
    LPF ADD_ITEM_EFFECT INT_VAR type = 2 opcode = 206 target = 2 parameter2 = 52 resist_dispel = 2
      probability1 =  66 probability2 = 34 STR_VAR resource = "spin134" END
    LPF ADD_ITEM_EFFECT INT_VAR type = 2 opcode = 206 target = 2 parameter2 = 52 resist_dispel = 2
      probability1 = 100 probability2 = 67 STR_VAR resource = "spin135" END
    BUT_ONLY
  
  // thac0/damage bonus not working vs. good/evil; wrong file name means it's targeting the wrong neutrals; see waukeen.sto
  COPY_EXISTING ~zz14in.itm~ ~override/zz44in.itm~ // inconsequence
  
  ACTION_IF game_is_totlm THEN BEGIN

    // ADHW ability not recharging on bracers of icelandic pearl
    COPY_EXISTING ~braceip.itm~ ~override~
      LPF ALTER_ITEM_HEADER INT_VAR flag_recharge = 1 END
      BUT_ONLY

    // flawless waterstar gem descripitong shouldn't refer to being riddled with flaws  
    COPY_EXISTING ~dgem05.itm~ ~override~
      SAY 0x54 @132
      BUT_ONLY

    COPY_EXISTING ~flailsk.itm~ ~override~ // SkullFlail
      LPF ALTER_ITEM_HEADER INT_VAR damage_bonus = 5 END // missing +1 from being a flail
      BUT_ONLY
    
    // remove charm, stun immunity from free action; have to remove all 261s and then re-add
    COPY_EXISTING ~freeact.itm~ ~override~
      LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 261 END
      PATCH_FOR_EACH addme IN 175 158 157 154 126 109 40 BEGIN
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 261 target = 1 timing = 2 parameter2 = "%addme%" END
      END
      BUT_ONLY
    
    // min lev set on CHR bonus
    COPY_EXISTING ~helmct.itm~   ~override~
                  ~stafhmg.itm~  ~override~
                  ~xstafhmg.itm~ ~override~
      LPF ALTER_EFFECT INT_VAR check_headers = 0 dicesize = 0 END // for op6
      BUT_ONLY
  
    // wrong ability target
    COPY_EXISTING ~ringlur.itm~ ~override~
      LPF ALTER_ITEM_HEADER INT_VAR target = 5 END
      BUT_ONLY

    // totlm knight tale scrolls should be plot-critical items
    COPY_EXISTING ~tale1.itm~ ~override~
                  ~tale2.itm~ ~override~
                  ~tale3.itm~ ~override~
                  ~tale4.itm~ ~override~
      WRITE_LONG 0x18 (THIS BOR BIT0) // set the plot critical flag
      BUT_ONLY
    
    // dagger range
    COPY_EXISTING ~xu2ham3.itm~ ~override~
      LPF ALTER_ITEM_HEADER INT_VAR range = 1 END
      BUT_ONLY

    // hells' bane has wrong fire resitance; no extra damage to 'unnatural'
    COPY_EXISTING ~zzc8hb.itm~ ~override~ // Hell's Bane +4
      LPF ALTER_EFFECT INT_VAR check_headers = 0 match_opcode = 30 parameter1 = 20 END
      LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 12 parameter1 = 4 dicenumber = 1 dicesize = 8 END
      BUT_ONLY
      
  END

END
  
/////                                                  \\\\\
///// spell general fixes                              \\\\\
/////                                                  \\\\\

// gender, sex, alignment, general, race, class fixes
INCLUDE ~iwdfixpack/lib/spell.tpa~

// file, anim, school, casting speed => file
ACTION_PHP_EACH cd_iwdfixpack_spell AS params => file BEGIN

  COPY_EXISTING ~%file%.spl~ ~override~
    PATCH_IF (IS_AN_INT params_1) BEGIN WRITE_BYTE 0x22 params_1 END // animation
    PATCH_IF (IS_AN_INT params_2) BEGIN WRITE_BYTE 0x25 params_2 END // school
    PATCH_IF (IS_AN_INT params_3) BEGIN LPF ALTER_SPELL_HEADER INT_VAR speed = params_3 END END // casting speed
    BUT_ONLY IF_EXISTS
    
END

/////                                                  \\\\\
///// spell school restriction fixes                   \\\\\
/////                                                  \\\\\

// opposition schools are different between iwd and how, hence all the conditionals

COPY_EXISTING ~spwi513.spl~ ~override~ // summon shadow
  WRITE_BYTE 0x1f THIS | BIT0 // add diviner flag
  PATCH_IF !game_is_iwd BEGIN  // also needs invoker flag for HoW
    WRITE_BYTE 0x1f THIS | BIT3 // add invoker flag
  END
  BUT_ONLY

COPY_EXISTING ~spwi605.spl~ ~override~ // death fog
              ~spwi612.spl~ ~override~ // otilukes freezing sphere
              ~spwi802.spl~ ~override~ // incendiary cloud
              ~spwi904.spl~ ~override~ // malavon's corrosive fog
  WRITE_BYTE 0x1e THIS | BIT6 // add abjurer flag
  BUT_ONLY

COPY_EXISTING ~spwi616.spl~ ~override~ // tensers transformation
  WRITE_BYTE 0x1f THIS | BIT1 // add enchanter flag
  PATCH_IF !game_is_iwd BEGIN // also needs conjurer flag for HoW
    WRITE_BYTE 0x1e THIS | BIT7 // add conjurer flag
  END
  BUT_ONLY

COPY_EXISTING ~spwi904.spl~ ~override~ // Malavon's Corrosive Fog
  WRITE_BYTE 0x1e THIS & `BIT6 // remove abjurer flag
  BUT_ONLY

ACTION_IF game_is_iwd THEN BEGIN // iwd-only changes

  COPY_EXISTING ~spwi110.spl~ ~override~ // id
                ~spwi111.spl~ ~override~ // infravision
                ~spwi202.spl~ ~override~ // detect evil
                ~spwi203.spl~ ~override~ // detect invis
    WRITE_BYTE 0x1e THIS | BIT7 // add conjurer flag
    BUT_ONLY

  COPY_EXISTING ~spwi303.spl~ ~override~
    WRITE_BYTE 0x1f THIS & `BIT3 // remove invoker flag
    BUT_ONLY

  COPY_EXISTING ~spwi418.spl~ ~override~ // shadow monsters
    WRITE_BYTE 0x1f THIS | BIT4 // add necromancer flag
    BUT_ONLY

  COPY_EXISTING ~spwi610.spl~ ~override~ // lich touch
    WRITE_BYTE 0x1f THIS & `BIT5 // remove transmuter flag
    BUT_ONLY

  // pw silence has wrong primary school set
  COPY_EXISTING ~spwi617.spl~ ~override~
    WRITE_BYTE 0x1e THIS | BIT0 // add conjurer flag
    WRITE_BYTE 0x1f THIS | BIT3 // add invoker flag
    WRITE_BYTE 0x1f THIS & `BIT1 // remove enchanter flag
    BUT_ONLY

END ELSE BEGIN // how/totlm changes

  COPY_EXISTING ~spwi104.spl~ ~override~ // only wrong in GoG version of the game
    WRITE_BYTE 0x1f THIS & `BIT3 // remove invoker flag
    WRITE_BYTE 0x1f THIS | BIT4 // add necromancer flag
    BUT_ONLY

END

/////                                                  \\\\\
///// spell batch fixes                                \\\\\
/////                                                  \\\\\

// spells using wrong power level
ACTION_FOR_EACH file IN sppr103 sppr105 sppr311 sppr314 sppr401 sppr504 sppr514 sppr611 
  spwi107 spwi108 spwi111 spwi118 spwi215 spwi410 spwi414 spwi417 spwi606 spwi610
BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~%file%.spl~) THEN BEGIN

    COPY_EXISTING ~%file%.spl~ ~override~
      READ_LONG  0x34 "level"    ELSE 0
      LPF ALTER_EFFECT INT_VAR power = level END
      BUT_ONLY

  END

END

// spells with non-zero durations on instant/permanent effects
ACTION_FOR_EACH file IN 
  pself0 spin104 spin107 spin108 spin109 spin110 spin121 spin122 spin123 spin124 spin126 
  spin132 spin134 spin135 spin136 spin148 spin149 spin152 spin155 spin156 spin168 spin169 
  spin171 spin173 spin179 spin181 spin182 spin989 spin994 spin995 spin996 spitm03 spitm04 
  spitm05 sppr103 sppr104 sppr107 sppr109 sppr110 sppr113 sppr201  sppr202 sppr204 sppr205 
  sppr206 sppr207 sppr208 sppr210 sppr211 sppr213 sppr214 sppr215 sppr305 sppr306 sppr311 
  sppr312 sppr313 sppr401 sppr403 sppr405 sppr407 sppr408 sppr409 sppr414 sppr417 sppr421
  sppr502 sppr508 sppr510 sppr511 sppr515 sppr518 sppr519 sppr609 sppr709 sppr714 sppr715 
  sppr716 sppr717 sppr988 sppr989 spwi021 spwi102 spwi104 spwi105 spwi107 spwi108 spwi111
  spwi113 spwi114 spwi116 spwi117 spwi118 spwi201 spwi203 spwi208 spwi210 spwi211 spwi212 
  spwi214 spwi221 spwi223 spwi299 spwi303 spwi305 spwi306 spwi310 spwi311 spwi312 spwi316 
  spwi317 spwi401 spwi405 spwi406 spwi411 spwi412 spwi414 spwi507 spwi508 spwi509 spwi518 
  spwi599 spwi601 spwi607 spwi608 spwi610 spwi612 spwi616 spwi617 spwi619 spwi704 spwi706 
  spwi708 spwi711 spwi804 spwi805 spwi888 spwi961 spwi962 spwi963 spwi964 spwi965 spwi983 
  spwi987 spwi988 spwi990 spwi991 spwi992 spwi993
BEGIN

  COPY_EXISTING ~%file%.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_timing = 1 duration = 0 END
    LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_timing = 9 duration = 0 END
    BUT_ONLY IF_EXISTS

END

/////                                                  \\\\\
///// clean up shapeshifting                           \\\\\
/////                                                  \\\\\

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_shapeshifts BEGIN
  spin107,   s1-12, druidpb, 3 => spin107 // shapeshift polar bear
  spin110, wolfwi2, druidww, 1 => spin110 // shapeshift winter wolf
  spin111,   s5-20, druidbb, 2 => spin111 // shapeshift boring beetle
END
ACTION_IF !game_is_iwd BEGIN // elemental shapeshifts added in how
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_shapeshifts BEGIN
    spin141,   felem,   felem, 4 => spin141 // shapeshift fire elemental
    spin142,   eelem,   eelem, 4 => spin142 // shapeshift earth elemental
    spin143,   welem,   welem, 4 => spin143 // shapeshift water elemental
  END
END

ACTION_PHP_EACH cd_shapeshifts AS params => spell BEGIN

  COPY_EXISTING ~%params_1%.itm~ ~override/%params_2%.itm~ // create unique weapon for non-elemental shifts
    WRITE_BYTE 0x18 THIS | BIT6 // flag as magical
    WRITE_LONG 0x60 params_3    // enchantment
    
  ACTION_IF ("%params_1%" STRING_COMPARE_CASE "%params_2%") BEGIN // if not the same
  
    COPY_EXISTING ~%spell%.spl~ ~override~ // update spells to new items
      LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = EVAL ~%params_2%~ END
      BUT_ONLY
      
    COPY_EXISTING ~%params_2%.cre~ ~override~
      REPLACE_CRE_ITEM ~%params_2%~ #0 #0 #0 NONE WEAPON1 EQUIP
      BUT_ONLY IF_EXISTS

  END
  
END

// clean up 'return to human' spells
COPY_EXISTING ~pself0.spl~  ~override~
              ~pself00.spl~ ~override~
              ~spin122.spl~ ~override~
              ~spin123.spl~ ~override~
              ~spin124.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 112 opcode = 111 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 112 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 111 opcode = 112 STR_VAR insert = below END
  LPF ALTER_EFFECT  INT_VAR match_opcode = 135 opcode = 276 parameter2 = 112 timing = 1 duration = 0 END
  PATCH_FOR_EACH spell IN spin107 spin110 spin111 BEGIN
    LPF CLONE_EFFECT INT_VAR match_opcode = 276 opcode = 254 parameter2 = 0 STR_VAR resource = EVAL ~%spell%~ END
  END
  PATCH_IF !game_is_iwd BEGIN // elementals shapeshifts added in how
    PATCH_FOR_EACH spell IN spin141 spin142 spin143 BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 276 opcode = 254 parameter2 = 0 STR_VAR resource = EVAL ~%spell%~ END
    END
  END  
  BUT_ONLY  

/////                                                  \\\\\
///// other spell fixes                                \\\\\
/////                                                  \\\\\

// cure X spells, hp drain spells shouldn't work on undead/extraplanar, like the cause X spells
COPY_EXISTING ~spin109.spl~  ~override~ // lay on hands
              ~sppr103.spl~  ~override~ // Cure Light Wounds
              ~sppr214.spl~  ~override~ // Cure Moderate Wounds
              ~sppr314.spl~  ~override~ // Cure Disease
              ~sppr315.spl~  ~override~ // Cause Disease
              ~sppr401.spl~  ~override~ // Cure Serious Wounds
              ~sppr502.spl~  ~override~ // Cure Critical Wounds
              ~sppr607.spl~  ~override~ // Heal
              ~spwi119.spl~  ~override~ // larloch's minor drain
              ~spwi314.spl~  ~override~ // vampiric touch
  LPF CLONE_EFFECT INT_VAR match_opcode = 233 opcode = 290 parameter2 = 55 timing = 0 duration = 1 resist_dispel = 0 STR_VAR insert = first resource = EVAL ~%SOURCE_RES%~ END 
  BUT_ONLY IF_EXISTS // cause disease not in iwd

// no portrait icon for creatures that can't be charmed
COPY_EXISTING ~spin119.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR parameter2 = 6 match_opcode = 5 opcode = 206 power = 0 timing = 0 duration = 0 resist_dispel = 0 STR_VAR insert = first resource = EVAL "%SOURCE_RES%" END
  BUT_ONLY 

// targeting prevents effects
COPY_EXISTING ~spin121.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR target = 1 END
  BUT_ONLY

// power issues for innate
COPY_EXISTING ~spin125.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR power = 0 END
  BUT_ONLY

// deafness should protect against spells and abilities that rely on hearing
COPY_EXISTING ~spin134.spl~ ~override~ // Mournful Wail
              ~spin135.spl~ ~override~ // Death Knell
              ~spin136.spl~ ~override~ // War Cry
              ~spin137.spl~ ~override~ // Undying Lament
              ~spin138.spl~ ~override~ // Great Roar
              ~sppr102.spl~ ~override~ // Command
              ~sppr518.spl~ ~override~ // Greater Command
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 206 parameter2 = 66 target = 2 duration = 1 STR_VAR resource = EVAL ~%SOURCE_RES%~ END
  BUT_ONLY IF_EXISTS

// disintegrate, petrification should have MR checks
COPY_EXISTING ~spin170.spl~ ~override~ // flesh to stone (beholder version)
              ~spin171.spl~ ~override~ // disintegrate (beholder version)
              ~spwi024.spl~ ~override~ // flesh to stone (trap)
              ~spwi607.spl~ ~override~ // disintegrate (arcane)
              ~spwi614.spl~ ~override~ // flesh to stone (wizard version)
  LPF ALTER_EFFECT INT_VAR resist_dispel = 1 END
  BUT_ONLY IF_EXISTS

// cloud spells shouldn't affect golems
COPY_EXISTING ~spin170.spl~ ~override~ // flesh to stone (beholder version)
              ~sppr322.spl~ ~override~ // mold touch
              ~sppr418.spl~ ~override~ // poison
              ~sppr510.spl~ ~override~ // Insect Plague
              ~spwi004.spl~ ~override~ // stinking cloud (trap)
              ~spwi018.spl~ ~override~ // cloud kill (trap)
              ~spwi024.spl~ ~override~ // flesh to stone (trap)
              ~spwi213.spl~ ~override~ // stinking cloud
              ~spwi417.spl~ ~override~ // dolorous decay
              ~spwi503.spl~ ~override~ // cloud kill
              ~spwi615.spl~ ~override~ // flesh to stone
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 206 target = 2 parameter2 = 27 STR_VAR resource = EVAL ~%SOURCE_RES%~ END // golems
  BUT_ONLY IF_EXISTS

// visual shouldn't have min level
COPY_EXISTING ~spin972.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR dicesize = 0 END
  BUT_ONLY

// add string, portrait for panic effect
COPY_EXISTING ~spin975.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 parameter1 = 14007 timing = 1 resist_dispel = 1 savingthrow = BIT3 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 2 parameter2 = 36 duration = 70 resist_dispel = 1 savingthrow = BIT3 END
  BUT_ONLY

// visual shouldn't have min level, power
COPY_EXISTING ~spin980.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR power = 0 dicesize = 0 END
  BUT_ONLY

// change berserk type, name spell
COPY_EXISTING ~spin990.spl~ ~override~ // Sprays Spores (Myconid)
  WRITE_LONG 0x08 4386 // name ability so "sprays spores"
  LPF ALTER_EFFECT INT_VAR match_opcode = 3 parameter2 = 1 END
  BUT_ONLY

// add portrait icons for effects
COPY_EXISTING ~spin992.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 2 parameter2 = 14 resist_dispel = 2 savingthrow = BIT2 probability1 =  40 probability2 = 21 duration = 18 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 2 parameter2 = 35 resist_dispel = 2 savingthrow = BIT2 probability1 =  80 probability2 = 41 duration = 70 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 2 parameter2 =  7 resist_dispel = 3 savingthrow = BIT2 probability1 = 100 probability2 = 81 duration = 70 END
  BUT_ONLY

// string bypasses MR
COPY_EXISTING ~spitm02.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR resist_dispel = 3 END
  BUT_ONLY
  
// mantle of hells' furnace should only charm fire elementals (elves handled in batch patches)
COPY_EXISTING ~spitm99.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 206 target = 2 parameter2 =  4 duration = 1 STR_VAR resource = spitm99 END // block non-fire-dwelling
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 206 target = 2 parameter2 = 10 duration = 1 STR_VAR resource = spitm99 END // block non-elementals
  BUT_ONLY

// string should bypass MR
COPY_EXISTING ~sppr101.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR resist_dispel = 3 END
  BUT_ONLY

// low level creature can't save; sound effect missing max level
COPY_EXISTING ~sppr102.spl~ ~override~
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 174 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 174 target = 2 power = 1 timing = 1 resist_dispel = 1
    insert_point = 5 dicenumber = 100 dicesize = 6 savingthrow = BIT0 STR_VAR resource = "eff_p04" END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 174 target = 2 power = 1 timing = 1 resist_dispel = 1
    insert_point = 2 dicenumber = 5 STR_VAR resource = "eff_p04" END

// because entangle and grease both set movement speeds, can actually escape an entangle if grease is cast
COPY_EXISTING ~sppr105.spl~ ~override~ // Entangle
  LPF CLONE_EFFECT INT_VAR check_globals = 0 match_opcode = 126 opcode = 206 parameter1 = 0 parameter2 = 0 STR_VAR resource = spwi101 END // Grease
  BUT_ONLY

// power, MR issues
COPY_EXISTING ~sppr108.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 254 power = 1 END // only for totlm
  LPF ALTER_EFFECT INT_VAR match_opcode =  23 resist_dispel = 3 END
  BUT_ONLY

// curse not affecting victim saves, string bypasses MR
COPY_EXISTING ~sppr111.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 238 target = 2 power = 1 resist_dispel = 1 duration = 42 parameter1 = "-1" END
  BUT_ONLY

// barkskin not improving AC as it should
COPY_EXISTING ~sppr202.spl~ ~override~
  READ_SHORT 0x68 abil_num
  LPF ALTER_EFFECT INT_VAR header = 0 match_opcode = 0 parameter1 = 6 END
  FOR (index = 1 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_opcode = 0 parameter1 = (6 - ((index + 3) / 4)) END
  END  
  BUT_ONLY

// chant's caster effects should bypass protections
COPY_EXISTING ~sppr203.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_target = 1 power = 0 END
  BUT_ONLY

// delayed sound bypasses save
COPY_EXISTING ~sppr204.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 savingthrow = BIT0 END
  BUT_ONLY

// clone find traps into internal version used by traps themselves
COPY_EXISTING ~sppr205.spl~ ~override/sppr299.spl~ // find traps
  WRITE_ASCII 0x3a ~~ #8
  WRITE_SHORT 0x22 0
  LPF ALTER_SPELL_HEADER INT_VAR speed = 0 END
  PATCH_FOR_EACH op IN 142 9 146 174 233 BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode = op END
  END  

// delayed 146s actually induce casting sequence, so change caster to specifics 118; 
// nearby traps detect this and reveal themselves by casting sppr299 from their trap scripts
COPY_EXISTING ~sppr205.spl~  ~override~ // find traps
  LPF DELETE_EFFECT INT_VAR match_opcode = 146 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 150 END
  LPF CLONE_EFFECT INT_VAR match_opcode = 142 opcode = 72 power = 0 parameter1 = 118 parameter2 = 4 END
  BUT_ONLY

// goodberries last 1 day/caster level
COPY_EXISTING ~sppr207.spl~ ~override~
  READ_LONG  0x64 abil_off
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_opcode = 122 duration = (7200 * (index + 3)) END
  END    
  BUT_ONLY

// some effects bypass MR; duration for hold person should be two rounds/level
COPY_EXISTING ~sppr208.spl~ ~override~
  READ_SHORT  0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_duration = ((index * 7) + 21) duration = ((index * 14) + 42) resist_dispel = 1 END
  END
  BUT_ONLY

// minor reliability corrections
COPY_EXISTING ~sppr208.spl~ ~override~ // Hold Person (divine version)
              ~sppr305.spl~ ~override~ // Hold Animal
              ~spwi306.spl~ ~override~ // Hold Person (arcane version)
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 206 duration = 1 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 290 duration = 1 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 175 parameter1 = 0 parameter2 = 2 END
  BUT_ONLY

// sound bypasses save
COPY_EXISTING ~sppr211.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 savingthrow = BIT0 END
  BUT_ONLY

// animate dead should last 12 hours, summons should always bypass spell protections
COPY_EXISTING ~sppr301.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 2880 duration = 2400 power = 0 END
  BUT_ONLY

// string bypassing MR
COPY_EXISTING ~sppr303.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
  BUT_ONLY

// sound bypassing save, string/visual bypassing MR
COPY_EXISTING ~sppr305.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 233 resist_dispel = 1 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 savingthrow = BIT0 END
  BUT_ONLY
  
// sound plays on wrong target, wrong school
COPY_EXISTING ~sppr309.spl~ ~override~ // invisibility purge
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 target = 2 END // play on targets, not caster
  BUT_ONLY

// sound bypassing save, string/visual bypassing MR
COPY_EXISTING ~sppr310.spl~ ~override~ // miscast magic
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 savingthrow = BIT0 savebonus = "-2" END
  LPF CLONE_EFFECT INT_VAR match_opcode = 60 parameter2 = 1 END // clone arcane miscast into divine
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~sppr398.spl~ ~override~
              ~sppr399.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 246 power = 0 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~sppr402.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 power = 0 END
  BUT_ONLY

// remove charm, stun immunity from free action; have to remove all 261s and then re-add
COPY_EXISTING ~sppr403.spl~  ~override~
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 261 END
//  LPF ALTER_EFFECT INT_VAR target = 2 END
  READ_SHORT 0x68 abil_num
  FOR (index = 1 ; index < (abil_num + 1) ; ++index) BEGIN
    PATCH_FOR_EACH addme IN 175 158 157 154 126 109 40 BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 261 target = 2 power = 4
         parameter2 = "%addme%" header = index duration = (420 + (70 * index)) END
    END
  END
  BUT_ONLY

// neutralize poison should bypass MR
COPY_EXISTING ~sppr404.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR resist_dispel = 3 END
  BUT_ONLY

// giant insect should last eight hours, summons should always bypass spell protections
COPY_EXISTING ~sppr410.spl~ ~override~ // Giant Insect
  LPF ALTER_EFFECT INT_VAR match_duration = 3360 duration = 2400 power = 0 END
  BUT_ONLY

// produce fire should only apply once per round
COPY_EXISTING ~sppr411.spl~ ~override~ // Produce Fire
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 206 duration = 7 END // if evaded, remain immune for a round; silent to avoid warning w/o how
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 duration = 7 STR_VAR resource = SPPR419 END // if affected, also remain immune for further damage until next round
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~sppr501.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 power = 0 END
  BUT_ONLY

// bad caster effects shouldn't be blocked
COPY_EXISTING ~sppr507.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_target = 1 power = 0 END
  BUT_ONLY

// per their descript, chaotic commands & impervious sanctity should prevent sleep; add to immunity batches to round out
COPY_EXISTING ~sppr508.spl~ ~override~ // Chaotic Commands
              ~sppr716.spl~ ~override~ // impervious sanctity of mind
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 261 parameter2 = 39  END // sleep
  BUT_ONLY

// sound/damage bypassing MR; strings for low HD effects; panic from insect plague should affect creatures up to *and including* 5 HD
COPY_EXISTING ~sppr510.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode =  12 resist_dispel = 1 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 resist_dispel = 1 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 power = 5 parameter1 = 14007 timing = 1
    resist_dispel = 1 dicenumber = 2 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 power = 5 parameter1 = 14007 timing = 1
    resist_dispel = 1 dicenumber = 5 dicesize = 3 savingthrow = BIT0 END
  LPF ALTER_EFFECT INT_VAR match_dicesize = 3 match_dicenumber = 4 dicenumber = 5 END
  LPF ALTER_EFFECT INT_VAR match_duration = 35 duration = 7 END // portrait durations wrong
  BUT_ONLY
  
// consistent resist/dispel
COPY_EXISTING ~sppr512.spl~ ~override~ // spike stones
  LPF ALTER_EFFECT INT_VAR resist_dispel = 0 END // was mix of 0,2
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~sppr602.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 power = 0 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~sppr605.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 246 power = 0 END
  BUT_ONLY

// entropy shield blocks 'missile-based' attacks; missing protections from
// reg darts & bullets, magic missile, melf's acid arrow, flame arrow, chromatic orb, 
// sol's searing orb, otiluke's freezing sphere, icelance, lance of disruption, magic stone, MFM
COPY_EXISTING ~sppr609.spl~ ~override~ // entropy shield
  PATCH_FOR_EACH proj IN 2 7 20 24 35 59 101 102 217 251 269 271 313 BEGIN
    LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 83 match_parameter2 = 1 parameter2 = proj END
  END
  PATCH_FOR_EACH spell IN spwi422 BEGIN // mfm
    LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 83 match_parameter2 = 1 opcode = 206 parameter2 = 0 STR_VAR resource = EVAL ~%spell%~ END
  END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~sppr702.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 246 power = 0 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~sppr704.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 power = 0 END
  BUT_ONLY

// fire storm damage wrong at level 15
COPY_EXISTING ~sppr705.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR header = 2 match_opcode = 12 match_parameter1 = 6 parameter1 = 7 END
  BUT_ONLY

// blindness via sunray should be 1d3 rounds, not a fixed two; string bypassing MR
COPY_EXISTING ~sppr707.spl~ ~override~ // Sunray
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
  LPF ALTER_EFFECT INT_VAR match_duration = 14 duration = 7 probability1 = 33 END
  LPF CLONE_EFFECT INT_VAR match_duration =  7 duration = 21 probability1 = 100 probability2 = 67 END
  LPF CLONE_EFFECT INT_VAR match_duration =  7 duration = 14 probability1 =  66 probability2 = 34 END
  BUT_ONLY

// range corrections
COPY_EXISTING ~sppr714.spl~ ~override~ // Symbol, Pain (divine version)
              ~sppr715.spl~ ~override~ // Symbol, Hopelessness (divine version)
  LPF ALTER_SPELL_HEADER INT_VAR range = 30 END
  BUT_ONLY

// hope should cancel hopelessness and vice versa; see spin134, spwi421
COPY_EXISTING ~sppr715.spl~ ~override~ // Symbol: Hopelessness
              ~spwi411.spl~ ~override~ // Emotion: Hopelessness
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 254 target = 2 STR_VAR resource = spwi421 END // remove Emotion: Hope
  BUT_ONLY

// sounds have wrong target
COPY_EXISTING ~sppr716.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 target = 1 END
  BUT_ONLY

// let sound always play for trap spell
COPY_EXISTING ~sppr984.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 power = 0 resist_dispel = 0 END
  BUT_ONLY

// no save for sound
COPY_EXISTING ~sppr988.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 savingthrow = BIT0 END
  BUT_ONLY
  
// block ancillary hold effects from creatures not held
COPY_EXISTING ~sppr989.spl~ ~override~ // trap_hold_person
  LPF CLONE_EFFECT INT_VAR match_opcode = 175 opcode = 290 parameter1 = 0 parameter2 = 6 timing = 0 duration = 0 STR_VAR insert = first resource = sppr989 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 175 parameter1 = 0 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 142 power = 2 END // portrait icon has no power
  BUT_ONLY

// like suffocate, cloud kill and stinking cloud shouldn't work on 'non-breathing' creatures
// not extending this to death fog, incendiary cloud, or cloud of pestilence
COPY_EXISTING ~spwi004.spl~ ~override~ // stinking cloud via trap
              ~spwi018.spl~ ~override~ // cloud kill via trap
              ~spwi213.spl~ ~override~ // stinking cloud 
              ~spwi503.spl~ ~override~ // cloud kill
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 206 target = 2 parameter2 = 48 duration = 1 STR_VAR resource = EVAL ~%SOURCE_RES%~ END
  BUT_ONLY

// trap arrow damage targets self; change damage types while we're here
COPY_EXISTING ~spwi006.spl~ ~override~
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 12 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 12 target = 2 parameter2 = 0x00800000 timing = 1 dicenumber = 1 dicesize = 6 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 12 target = 2 parameter2 = 0x00040000 timing = 9 dicenumber = 2 dicesize = 6 END
  BUT_ONLY

// trap arrow damage targets self
COPY_EXISTING ~spwi007.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 target = 2 END
  BUT_ONLY

// trap kill effect bypassing MR
COPY_EXISTING ~spwi020.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 13 resist_dispel = 1 END
  BUT_ONLY

// self-sound should bypass MR/save, visual bypassing MR
COPY_EXISTING ~spwi021.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 power = 0 resist_dispel = 0 savingthrow = 0 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 233 resist_dispel = 1 END
  BUT_ONLY

// undead not being protected from horror trap
COPY_EXISTING ~spwi025.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 206 STR_VAR resource = "spwi025" END
  BUT_ONLY

// armor should last nine hours
COPY_EXISTING ~spwi102.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 3780 duration = 2700 END
  BUT_ONLY

// crazy staggered damage to get 1d3+2/level (save for half) correct, rounding up 0.5s to 1s - had to sit down and do some math for this one
// caster pause shouldn't be blocked by spell protections
COPY_EXISTING ~spwi103.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 12 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 165 power = 0 dicesize = 0 END
  FOR (index = 1 ; index < 11 ; ++index) BEGIN
    // always damage
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 99 header = index opcode = 12 target = 2 power = 1 timing = 1 parameter2 = 524288 resist_dispel = 1
      parameter1 = (index + 1) probability1 =  0 probability2 =  66 END
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 99 header = index opcode = 12 target = 2 power = 1 timing = 1 parameter2 = 524288 resist_dispel = 1
      parameter1 = (index + 2) probability1 =  67 probability2 =  100 END
    // damage that only applies with failed save
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 99 header = index opcode = 12 target = 2 power = 1 timing = 1 parameter2 = 524288 resist_dispel = 1
      parameter1 = index probability1 =  0 probability2 =  33 savingthrow = BIT0 END
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 99 header = index opcode = 12 target = 2 power = 1 timing = 1 parameter2 = 524288 resist_dispel = 1
      parameter1 = (index + 1) probability1 = 34 probability2 =  100 savingthrow = BIT0 END
  END
  BUT_ONLY

// caster pause shouldn't be blocked by spell protections, visual bypassing MR
COPY_EXISTING ~spwi105.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 165 power = 0 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 233 resist_dispel = 1 END
  BUT_ONLY

// visual has 0 power
COPY_EXISTING ~spwi108.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 233 power = 2 END
  BUT_ONLY

// per descript, infravision lasts 8 hours
 COPY_EXISTING ~spwi111.spl~ ~override~ // Infravision
  LPF ALTER_EFFECT INT_VAR match_duration = 3200 duration = 2400 END
  BUT_ONLY

// misordering of spell protections prevents effects; one spell ref wrong
COPY_EXISTING ~spwi113.spl~ ~override~
  WRITE_LONG 0x50 12148 // base iwd uses divine descript
  BUT_ONLY

// shield blocks direct targeting from mordy missiles, but not the splash; 139 has 0 power; block wand of magic missiles
COPY_EXISTING ~spwi114.spl~ ~override~ // Shield
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 power = 1 END
  LPF CLONE_EFFECT INT_VAR check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = 36 STR_VAR insert = below END // Wand of Magic Missiles
  LPF CLONE_EFFECT INT_VAR match_opcode = 142 opcode = 206 parameter2 = 0 STR_VAR resource = spwi422 insert = below END // mordy force missiles
  BUT_ONLY

// sound, visual not restricted to HD
COPY_EXISTING ~spwi116.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR dicenumber = 4 END
  BUT_ONLY
  
// chromatic orb causes double blindness messages at level 4; lev 7+ portrait icon bypassing MR
// level 1 does something random instead of blind
COPY_EXISTING ~spwi118.spl~ ~override~
  // add blindness effects to level 1 header; nuke existing below
  LPF ADD_SPELL_EFFECT INT_VAR header = 1 opcode =  12 target = 2 power = 1 resist_dispel = 1 
    parameter2 = 0x00400000 timing = 1 savingthrow = BIT0 dicenumber = 1 dicesize = 4 END
  LPF ADD_SPELL_EFFECT INT_VAR header = 1 opcode =  74 target = 2 power = 1 resist_dispel = 1 
    duration = 7 savingthrow = BIT0 END
  LPF ADD_SPELL_EFFECT INT_VAR header = 1 opcode = 142 target = 2 power = 1 resist_dispel = 1 
    parameter2 = 8 duration = 7 savingthrow = BIT0 END
  LPF ADD_SPELL_EFFECT INT_VAR header = 1 opcode = 174 target = 2 power = 1 resist_dispel = 1 
    timing = 4 duration = 7 savingthrow = BIT0 STR_VAR resource = eff_m05 END
  LPF ADD_SPELL_EFFECT INT_VAR header = 1 opcode = 233 target = 2 power = 1 resist_dispel = 3
    timing = 1 parameter2 = 3 savingthrow = BIT0 END
  LPF ADD_SPELL_EFFECT INT_VAR header = 1 opcode = 139 target = 2 power = 1 resist_dispel = 1
    timing = 1 parameter1 = 14674 savingthrow = BIT0 END
  LPF ADD_SPELL_EFFECT INT_VAR header = 1 opcode = 174 target = 2 power = 1 resist_dispel = 2
    timing = 1 STR_VAR resource = eff_m06 END
  READ_LONG  0x64 abil_off ELSE 0
  READ_SHORT 0x68 abil_num ELSE 0
  READ_LONG  0x6a fx_off   ELSE 0
  SET abil_length = 0x28
  SET delta = 0
  FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN // iterate through abilities
    READ_SHORT  (abil_off + 0x1e + (abil_length * index)) abil_fx_num
    READ_SHORT  (abil_off + 0x20 + (abil_length * index)) abil_fx_idx
    SET abil_fx_idx = (abil_fx_idx + delta)
    WRITE_SHORT (abil_off + 0x20 + (abil_length * index)) abil_fx_idx
    PATCH_IF (index = 0) BEGIN
      DELETE_BYTES (fx_off +        (0x30 * (abil_fx_idx))) (0x30 * (abil_fx_num - 7)) // nuke old lev1 effects
      SET delta       -= (abil_fx_num - 7)
      SET index2      -= (abil_fx_num - 7)
      SET abil_fx_num -= (abil_fx_num - 7)
    END
    FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
      READ_SHORT (fx_off +        (0x30 * (abil_fx_idx + index2))) opcode
      READ_SHORT (fx_off + 0x04 + (0x30 * (abil_fx_idx + index2))) string
      READ_LONG  (fx_off + 0x0e + (0x30 * (abil_fx_idx + index2))) duration
      PATCH_IF ((opcode = 139) AND (string = 14071)) BEGIN // 'blindness' string
        DELETE_BYTES (fx_off +        (0x30 * (abil_fx_idx + index2))) 0x30 // delete effect
        SET delta       -= 1
        SET index2      -= 1
        SET abil_fx_num -= 1
      END
      PATCH_IF ((opcode = 142) AND (index = 5)) BEGIN // portrait icon
        WRITE_BYTE (fx_off + 0x0c + (0x30 * (abil_fx_idx + index2))) 0 // instant/limited
        WRITE_BYTE (fx_off + 0x0d + (0x30 * (abil_fx_idx + index2))) 1 // don't bypass MR
      END
    END
    WRITE_SHORT  (abil_off + 0x1e + (abil_length * index)) abil_fx_num
  END
  BUT_ONLY_IF_IT_CHANGES

// horror duration should be one turn not four rounds; undocumented save bonus
COPY_EXISTING ~spwi205.spl~ ~override~ // Horror
  LPF ALTER_EFFECT INT_VAR match_duration = 28 duration = 70 savebonus = 0 END
  LPF ALTER_EFFECT INT_VAR savebonus = 0 END
  BUT_ONLY

// should last 4 hours
COPY_EXISTING ~spwi206.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 1680 duration = 1200 END
  BUT_ONLY

// knock should bypass MR
COPY_EXISTING ~spwi207.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR resist_dispel = 3 END
  BUT_ONLY

// shouldn't bypass MR
COPY_EXISTING ~spwi208.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR resist_dispel = 1 END
  BUT_ONLY

// melf acid arrow lacks casting sound, header corrections
COPY_EXISTING ~spwi211.spl~ ~override~
  WRITE_ASCII 0x10 "cas_m03" #8
  LPF ALTER_SPELL_HEADER INT_VAR type_new = 1 END
  BUT_ONLY

// lasts 1.2 hours/level istead of 1/level
COPY_EXISTING ~spwi214.spl~ ~override~ // Strength
              ~spwi223.spl~ ~override~ // Cat's Grace
  READ_SHORT  0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_duration = ((index + 3) * 360) duration = ((index + 3) * 300) END
  END
  BUT_ONLY IF_EXISTS // cat's grace is how

// wrong sound reference
COPY_EXISTING ~spwi214.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 STR_VAR match_resource = eff_m12 resource = eff_e03 END
  BUT_ONLY

// caster pause shouldn't be blocked by spell protections
COPY_EXISTING ~spwi217.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 165 power = 0 END
  BUT_ONLY

// ghoul touch should match its description: see ghoult.itm, scrl1c.itm, and kuorrick.bcs et al
COPY_EXISTING ~spwi218.spl~ ~override~
  LPF DELETE_EFFECT END // delete existing effects
  LPF ALTER_SPELL_HEADER INT_VAR target = 5 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 111 target = 1 power = 2 duration = 42 resist_dispel = 3 STR_VAR resource = GHOULT END // create ghoult as magical weapon
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 233 target = 1 power = 2 parameter2 = 4 timing = 1 resist_dispel = 3 END // sparklies
  BUT_ONLY

// sound blocked by MR
COPY_EXISTING ~spwi221.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 resist_dispel = 3 END
  BUT_ONLY

// sound missing save, should last eight hours
COPY_EXISTING ~spwi222.spl~ ~override~ // blindness
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 savingthrow = BIT0 END
  LPF ALTER_EFFECT INT_VAR match_duration = 3360 duration = 2400 END
  BUT_ONLY

// string bypasses MR
COPY_EXISTING ~spwi302.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
  BUT_ONLY

// change haste to visual range
COPY_EXISTING ~spwi305.spl~ ~override~
  LPF ALTER_SPELL_HEADER INT_VAR range = 30 END
  BUT_ONLY

// duration for hold person should be two rounds/level; sound/visual bypassing MR
COPY_EXISTING ~spwi306.spl~ ~override~
  READ_SHORT  0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_duration = ((index * 7) + 35) duration = ((index * 14) + 70) END
  END
  LPF ALTER_EFFECT INT_VAR resist_dispel = 1 END // 
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 206 resist_dispel = 2 END // iwd w/o how
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 290 resist_dispel = 2 END // how, totlm
  BUT_ONLY
  
// ms1 should summon 2d3 monsters, not 2d4, summons should always bypass spell protections
COPY_EXISTING ~spwi309.spl~ ~override~ // Monster Summoning I
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 dicesize = 3 power = 0 END
  BUT_ONLY

// non-detection should be 7 turns/level
COPY_EXISTING ~spwi310.spl~ ~override~
  READ_SHORT  0x68 abil_num
  PATCH_IF game_is_iwd BEGIN SET scale = 49 END ELSE BEGIN SET scale = 420 END
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_duration = ((index + 5) * scale) duration = ((index + 5) * 490) END
  END
  BUT_ONLY

// skull trap damage is incorrect 
COPY_EXISTING ~spwi313.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 174 power = 0 END
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_opcode = 12 match_savingthrow =    0 dicesize = 6 dicenumber = ((index + 6) / 2) END
    LPF ALTER_EFFECT INT_VAR header = index match_opcode = 12 match_savingthrow = BIT1 dicesize = 6 dicenumber = ((index + 5) / 2) END
  END
  BUT_ONLY  

// vamp touch shouldn't bypass MR
COPY_EXISTING ~spwi314.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 241 resist_dispel = 1 END
  BUT_ONLY

// color set should bypass MR
COPY_EXISTING ~spwi317.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 7 resist_dispel = 3 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi407.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 power = 0 END
  BUT_ONLY
  
// stoneskin should be 1d4 skins +1 skin per 2 levels; was defaulting to 2 +1 skin per 2 levels
COPY_EXISTING ~spwi408.spl~ ~override~ // Stoneskin
  READ_SHORT 0x68 abil_num
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    LPF ALTER_EFFECT INT_VAR header = index match_opcode = 218 match_parameter1 = (index + 5) parameter1 = (index + 4) probability1 =  24 probability2 =  0 END // 1 on 1d4
    LPF CLONE_EFFECT INT_VAR header = index match_opcode = 218 match_parameter1 = (index + 4) parameter1 = (index + 7) probability1 = 100 probability2 = 75 END // 4 on 1d4
    LPF CLONE_EFFECT INT_VAR header = index match_opcode = 218 match_parameter1 = (index + 4) parameter1 = (index + 6) probability1 =  74 probability2 = 50 END // 3 on 1d4
    LPF CLONE_EFFECT INT_VAR header = index match_opcode = 218 match_parameter1 = (index + 4) parameter1 = (index + 5) probability1 =  49 probability2 = 25 END // 2 on 1d4
  END
  BUT_ONLY 

// string bypassing MR
COPY_EXISTING ~spwi411.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
  BUT_ONLY

// missing save
COPY_EXISTING ~spwi413.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR savingthrow = BIT0 END
  BUT_ONLY

// spirit armor damage shouldn't dispel
COPY_EXISTING ~spwi414.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 resist_dispel = 2 END
  BUT_ONLY

// BBB not always checking MR correctly  
COPY_EXISTING ~spwi417.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_resist_dispel = 1 resist_dispel = 3 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi418.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 248 power = 0 END
  BUT_ONLY

// effects should bypass MR; emotions courage and fear should cancel each other
COPY_EXISTING ~spwi419.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR resist_dispel = 3 END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 254 target = 2 power = 4 timing = 1 resist_dispel = 3 STR_VAR resource = spwi420 END // remove Emotion: Fear
  BUT_ONLY

// add panic string to spell; emotions courage and fear should cancel each other
COPY_EXISTING ~spwi420.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 power = 4 parameter1 = 14007 timing = 1 resist_dispel = 1 savingthrow = BIT0 END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 254 target = 2 resist_dispel = 1 savingthrow = BIT0 STR_VAR resource = spwi419 END // remove Emotion: Courage
  BUT_ONLY

// hope should cancel hopelessness and vice versa; see spin134, sppr715, spwi411
COPY_EXISTING ~spwi421.spl~ ~override~ // Emotion: Hope
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 254 target = 2 STR_VAR resource = spwi411 END // remove Emotion: Hopelessness
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 254 target = 2 STR_VAR resource = sppr715 END // remove Symbol: Hopelessness
  PATCH_IF !game_is_iwd BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 254 target = 2 STR_VAR resource = spin134 END // remove Mournful Wail
  END
  BUT_ONLY

// summons should always bypass spell protections; spell icon
COPY_EXISTING ~spwi501.spl~ ~override~
  WRITE_ASCII 0x3a ~spwi501c~
  LPF ALTER_EFFECT INT_VAR match_opcode = 243 power = 0 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi505.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 power = 0 END
  BUT_ONLY

// string bypassing MR
COPY_EXISTING ~spwi508.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
  BUT_ONLY

// visual effects not following min/max levels
COPY_EXISTING ~spwi509.spl~ ~override~
  READ_BYTE 0x68 abil_num
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 233 END
  LPF ALTER_EFFECT INT_VAR match_savebonus = "-2" savebonus = "-4" END // chaos save should be -4 for >4HD, not -2
  FOR (index = 1 ; index < (abil_num + 1) ; ++index) BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 233 target = 2 power = 5 parameter2 = 13 timing = 1 resist_dispel = 1
      savingthrow = BIT0 savebonus = "-4" dicesize = 5 insert_point = 99 header = index END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 233 target = 2 power = 5 parameter2 = 13 timing = 1 resist_dispel = 1 
      dicenumber = 4 insert_point = 99 header = index END
  END
  BUT_ONLY

// per descript feeblemind is permanent, not 20 rounds; string bypassing MR
COPY_EXISTING ~spwi510.spl~ ~override~ // Feeblemind
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_duration = 140 duration = 3600000 END // totlm
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi512.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 248 power = 0 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi513.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 246 power = 0 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi514.spl~ ~override~
              ~spwi515.spl~ ~override~
              ~spwi516.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 246 power = 0 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi598.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 246 power = 0 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi599.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 243 power = 0 END
  BUT_ONLY

// dispel needs power 0
COPY_EXISTING ~spwi601.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 58 power = 0 END
  BUT_ONLY
  
// kill effect misorder prevents other effects
COPY_EXISTING ~spwi606.spl~ ~override~ // death spell
              ~spwi607.spl~ ~override~ // disintegrate (actually fine in base iwd)
              ~spwi903.spl~ ~override~ // power word kill
  LPF CLONE_EFFECT  INT_VAR match_opcode = 13 STR_VAR insert = last END // clone to last effect
  LPF DELETE_EFFECT INT_VAR match_opcode = 13 multi_match = 1 END       // delete original effect
  BUT_ONLY

// fade should bypass MR
COPY_EXISTING ~spwi606.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 232 resist_dispel = 0 END
  BUT_ONLY

// visual bypassing save
COPY_EXISTING ~spwi607.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 233 savingthrow = BIT0 END
  BUT_ONLY

// invisble stalker is 2 hours = 10 turns = 100 rounds; summons should always bypass spell protections
COPY_EXISTING ~spwi609.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_duration = 840 power = 0 duration = 700 END
  BUT_ONLY

// change immunity opcode on lich touch so it'll get fixed by the immunity effect batches
COPY_EXISTING ~spwi610.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 101 opcode = 261 END // change immunity opcode
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi611.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 power = 0 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi613.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 248 power = 0 END
  BUT_ONLY

// make un-petrify stuff destroy the gorgon ring, otherwise victim will just re-petrify on load; see scrlpet, scrlpet2.itm
COPY_EXISTING ~spwi614.spl~  ~override~ // stone to flesh
  LPF CLONE_EFFECT INT_VAR match_opcode = 43 opcode = 112 timing = 1 STR_VAR resource = gorgon END
  BUT_ONLY

// tenser's transformation shouln't disable innate abilities 
COPY_EXISTING ~spwi616.spl~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 145 match_parameter2 = 2 END
  BUT_ONLY

// string, sound bypassing MR
COPY_EXISTING ~spwi617.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR resist_dispel = 1 END
  BUT_ONLY

// kill opcode bypassing MR
COPY_EXISTING ~spwi702.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 13 resist_dispel = 1 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi703.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 power = 0 END
  BUT_ONLY

// visual should target self
COPY_EXISTING ~spwi704.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 233 target = 1 END
  BUT_ONLY

// visual should bypass MR
COPY_EXISTING ~spwi706.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 233 resist_dispel = 0 END
  BUT_ONLY

// self-visual should bypass spell protections
COPY_EXISTING ~spwi707.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 233 power = 0 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi803.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 power = 0 END
  BUT_ONLY

// mind blank blocks command, but should also block greater command; duration fix
COPY_EXISTING ~spwi804.spl~ ~override~ // mind blank
  LPF ALTER_EFFECT INT_VAR match_timing = 0 duration = 7200 END // 24 hours
  LPF ALTER_EFFECT INT_VAR match_timing = 1 duration = 0 END // instant/permanent should be suration 0
  LPF CLONE_EFFECT INT_VAR opcode = 206 STR_VAR match_resource = sppr102 resource = sppr518 END // blocks command but not greater command
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi899.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 power = 0 END
  BUT_ONLY

// summons should always bypass spell protections
COPY_EXISTING ~spwi902.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 240 power = 0 END
  BUT_ONLY

ACTION_IF game_is_iwd THEN BEGIN // iwd w/o how

  // luck fixes, 3/4 (see spwi209.spl (x2), chance.itm, and lucky.itm)
  COPY_EXISTING ~spwi209.spl~ ~override~
    PATCH_FOR_EACH op IN 59 90 91 92 BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR opcode = op target = 2 power = 2 resist_dispel = 3 duration = 21 parameter1 = 5 END
    END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 238 target = 2 power = 2 resist_dispel = 3 duration = 21 parameter1 = 1 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 power = 2 resist_dispel = 3 duration = 21 STR_VAR resource = spwi209 END

END ELSE BEGIN // how, totlm fixes
  
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_evasion BEGIN
      spin161.spl  =>  12 // white dragon breath
      spin990.spl  => 174 // innate_myconid_spores
      sppr510.spl  =>  12 // insect plague
      sppr610.spl  =>  12 // blade barrier
      sppr983.spl  =>  12 // glyph of warding, trap
      sppr984.spl  =>  12 // chromatic orb, trap (flame strike?)
      spwi001.spl  =>  12 // fireball, trap
      spwi002.spl  =>  12 // lightning bolt, trap
      spwi021.spl  => 233 // color spray, trap
      spwi023.spl  =>  12 // 4d6 fireball, trap
      wand06.itm   =>  12 // wand of frost
      wandrea.itm  =>  12 // pomab's lightning attack
  END

  ACTION_PHP_EACH cd_evasion AS file => op BEGIN

    COPY_EXISTING ~%file%~ ~override~
      LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = op opcode = 206 parameter2 = 63 timing = 0 duration = 0 savingthrow = 0 
        dicesize = 0 dicenumber = 0 STR_VAR insert = first resource = EVAL ~%SOURCE_RES%~ END
      BUT_ONLY IF_EXISTS
      
  END

  // charm animal can charm non-animals (ok on base iwd)
  COPY_EXISTING ~spin108.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 206 target = 2 parameter2 = 8 resist_dispel = 2 STR_VAR resource = spin108 END

  // sound effect bypasses MR
  COPY_EXISTING ~spin127.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR resist_dispel = 1 END
    BUT_ONLY
  
  // poison effect using acid visual
  COPY_EXISTING ~spin129.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 233 parameter2 = 82 END
    BUT_ONLY
  
  // cosmetic effects using different parameters
  COPY_EXISTING ~spin130.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR check_globals = 0 power = 7 resist_dispel = 1 savingthrow = BIT0 END
    BUT_ONLY

  // pause caster shouldn't be blocked; string lacks save
  COPY_EXISTING ~spin131.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 savingthrow = BIT0 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 165 power = 0 END
    BUT_ONLY
  
  // effects bypassing MR
  COPY_EXISTING ~spin132.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 174 resist_dispel = 1 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 233 resist_dispel = 1 END
    BUT_ONLY
    
  // hope should cancel hopelessness and vice versa; see sppr715, spwi411, spwi421
  COPY_EXISTING ~spin134.spl~ ~override~ // Mournful Wail; do separately since it has a 206 at top of stack
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 1 opcode = 254 target = 2 STR_VAR resource = spwi421 END // remove Emotion: Hope
    BUT_ONLY
  
  // effects bypassing MR, mismatched durations
  COPY_EXISTING ~spin138.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 44 duration = 35 savingthrow = BIT0 savebonus = `4 END
    BUT_ONLY

  // bard song forces visibility
  COPY_EXISTING ~spin144.spl~ ~override~ // The Tale of Curran Strongheart
                ~spin145.spl~ ~override~ // Tymora's Melody
                ~spin146.spl~ ~override~ // The Song of Kaudies
                ~spin147.spl~ ~override~ // The Siren's Yearning
                ~spin148.spl~ ~override~ // War Chant of Sith
                ~spin151.spl~ ~override~ // The Ballad of Three Heroes 
    READ_LONG  0x64 abil_off
    READ_SHORT 0x68 abil_num
    READ_LONG  0x6a fx_off
    WRITE_SHORT 0x70 THIS + 1
    INSERT_BYTES fx_off 0x30
    WRITE_SHORT (fx_off + 0x00) 136 // force visible
    WRITE_BYTE  (fx_off + 0x02)   1 // target: self
    WRITE_BYTE  (fx_off + 0x0c)   1 // timing
    WRITE_BYTE  (fx_off + 0x12) 100 // probability
    FOR (index = 0 ; index < abil_num ; ++index) BEGIN
      WRITE_SHORT (abil_off + 0x20 + (index * 0x28)) THIS + 1 // push back indices
    END
    BUT_ONLY  
  
  // string bypassing MR
  COPY_EXISTING ~spin147.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
    BUT_ONLY

  // power issues for innate
  COPY_EXISTING ~spin159.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR power = 0 END
    BUT_ONLY

  // sunscorch now does extra damage to myconids in addition to undead
  COPY_EXISTING ~sppr113.spl~ ~override~ // Sunscorch
    LPF ALTER_EFFECT INT_VAR match_opcode = 206 match_parameter2 = 2 parameter2 = 32 END
    BUT_ONLY

  // stalker, cause disease should last eight hours 
  COPY_EXISTING ~sppr315.spl~ ~override~
                ~sppr723.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_duration = 2880 duration = 2400 END
    BUT_ONLY
  
  // self-visuals and movement speed shouldn't be blocked
  COPY_EXISTING ~sppr319.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_target = 1 power = 0 END
    BUT_ONLY
  
  // 2d3 damage to cold/fire creatures should be at 100%, not 50
  COPY_EXISTING ~sppr321.spl~  ~override~ // cloudburst: 50% lightning strike 
    LPF ALTER_EFFECT INT_VAR match_opcode = 12 match_dicenumber = 2 probability1 = 100 END 
    LPF ALTER_EFFECT INT_VAR match_opcode = 206 match_parameter2 = 0 duration = 7 END // durations for spell protections lasting waaaaay too long
    BUT_ONLY
  
  // add blind icon
  COPY_EXISTING ~sppr324.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 2 power = 3 parameter2 = 8 resist_dispel = 1
      duration = 7 savingthrow = BIT0 END
    BUT_ONLY
  
  // add 'weakened' string
  COPY_EXISTING ~sppr325.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 power = 3 parameter1 = 10039 timing = 1
      resist_dispel = 1 savingthrow = BIT0 END
    BUT_ONLY

  COPY_EXISTING ~sppr415.spl~  ~override~ // blood rage
    LPF CLONE_EFFECT INT_VAR match_opcode = 261 match_parameter2 = 17 parameter2 = 98 END // also block regeneration
    LPF CLONE_EFFECT INT_VAR match_opcode = 261 match_parameter2 = 17 parameter2 = 18 END // and max hp bonus effects
    BUT_ONLY  
    
  COPY_EXISTING ~sppr417.spl~ ~override~ // cloud of pestilence
    LPF ALTER_EFFECT INT_VAR match_duration = 100 duration = 0 END // catches 206, 290s
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 power = 4 timing = 1 resist_dispel = 1
      parameter1 = 14674 savingthrow = BIT1 END // add blinded string
    BUT_ONLY
  
  // add portrait icons, string bypasses MR
  COPY_EXISTING ~sppr421.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 2 power = 4 resist_dispel = 1 duration = 7
      parameter2 = 44 probability1 = 30 probability2 = 6 savingthrow = BIT1 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 2 power = 4 resist_dispel = 1 duration = 14
      parameter2 = 14 probability1 = 5 savingthrow = BIT1 END
    BUT_ONLY
  
  // should be restricted from neutrals, per descript
  COPY_EXISTING ~sppr513.spl~ ~override~
    WRITE_BYTE 0x1e THIS | BIT5 // add neutral flag
    BUT_ONLY
  
  // sound bypassing MR
  COPY_EXISTING ~sppr515.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 174 resist_dispel = 1 END
    BUT_ONLY
  
  // self visual blocked by spell protections
  COPY_EXISTING ~sppr516.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 233 power = 0 END
    BUT_ONLY
  
  // min levels set for rage and hp bonus; stop visuals and other stuff on non-allies
  COPY_EXISTING ~sppr517.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR dicesize = 0 END
    LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 279 opcode = 206 parameter2 = 50 timing = 0 duration = 0 savingthrow = 0 STR_VAR insert = first resource = EVAL ~%SOURCE_RES%~ END
    BUT_ONLY

  // animal rage shouldn't persist through a rest - tried with both 254 and 276, both crash the game when resting
  //COPY_EXISTING ~sppr517.spl~ ~override~ // Animal Rage
  //  LPF ADD_SPELL_EFFECT INT_VAR opcode = 254 target = 2 duration = 105 timing = 4 STR_VAR resource = sppr517 END
  //  BUT_ONLY

  // self-visuals and movement speed shouldn't be blocked
  COPY_EXISTING ~sppr610.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_target = 1 power = 0 END
    BUT_ONLY
  
  // string bypassing MR
  COPY_EXISTING ~sppr611.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
    BUT_ONLY
  
  // immunity should bypass MR, power
  COPY_EXISTING ~sppr612.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 290 resist_dispel = 2 power = 0 END
    BUT_ONLY

  COPY_EXISTING ~sppr613.spl~  ~override~ // whirlwind
    LPF ALTER_EFFECT INT_VAR resist_dispel = 1 match_savingthrow = BIT1 END // stun et al
    BUT_ONLY
  
  // sound bypassing MR
  COPY_EXISTING ~sppr717.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 174 resist_dispel = 1 END
    BUT_ONLY

  // had two extraneous sleep effects
  COPY_EXISTING ~sppr719.spl~ ~override~
    LPF DELETE_EFFECT INT_VAR check_globals = 0 match_opcode = 285 match_duration = 1 END
    BUT_ONLY
  
  // add 'healed' string
  COPY_EXISTING ~sppr720.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 power = 7 parameter1 = 14022 timing = 1 resist_dispel = 3 END
    BUT_ONLY
  
  // summons should always bypass spell protections
  COPY_EXISTING ~sppr723.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 259 power = 0 END
    BUT_ONLY

  // luck fixes, 4/4 (see spwi209.spl (x2), chance.itm, and lucky.itm)
  COPY_EXISTING ~spwi209.spl~ ~override~
    LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 73 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 54 opcode = 133 END
    PATCH_FOR_EACH op IN 59 90 91 92 BEGIN
      LPF ALTER_EFFECT INT_VAR match_opcode = op parameter1 = 5 END
    END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 power = 2 resist_dispel = 3 duration = 21 STR_VAR resource = chance END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 power = 2 resist_dispel = 3 duration = 21 STR_VAR resource = spwi209 END

  // agannazar's scorcher should allow evasion
  COPY_EXISTING ~spwi217.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 206 target = 2 parameter2 = 63 STR_VAR resource = spwi217 END
    BUT_ONLY

  // caster pause shouldn't be blocked by spell protections, deafness string bypassing save
  COPY_EXISTING ~spwi423.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 165 power = 0 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 savingthrow = BIT0 END
    BUT_ONLY

  // summons should always bypass spell protections, cosmetics on wrong target
  COPY_EXISTING ~spwi517.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR power = 0 target = 1 END
    BUT_ONLY

  // caster pause shouldn't be blocked by spell protections
  COPY_EXISTING ~spwi806.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 165 power = 0 END
    BUT_ONLY
  
  // sound should be self-targeted
  COPY_EXISTING ~spwi807.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 174 target = 1 END
    BUT_ONLY
  
  // add portrait icon for blindness
  COPY_EXISTING ~spwi808.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 2 power = 8 parameter2 = 8 resist_dispel = 1 duration = 70 END
    BUT_ONLY
  
  ACTION_IF game_is_totlm THEN BEGIN
  
    // add harpy wail to song of kaudies' protection list
    COPY_EXISTING ~spin146.spl~ ~override~ 
      LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spin134 resource = spin166 END
      BUT_ONLY

    // seems unused, adding portrait icon anyway
    COPY_EXISTING ~spin164.spl~ ~override~
      LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 60 opcode = 142 parameter1 = 0 parameter2 = 60 END // clone miscast into miscast icon
      BUT_ONLY
  
    // string bypassing MR
    COPY_EXISTING ~spin166.spl~ ~override~
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 resist_dispel = 1 END
      BUT_ONLY

    // beholder innates should match power, resistance of underlying spells
    COPY_EXISTING ~spin168.spl~ ~override~ // Charm Person (beholder version)
                  ~spin169.spl~ ~override~ // Sleep (beholder version)
      LPF ALTER_EFFECT INT_VAR power = 1 END
      BUT_ONLY
      
    // beholder sleep should be wake-on-damge 
    COPY_EXISTING ~spin169.spl~ ~override~ // Sleep (Beholder)
      LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 39 match_parameter2 = 1 parameter2 = 0 END
      BUT_ONLY

    // beholder innates should match power, resistance of underlying spells
    COPY_EXISTING ~spin170.spl~ ~override~ // Flesh To Stone (beholder version)
      LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = "-1" power = 6 resist_dispel = 1 END
      BUT_ONLY

    // beholder innates should match power, resistance of underlying spells; no save for visual
    COPY_EXISTING ~spin171.spl~ ~override~ // Disintegrate (beholder version)
      LPF ALTER_EFFECT INT_VAR match_opcode = 233 savingthrow = BIT0 savebonus = "-3" END
      LPF ALTER_EFFECT INT_VAR power = 6 END
      LPF ALTER_SPELL_HEADER INT_VAR location = 4 END // location
      BUT_ONLY
      
    // beholder innates should match power, resistance of underlying spells
    COPY_EXISTING ~spin172.spl~ ~override~ // Fear (beholder version)
      LPF ALTER_EFFECT INT_VAR power = 2 END
      BUT_ONLY

    // beholder innates should match power, resistance of underlying spells
    COPY_EXISTING ~spin173.spl~ ~override~ // Slow (beholder version)
      LPF ALTER_EFFECT INT_VAR power = 3 END
      BUT_ONLY

    // beholder innates should match power, resistance of underlying spells
    COPY_EXISTING ~spin174.spl~ ~override~ // Cause Serious Wounds (beholder version)
      LPF ALTER_EFFECT INT_VAR power = 4 END
      BUT_ONLY

    // beholder innates should match power, resistance of underlying spells; kill bypassing mr
    COPY_EXISTING ~spin175.spl~ ~override~ // Death Ray (beholder version)
      LPF ALTER_EFFECT INT_VAR resist_dispel = 1 power = 7 END
      BUT_ONLY
      
  END

END

/////                                                  \\\\\
///// spell stacking                                   \\\\\
/////                                                  \\\\\

COPY_EXISTING ~spwi212.spl~ ~override~ // Mirror Image (mage version)
  LPF DELETE_EFFECT INT_VAR match_opcode = 261 END // delete the effect blocking more mirror images
  BUT_ONLY

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_spell_stacking BEGIN // spells w/o an existing 206
  sppr111 => 142 // curse
  sppr201 => 142 // aid
  sppr202 => 142 // barkskin
  sppr210 => 142 // resist fire/cold
  sppr216 =>   0 // alicorn lance
  sppr306 => 142 // protection from fire
  sppr310 => 142 // miscast magic
  sppr319 => 266 // circle of bones
  sppr323 =>  28 // storm shell
  sppr407 => 142 // protection from lightning
  sppr412 => 264 // static charge
  sppr417 => 142 // cloud of pestilence
  sppr512 => 126 // spike stones
  sppr517 => 142 // animal rage
  sppr519 => 142 // magic resistance
  sppr609 =>   0 // entropy shield
  sppr610 => 266 // blade barrier
  sppr714 => 142 // symbol pain
  spwi212 => 119 // mirror image
  spwi223 => 142 // cat's grace
  spwi412 => 142 // greater malison
  spwi417 => 247 // beltyn's burning blood
  spwi419 => 142 // emotion courage
  spwi421 => 142 // emotion hope
  spwi511 => 278 // shroud of flame
  spwi620 => 142 // trollish fortitude
  spwi711 =>  54 // suffocate
  spwi807 => 142 // iron body
  // spells with existing 206 blocks
  sppr406 => 142 // defensive harmony
//sppr415 => 142 // blood rage - keep 206 so that bad effects will apply
  spwi107 => 142 // friends
  spwi114 => 142 // shield
  spwi201 =>  65 // blur
  spwi214 => 142 // strength
  // patched here, get additional touchups below
  spwi209 => 142 // luck
  spin173 => 142 // beholder slow
  spwi312 => 142 // slow
  spin121 => 142 // protection from evil
  sppr107 => 142 // protection from evil
  sppr408 => 142 // protection from evil 10'
  spwi113 => 142 // protection from evil
END 

ACTION_PHP_EACH cd_spell_stacking AS spell => op BEGIN

  COPY_EXISTING ~%spell%.spl~ ~override~ // suffocate - not in main batch because no op 142
    LPF cd_spell_stacking INT_VAR cloneop = op END
    BUT_ONLY IF_EXISTS  
    
END
  
// iwd: no chance, no 206; how: chance exists, but no 206; totlm: has 2x 206 vs. self and chance
ACTION_IF !game_is_iwd BEGIN

  COPY_EXISTING ~spwi209.spl~ ~override~ // luck
    LPF CLONE_EFFECT INT_VAR match_opcode = 254 STR_VAR insert = below match_resource = spwi209 resource = chance END // dispel chance, too
    PATCH_IF !game_is_totlm BEGIN
      LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 142 opcode = 206 parameter2 = 0 STR_VAR insert = last resource = chance END // add 206 vs. chance
    END  
    BUT_ONLY

END
 
ACTION_IF game_is_totlm BEGIN

  ACTION_FOR_EACH spell IN spin173 spwi312 BEGIN // slow cross-dispelling
   
     COPY_EXISTING ~%spell%.spl~ ~override~
       PATCH_FOR_EACH spell2 IN spin173 spwi312 BEGIN
         PATCH_IF ("%spell%" STRING_COMPARE_CASE "%spell2%") BEGIN // dont' add another self-dispel
           LPF CLONE_EFFECT INT_VAR match_opcode = 254 STR_VAR insert = below match_resource = EVAL ~%spell%~ resource = EVAL ~%spell2%~ END
         END
       END
       BUT_ONLY   

  END   
    
END
 
ACTION_FOR_EACH spell IN spin121 sppr107 sppr408 spwi113 BEGIN // pfe cross-dispelling
 
  COPY_EXISTING ~%spell%.spl~ ~override~
    PATCH_FOR_EACH spell2 IN spin121 sppr107 sppr408 spwi113 BEGIN
      PATCH_IF ("%spell%" STRING_COMPARE_CASE "%spell2%") BEGIN // dont' add another self-dispel
        LPF CLONE_EFFECT INT_VAR match_opcode = 254 STR_VAR insert = below match_resource = EVAL ~%spell%~ resource = EVAL ~%spell2%~ END
      END
    END
    BUT_ONLY   

END   

/////                                                  \\\\\
///// store fixes                                      \\\\\
/////                                                  \\\\\

COPY_EXISTING ~de_lib.sto~ ~override~
  WRITE_LONG 0xC 2865 // add store name

// infinite books of fateful coin
COPY_EXISTING ~ehinn.sto~  ~override~
              ~kuinn1.sto~ ~override~
              ~kuinn2.sto~ ~override~
              ~kuinn3.sto~ ~override~
  ADD_STORE_ITEM + ~book40~ #0 #0 #0 ~IDENTIFIED~ #1
  BUT_ONLY

COPY_EXISTING ~ehtemple.sto~ ~override~
  ADD_STORE_ITEM + ~adisease~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~ // identify
  BUT_ONLY

// book45 references events 80 years in the future
COPY_EXISTING ~kugerth.sto~ ~override~
  LPF DELETE_STORE_ITEM STR_VAR item_to_delete = book45 END
  BUT_ONLY

// conlan has separate instances of composite long bows for sale
COPY_EXISTING ~kusmith.sto~ ~override~
  REMOVE_STORE_ITEM ~bow01~ // remove both instances
  ADD_STORE_ITEM ~bow01~ AFTER ~xbow01~ #0 #0 #0 ~NONE~ #1 ~UNLIMITED~ // re-add one back
  BUT_ONLY

ACTION_IF !game_is_iwd THEN BEGIN

  COPY_EXISTING ~edion.sto~ ~override~
    ADD_STORE_ITEM + ~cone~ #1 #0 #0 ~IDENTIFIED~ #2 // overcharged
    BUT_ONLY

  // infinite lucky scimitars
  COPY_EXISTING ~kusmith.sto~ ~override~
    ADD_STORE_ITEM + ~lucky~ #1 #1 #1 ~IDENTIFIED~ #1
    BUT_ONLY

  // inconsequence not working due to bad filename; see zz44in.itm
  COPY_EXISTING ~waukeen.sto~ ~override~ // the golden lodge
    LPF REPLACE_STORE_ITEM INT_VAR number_in_stock = 1 STR_VAR old_item = zz14in new_item = zz44in flags = IDENTIFIED END // keeps identified flags, 1 in stock 
    BUT_ONLY

  ACTION_IF game_is_totlm THEN BEGIN

    // now has no mummy tea in this store
    COPY_EXISTING ~kugerth.sto~ ~override~
      ADD_STORE_ITEM + ~adisease~ #1 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
      BUT_ONLY
  
  END

END

/////                                                  \\\\\
///// large batch fixes                                \\\\\
/////                                                  \\\\\

INCLUDE ~iwdfixpack/lib/iwdfp_effect_batches.tpa~
INCLUDE ~iwdfixpack/lib/batch_patches.tpa~

/////                                                  \\\\\
///// bulk probability fixes                           \\\\\
/////                                                  \\\\\

COPY_EXISTING //~amaunat.itm~  ~override~ // amaunator's legacy: 5% beltyn's burning blood, fixed below
              ~axemino.itm~  ~override~ // axe of the minotaur lord: 25% stun 
//            ~behwep.itm~   ~override~ // belhifet's weapon, fixed elsewhere
//            ~bess.itm~     ~override~ // the love of black bess: 10% bad luck; fixed individually above
              ~biteme.itm~   ~override~ // spear of kerish: 5% cold damage
              ~blast.itm~    ~override~ // bracers of blasting: 10% chance to explode
              ~bloodgf.itm~  ~override~ // blood of the gloomfrost: 5% chance to heal
              ~cwreve.itm~   ~override~ // revenant: 50% stun
              ~cwszomb.itm~  ~override~ // slime zombie: 10% poison
              ~cynicis.itm~  ~override~ // cynicism: display stringheads
              ~days.itm~     ~override~ // the sword of days: 25% slow
              ~decasta.itm~  ~override~ // decastave: 50% chance to drain 1 or 2 hp
              ~diver.itm~    ~override~ // spell diver: 50% to block spellcasting
//            ~firekis.itm~  ~override~ // fire kiss: 5% shroud of flame - fixed in items casting spells, below
              ~fistgf.itm~   ~override~ // fist of the gloomfrost: 5% chance for extra crush damage 
              ~fkiller.itm~  ~override~ // faith killer: 5% dispel magic
              ~flailsk.itm~  ~override~ // skullflail: 10% piercing damage
//            ~gasp.itm~     ~override~ // stalker weapon: seems correct already
//            ~ghoul1.itm~   ~override~ // ghoul touch, fixed elsewhere 
              ~giving.itm~   ~override~ // the giving star: 10% cure and str bonus
              ~gvalor2.itm~  ~override~ // gauntlet of valor: 12% extra damage and stun
              ~handgf.itm~   ~override~ // hand of the gloomfrost: 5% suffocate
              ~hlydeck.itm~  ~override~ // holy chaos! deck: 50% bless, 25% prayer, 15% recitation, 10% RWotF
              ~holding.itm~  ~override~ // jester's bag of holding: all inventory creation effects
              ~icegolem.itm~ ~override~ // ice golem weapon: 10% stun
              ~kinetic.itm~  ~override~ // kinetic spear: 5% lance of disruption
              ~kissgf.itm~   ~override~ // kiss of the gloomfrost: 5% cold damage
//            ~kris.itm~     ~override~ // bone kris of black ichor: 20% poison; fixed already in individual patch
              ~lizspear.itm~ ~override~ // no such index: 10% extra piercing damage 
              ~mae.itm~      ~override~ // flail of mae: 5% petrification 
              ~pest.itm~     ~override~ // pestilent dawn: 25% cause disease
//            ~poq2-16.itm~  ~override~ // no such index: stun chance
              ~reaver.itm~   ~override~ // the snow maiden's reaver: 2% freezing target
              ~remorha.itm~  ~override~ // remorhaz weapon: 5% fire damage
              ~sbowebu.itm~  ~override~ // short bow of ebullience: 25% fire damage
              ~serrate.itm~  ~override~ // serrated bone blade: 20% cold damage
              ~sflail.itm~   ~override~ // conrnugon: 50% bleed, 25% stun 
              ~shax4d4c.itm~ ~override~ // shadowed orc weapon: 40% cold damage
              ~shaxe2c.itm~  ~override~ // shadowed orc weapon: 40% cold damage
              ~slimed2.itm~  ~override~ // kill targets in increments of 20%
              ~spin126.spl~  ~override~ // eye of the mind: elf, half-elf immunity
//            ~spin134.spl~  ~override~ // mournful wail: percntages already OK
              ~spin146.spl~  ~override~ // the song of kaudies - resistant to sound attacks: 50% immunity to some spells
              ~spin147.spl~  ~override~ // the siren's yearning - enthralls creatures: elf, half-elf immunity
              ~spin166.spl~  ~override~ // harpy wail: elf, half-elf immunity
              ~spin168.spl~  ~override~ // charm person: elf, half-elf immunity
//            ~spin990.spl~  ~override~ // INNATE_MYCONID_SPORES: percntages already OK
              ~spin992.spl~  ~override~ // INNATE_ZOMBIE_LORD_AURA: 20% each kill, sleep, stats drain, ac/save penalty, disease
              ~spin993.spl~  ~override~ // INNATE_BOMBARDIER_BEETLE_CLOUD: 20% stun, 20% deaf
              ~sppr315.spl~  ~override~ // cause disease: 5-20 strength drain
              ~sppr321.spl~  ~override~ // cloudburst: 50% lightning strike
              ~sppr421.spl~  ~override~ // smashing wave: 5% sleep, 25% stun
              ~sppr507.spl~  ~override~ // champion's strength: 25% each to set strength to 20-23
              ~sppr611.spl~  ~override~ // harm: reduces HP to 1d4
              ~spwi104.spl~  ~override~ // charm person: elf, half-elf immunity
              ~spwi119.spl~  ~override~ // larloch's minor drain: 1d4 hp drain
              ~spwi316.spl~  ~override~ // dire charm: elf, half-elf immunity
              ~spwi518.spl~  ~override~ // lower resistance: 50% chance for MR to resist
              ~spwi706.spl~  ~override~ // prismatic spray: 1/6 chances for effects
              ~sw1h99.itm~   ~override~ // sword of chaos: slay lawful 10%
//            ~talongf.itm~  ~override~ // talon of the gloomfrost: 15% entangle; fixed in items casting spells
              ~tonggf.itm~   ~override~ // tongue of the gloomfrost: 3% cone of cold 
              ~tongue.itm~   ~override~ // the salamander's tongue: 20% fire damage
              ~u1ham3a.itm~  ~override~ // corrosive hammer: 30% acid damage
              ~u1ham3b.itm~  ~override~ // war hammer of sparks +2: 50% electrical damage, 10% stun 
              ~u1ham4a.itm~  ~override~ // war hammer +3: life giver: 20% heal
              ~u1ham5a.itm~  ~override~ // war hammer of phasing +3: 15% phase, 25% cold damage
              ~u1hax2a.itm~  ~override~ // charged battle axe +2: 50% electrical damage, 15% stun
              ~u1hax3a.itm~  ~override~ // poisonous battle axe +2: 25% poison
              ~u1hax4a.itm~  ~override~ // battle axe +3: fatigue: 20% slow 
              ~u1hax4b.itm~  ~override~ // benorg's truth: 10% stun
              ~u2ham4b.itm~  ~override~ // war hammer of phasing +2: 10% disease, 15% cold damage
              ~u2ham5a.itm~  ~override~ // demon's breath: 20% fireball, 50% fire damage
              ~u2hax2a.itm~  ~override~ // two handed fire axe +1: 50% fire damage
              ~u2hax3a.itm~  ~override~ // infected two handed axe +2: 15% disease
              ~u2hax3b.itm~  ~override~ // two handed axe +2: life giver: 10% heal
              ~u2hax4a.itm~  ~override~ // 2h axe of greater phasing +2: 15% phased, 25% cold damage
              ~u2hax5a.itm~  ~override~ // foe's fate: 25% berserk, 25% electric damage
              ~uarow3a.itm~  ~override~ // confusion arrows +3: 10% confusion
              ~uarow3b.itm~  ~override~ // hammer arrow +1: 10% stun
              ~ubswd1a.itm~  ~override~ // fang: 25% acid damage
              ~ubswd2a.itm~  ~override~ // phasing bastard sword +1: 15% phased
              ~ubswd2b.itm~  ~override~ // flaming bastard sword +1: 25% fire damage
              ~ubswd3a.itm~  ~override~ // bastard sword +2: life giver: 5% heal
              ~ubswd4b.itm~  ~override~ // bastard sword of greater phasing: 15% phased 
              ~ubswd4c.itm~  ~override~ // bastard sword +2: conflagration: 10% fireball
              ~ubswd5a.itm~  ~override~ // cancerous bastard sword +4: 15% disease 
              ~ubswd5b.itm~  ~override~ // bastard sword +3: incinerator: 50% fire damage, 10% stun
              ~ubull4a.itm~  ~override~ // bullets of fire +2: 50% fire damage
              ~udagg1a.itm~  ~override~ // fire dagger: 15% fire damage
              ~udagg1b.itm~  ~override~ // phase dagger: 15% phased
              ~udagg2a.itm~  ~override~ // static dagger +1: 50% electric damage
              ~udagg3a.itm~  ~override~ // fire dagger +2: 50% fire damage
              ~udagg3c.itm~  ~override~ // life dagger +2: 15% heal
              ~udagg4a.itm~  ~override~ // chaos dagger +3: 20% disease
              ~udart1a.itm~  ~override~ // hammer darts: 15% stun
              ~udart2a.itm~  ~override~ // berserker darts: 10% berserk
//            ~udart3b.itm~  ~override~ // blinding darts +2: 25% blind, fixed elsewhere
              ~udart5a.itm~  ~override~ // inferno darts +4: 20% fire damage
              ~uflal2a.itm~  ~override~ // crooked flail: 50% pierce damage
              ~uflal2b.itm~  ~override~ // hammer flail +2: 15% stun
              ~uflal4a.itm~  ~override~ // fire flail +3: 50% fire damage
              ~uflal5a.itm~  ~override~ // shocking flail +4: 50% electric damage, 10% stun 
              ~uhalb1a.itm~  ~override~ // halberd of sparks +1: 15% electric damage
              ~uhalb2a.itm~  ~override~ // diseased halberd +1: 10% disease
              ~uhalb3b.itm~  ~override~ // darig's rest: 10% sleep
              ~uhalb3c.itm~  ~override~ // life halberd +2: 10% heal
              ~uhalb4a.itm~  ~override~ // doom halberd +3: 50% fire damage, 15% stun
              ~uhalb4b.itm~  ~override~ // star forged halberd +3: 15% stun
              ~uhalb5a.itm~  ~override~ // great halberd +4: 50% acid damage
              ~ulswd2b.itm~  ~override~ // spiked long sword +1: 25% pierce damage
              ~ulswd3b.itm~  ~override~ // long sword of confusion +2: 25% confusion
              ~ulswd3c.itm~  ~override~ // life's gift: 15% heal
              ~ulswd4a.itm~  ~override~ // hold fast: 15% hold
              ~ulswd5b.itm~  ~override~ // bhaal's fire: 15% stun
              ~umstr2a.itm~  ~override~ // morning star of confusion +1: 10% confusion
              ~umstr2b.itm~  ~override~ // lesser static star +1: 20% electric damage, 10% stun
              ~umstr3a.itm~  ~override~ // morning star +2: hammer: 20% stun
              ~umstr3c.itm~  ~override~ // morning star of lesser phasing: 15% phase, 25% cold
              ~umstr4b.itm~  ~override~ // static star +3: 15% slow, 25% electric damage
              ~umstr5a.itm~  ~override~ // morning star of action +4: 5% stun
              ~usswd2a.itm~  ~override~ // flaming short sword +1: 50% fire damage
              ~usswd2b.itm~  ~override~ // short sword of lesser phasing: 10% phased
              ~usswd3a.itm~  ~override~ // sloth: 10% slow
              ~usswd3b.itm~  ~override~ // some god's lesser promise: 5% heal
              ~usswd5a.itm~  ~override~ // short sword of health +4: 15% heal
              ~usswd5b.itm~  ~override~ // short sword +4: hammer: 20% stun 
              ~utswd2b.itm~  ~override~ // two handed sword +1: hammering: 20% stun
              ~utswd3a.itm~  ~override~ // two handed sword +2: hammering: 20% stun
              ~utswd4a.itm~  ~override~ // two handed sword +4: backbiter: 25% user takes piercing
              ~utswd4b.itm~  ~override~ // two handed sword +3: bane: 25% disease
//            ~utswd5a.itm~  ~override~ // static two handed sword +4: 50% electric damage, 25% stun, fixed elsewhere
              ~utswd5b.itm~  ~override~ // two handed sword +4: life giver: 5% heal
//            ~wepwyv.itm~   ~override~ // wyvern attack: 50% poison, already correct
              ~whtash.itm~   ~override~ // the spear of white ash: 5% extra damage
              ~withery.itm~  ~override~ // ol' withery: 2% finger of death
//            ~wranged.itm~  ~override~ // no such index: existing splits are fine
//            ~xclub.itm~    ~override~ // dazer: 5% stun, fixed elsewhere
              ~young.itm~    ~override~ // young rage: 5% berserk
//            ~zz05we.itm~   ~override~ // the mace of weal and woe: 13% curse - fixed in items casting spells, below
              ~zze6pe.itm~   ~override~ // pig's eye: 50% blinds orcs
              ~zzj6sp.itm~   ~override~ // selune's promise: 2% morale failure
              ~zzm5mh.itm~   ~override~ // misery's herald: 10% horror
  READ_ASCII 0x00 type (3)
  SET min_size = 0
  PATCH_IF ("%type%" STRING_COMPARE_CASE "spl" = 0) BEGIN
    READ_LONG  0x64 abil_off ELSE 0
    READ_SHORT 0x68 abil_num ELSE 0
    READ_LONG  0x6a fx_off   ELSE 0
    SET counter_offset = 0x70
    SET abil_length    = 0x28
    SET global_loop    = 0
    SET fx_type        = 0
    SET min_size       = 0x72
  END ELSE
  PATCH_IF ("%type%" STRING_COMPARE_CASE "itm" = 0) BEGIN
    READ_LONG  0x64 abil_off ELSE 0
    READ_SHORT 0x68 abil_num ELSE 0
    READ_LONG  0x6a fx_off   ELSE 0
    SET counter_offset = 0x70
    SET abil_length    = 0x38
    SET global_loop    = 1
    SET fx_type        = 0
    SET min_size       = 0x72
/*  END ELSE
  PATCH_IF ("%type%" STRING_COMPARE_CASE "cre" = 0) BEGIN
    READ_LONG  0x2c4 fx_off ELSE 0
    READ_BYTE 0x33 fx_type ELSE 2
    SET abil_off       = 0
    SET abil_num       = 0
    SET counter_offset = 0x2c8
    SET abil_length    = 0
    SET global_loop    = 1
    SET min_size       = 0x2d4
    SET cosmetic       = 0 */
  END
  PATCH_IF ((SOURCE_SIZE >= min_size) AND (min_size != 0)) BEGIN // min_size must get set by file type detection
    FOR (index = (0 - global_loop) ; index < abil_num ; ++index) BEGIN
      PATCH_IF (index < 0) BEGIN // if loop through globals needed
        SET abil_fx_idx = 0
      END ELSE BEGIN // otherwise normal ability
        SET counter_offset = (abil_off + 0x1e + (abil_length * index))
        READ_SHORT  (abil_off + 0x20 + (abil_length * index)) abil_fx_idx
      END
      READ_SHORT counter_offset counter // fx_num on global loop, otherwise abil_fx_num
      // run one pass purely looking for keys
      FOR (index2 = 0 ; index2 < counter ; ++index2) BEGIN
        PATCH_FOR_EACH offset IN 0x12 0x13 BEGIN
          READ_BYTE  (fx_off + offset + (offset * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) prob
          PATCH_IF (prob > 0) AND (prob < 100) BEGIN
            WRITE_BYTE  (fx_off + offset + (offset * fx_type) + ((abil_fx_idx + index2) * (0x30 + (0xd8 * fx_type)))) (prob - 1)
          END
        END
      END
    END
  END
  BUT_ONLY IF_EXISTS

/////                                                  \\\\\
///// items casting spells                             \\\\\
/////                                                  \\\\\

/* 
origially I was planning on making all items that cast X on hit or cast X directly to use 146/148 opcodes 
so that the items would always use the spells fully fixed and I could avoid duplicating work. However, in oIWD,
these cause the source to go through a casting animation, e.g. the character throwing force bullets would 
actually go through a casting animation when it caused Otiluke's on the target. So instead we run them through
this macro *at the end* so that the fixed effects are copied over.

First we delete effects from item, then run the macro.  
*/
  
COPY_EXISTING ~amul15.itm~  ~override~ // shield amulet cn cast shield
              ~firekis.itm~ ~override~ // Fire Kiss +3 casting shroud of flame on-hit
              ~force.itm~   ~override~ // force bullets causing ORS on-hit
              ~wandcor.itm~ ~override~ // wand of corrosion casts death fog or acid storm
              ~wandfre.itm~ ~override~ // wand of freezing death
  LPF DELETE_EFFECT INT_VAR check_globals = 0 END // delete all effects
  BUT_ONLY IF_EXISTS //force is a how item

COPY_EXISTING ~gauntid.itm~ ~override~ // Gauntlets of Infernal Damnation casting shroud of flame on-equip
              ~shldrng.itm~ ~override~ // shield ring has shield spell active while equipped
  LPF DELETE_EFFECT INT_VAR check_headers = 0 END // delete all equipping effects
  BUT_ONLY
  
COPY_EXISTING ~zz05we.itm~  ~override~ // The Mace of Weal and Woe +1 casting curse on-hit
  PATCH_FOR_EACH op IN 54 9 23 174 233 142 BEGIN // delete leftover curse stuff
    LPF DELETE_EFFECT INT_VAR check_globals = 0 match_opcode = op END // delete all effects
  END  
  BUT_ONLY

LAF cd_items_casting_spells INT_VAR cast_at_level =  1 item_header = 1                               duration = 300 STR_VAR item = amul15  spell = spwi114 END
LAF cd_items_casting_spells INT_VAR cast_at_level =  1 item_header = 0                               duration =   0 STR_VAR item = shldrng spell = spwi114 END
LAF cd_items_casting_spells INT_VAR cast_at_level =  1 item_header = 1              probability =  4 duration =   7 STR_VAR item = firekis spell = spwi511 END 
LAF cd_items_casting_spells INT_VAR cast_at_level =  1 item_header = 0                               duration =   7 STR_VAR item = gauntid spell = spwi511 END 
LAF cd_items_casting_spells INT_VAR cast_at_level =  1 item_header = 1 set_icon = 1                                 STR_VAR item = wandcor spell = spwi605 END // death fog
LAF cd_items_casting_spells INT_VAR cast_at_level = 15 item_header = 2 set_icon = 1                                 STR_VAR item = wandcor spell = spwi708 END // acid storm
LAF cd_items_casting_spells INT_VAR cast_at_level =  1 item_header = 1 set_icon = 1                                 STR_VAR item = wandfre spell = spwi318 END // icelance, 21 duration
LAF cd_items_casting_spells INT_VAR cast_at_level =  4 item_header = 2 set_icon = 1                                 STR_VAR item = wandfre spell = spwi318 END // snowball swarm, 21 duration
LAF cd_items_casting_spells INT_VAR cast_at_level =  1 item_header = 3 set_icon = 1                                 STR_VAR item = wandfre spell = spwi318 END // ice storm, 21 duration
LAF cd_items_casting_spells INT_VAR cast_at_level =  1 item_header = 1              probability = 12                STR_VAR item = zz05we  spell = sppr111 END 

COPY_EXISTING ~gauntid.itm~ ~override~
  LPF ALTER_EFFECT INT_VAR check_headers = 0 power = 0 END // remove power checks from cursed effects
  BUT_ONLY
  
COPY_EXISTING ~shldrng.itm~ ~override~ // make these equipping effects, not time-limited
  LPF ALTER_EFFECT INT_VAR timing = 2 END
  BUT_ONLY

ACTION_IF !game_is_iwd BEGIN

  COPY_EXISTING ~amaunat.itm~ ~override~ // amaunator's legacy
    LPF DELETE_EFFECT INT_VAR match_opcode = 247 END // delete existing beltyn
    LPF DELETE_EFFECT INT_VAR match_opcode = 174 END
    LPF DELETE_EFFECT INT_VAR match_opcode = 233 END
    BUT_ONLY  
    
  COPY_EXISTING ~handgf.itm~ ~override~ //  hand of the gloomfrost
    LPF DELETE_EFFECT INT_VAR check_globals = 0 header_type = 1 END // delete all melee effects
    LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 233 target = 2 power = 7 parameter2 = 93 timing = 1 probability1 = 4 savingthrow = BIT0 END // re-add visual
    BUT_ONLY  
    
  COPY_EXISTING ~talongf.itm~ ~override~ // talon of the gloomfrost
    LPF DELETE_EFFECT INT_VAR check_globals = 0 header_type = 1 END // delete all melee effects
    BUT_ONLY

  COPY_EXISTING ~tonggf.itm~ ~override~ // tongue of the gloomfrost
    LPF DELETE_EFFECT INT_VAR match_opcode = 12 END // delete all CoC effects
    BUT_ONLY

  LAF cd_items_casting_spells INT_VAR cast_at_level =  1 item_header = 1 probability =  4 STR_VAR item = amaunat spell = spwi417 END
  LAF cd_items_casting_spells INT_VAR cast_at_level =  1 item_header = 1                  STR_VAR item = force   spell = spwi413 END
  LAF cd_items_casting_spells INT_VAR cast_at_level = 10 item_header = 1 probability =  4 STR_VAR item = handgf  spell = spwi711 END
  LAF cd_items_casting_spells INT_VAR cast_at_level =  1 item_header = 1 probability = 14 STR_VAR item = talongf spell = sppr105 END
  LAF cd_items_casting_spells INT_VAR cast_at_level = 10 item_header = 1 probability =  2 STR_VAR item = tonggf  spell = spwi504 END
  
END

/////                                                  \\\\\
///// magical weapons for tougher creatures            \\\\\
/////                                                  \\\\\

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_creature_weapons BEGIN

// creature, original weapon, enchantment => new weapon
  as2cave,  s1-10,   2 => s1-10m2
  as2dire,  p1-8,    1 => p1-8m1
  as3polr,  s1-12,   3 => bearpolr
  bbspir,   s2-16,   2 => s2-16m2
  bearpold, s1-12,   3 => bearpolr
  bearpolr, s1-12,   3 => bearpolr
  beetrhin, s2-16,   2 => s2-16m2
  beholder, p1-8,    1 => p1-8m1
  beholdh,  p1-8,    1 => p1-8m1
  bergclaw, s1-20,   3 => s1-20m3
  biknight, s1-12,   2 => s1-12m2
  bon8005,  b1-10,   2 => b1-10m2
  bon8013,  b1-10,   2 => b1-10m2
  bonegard, b1-10,   2 => b1-10m2
  ckinvst,  s1-12,   3 => invstlk
  crypthng, s1-8,    1 => s1-8m1
  cybeetr,  s2-16,   2 => s2-16m2
  cyearthe, b4-32,   4 => eelem
  cyshamb,  b2-16,   2 => b2-16m2
  cywatere, b4-32,   4 => welem
  direb,    s1-20,   3 => s1-20m3
  dlbnegrd, b1-10,   2 => b1-10m2
  dlbonec,  s1-10,   2 => s1-10m2
  dlcldhnd, b1-8,    1 => b1-8m1
  dlfznbne, b1-10,   2 => b1-10m2
  dlgolem,  b4-32,   4 => b4-32m4
  dlicehnd, b1-8,    1 => b1-8m1
  dlicetr,  s1-12,   2 => s1-12m2
  dlscrag,  s2-16,   2 => s2-16m2
  dlsnotr,  s1-10,   2 => s1-10m2
  drowned,  s1-8,    1 => s1-8m1
  eele8013, b4-32,   4 => eelem
  elemeart, b4-32,   4 => eelem
  elemwat,  b4-32,   4 => welem
  es12erth, b4-32,   4 => eelem
  es12watr, b4-32,   4 => welem
  es16erth, b4-32,   4 => eelem
  es16watr, b4-32,   4 => welem
  es20erth, b4-32,   4 => eelem
  es20watr, b4-32,   4 => welem
  es24erth, b4-32,   4 => eelem
  es24watr, b4-32,   4 => welem
  es8erth,  b4-32,   4 => eelem
  es8watr,  b4-32,   4 => welem
  esmerth,  b4-32,   4 => eelem
  ettin,    b2-16,   2 => b2-16m2
  frostbit, b1-8,    1 => b1-8m1
  frostbit, b2-16,   2 => b2-16m2
  glbcat,   s2-16,   4 => s2-16m4
  gorg,     b2-16,   2 => b2-16m2
  hobartf,  s1-10,   2 => s1-10m2
  hrpcat,   s1-12,   2 => s1-12m2
  hrpmat,   s2-16,   2 => s2-16m2
  icasa,    s2-16,   5 => icasa
  invistrl, b1-8,    1 => b1-8m1
  invstlk,  s1-12,   3 => invstlk
  jklgtr,   s1-12,   2 => s1-12m2
  jklldr,   s1-20,   3 => s1-20m3
  jklwar,   s1-10,   2 => s1-10m2
  kubear,   s1-10,   2 => s1-10m2
  lwight,   s1-4,    1 => s1-4m1
  ms5mino,  s1-8,    1 => s1-8m1
  ms7bgrd,  b1-10,   2 => b1-10m2
  ms7umbh,  s1-12,   3 => umberhlk
  pbspir,   s5-20,   3 => s5-20m3
  rakshasa, s1-10,   2 => s1-10m2
  rakshinv, s1-10,   2 => s1-10m2
  rbidrow,  s1-8,    1 => s1-8m1
  rikasha,  s1-12,   2 => s1-12m2
  rldumbh,  s1-12,   3 => umberhlk
  rndcryp,  s1-8,    1 => s1-8m1
  rndhrpc,  s1-12,   2 => s1-12m2
  rndstrol, b1-8,    1 => b1-8m1
  rndwwolf, p1-8,    1 => p1-8m1
  rwtgice,  b1-8,    1 => b1-8m1
  rwtwwolf, p1-8,    1 => p1-8m1
  saablic,  s1-12,   3 => umberhlk
  sagolem,  b2-16,   2 => b2-16m2
  saumber,  s1-12,   3 => umberhlk
  saumber2, s1-12,   3 => umberhlk
  sdogre,   s1-10,   2 => s1-10m2
  sevsoul,  s1-20,   3 => s1-20m3
  shadsoul, s1-10,   2 => s1-10m2
  shatsoul, s1-10,   2 => s1-10m2
  shmblr,   b2-16,   2 => b2-16m2
  skelblaz, s1-4,    1 => s1-4m1
  ss3umb8,  s1-12,   3 => umberhlk
  ss3umb9,  s1-12,   3 => umberhlk
  sumstlk,  s1-12,   3 => invstlk
  tanarri,  s2-16,   4 => yxun
  trollice, s1-8,    1 => s1-8m1
  trollsno, b1-8,    1 => b1-8m1
  udwight,  s1-4,    1 => s1-4m1
  uhlk8009, s1-12,   3 => umberhlk
  uhlk8010, s1-12,   3 => umberhlk
  umberhlk, s1-12,   3 => umberhlk
  vexing2,  s2-16,   4 => s2-16m4
  vexing3,  s2-16,   4 => s2-16m4
  vexing4,  s2-16,   4 => s2-16m4
  wight,    s1-4,    1 => s1-4m1
  wight2,   s1-4,    1 => s1-4m1
  wight3,   s1-4,    1 => s1-4m1
  winwolf,  p1-8,    1 => p1-8m1
  winwolfp, p1-8,    1 => p1-8m1
  wspir,    s2-16,   2 => s2-16m2
  wtettin,  b2-16,   2 => b2-16m2
  wticetr1, b1-8,    1 => b1-8m1
  wticetrl, b1-8,    1 => b1-8m1
  wtsnotro, b1-8,    1 => b1-8m1
  wylfdene, s2-16,   2 => s2-16m2
  wyrm,     b1-12,   2 => b1-12m2
END


ACTION_PHP_EACH cd_creature_weapons AS params => item BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%params_0%.cre~ BEGIN
  
    ACTION_IF !FILE_EXISTS_IN_GAME ~%item%.itm~ BEGIN
    
      COPY_EXISTING ~%params_1%.itm~ ~override/%item%.itm~
        WRITE_BYTE 0x18 THIS | BIT6 // add magical flag
        WRITE_LONG 0x60 params_2    // enchanment
             
    END 

    COPY_EXISTING ~%params_0%.cre~ ~override~
      READ_LONG 0x324 item_off
      READ_LONG 0x328 item_num
      FOR (index = 0 ; index < item_num ; ++index) BEGIN
        READ_ASCII (item_off + 0x00 + (index * 0x14)) itemcheck
        PATCH_IF ("%itemcheck%" STRING_COMPARE_CASE "%params_1%" = 0) BEGIN // if matched
          WRITE_ASCIIE (item_off + 0x00 + (index * 0x14)) "%item%" #8
        END
      END
      BUT_ONLY    

  END

END
  
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Game Text Update                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @1000 DESIGNATED 100
REQUIRE_PREDICATE GAME_IS ~iwd how totlm~ @113
REQUIRE_PREDICATE ((FILE_EXISTS ~iwdfixpack/languages/%LANGUAGE%/gtu.tra~) AND
                   (FILE_EXISTS ~iwdfixpack/languages/%LANGUAGE%/gtu.tpa~)) @1001
LABEL ~cd_iwd_fixpack_gtu~

// key
//
// @10xxxxx - string for all games
// @22xxxxx - string for iwd w/o how
// @33xxxxx - string for how w/o totlm
// @44xxxxx - string for how with or without totlm
// @55xxxxx - string for totlm

LOAD_TRA ~iwdfixpack/languages/%LANGUAGE%/gtu.tra~
INCLUDE  ~iwdfixpack/languages/%LANGUAGE%/gtu.tpa~
